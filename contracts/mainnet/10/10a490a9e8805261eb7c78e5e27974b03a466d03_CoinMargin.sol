// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.0;

import "../../helpers/Quantity.sol";

library CoinMargin {
    using Quantity for int256;

    function calculateNotional(
        uint256 _price,
        uint256 _quantity,
        uint256 _baseBasisPoint
    ) public pure returns (uint256) {
        if (_price != 0) {
            return _quantity * _baseBasisPoint / _price;
        }
        return 0;
    }

    function calculateEntryPrice(
        uint256 _notional,
        uint256 _quantity,
        uint256 _baseBasisPoint
    ) public pure returns (uint256) {
        if (_notional != 0) {
            return _quantity * _baseBasisPoint / _notional;
        }
        return 0;
    }

    function calculatePnl(
        int256 _quantity,
        uint256 _openNotional,
        uint256 _closeNotional
    ) public pure returns (int256) {
        // LONG position
        if (_quantity > 0) {
            return int256(_openNotional) - int256(_closeNotional);
        }
        // SHORT position
        return int256(_closeNotional) - int256(_openNotional);
    }

    function calculateLiquidationPip(
        int256 _quantity,
        uint256 _margin,
        uint256 _positionNotional,
        uint256 _maintenanceMargin,
        uint256 _basisPoint
    ) public pure returns (uint256) {
        // if position is long
        if (_quantity > 0) {
            return _quantity.abs() * _basisPoint / (_positionNotional + _margin - _maintenanceMargin);
        }
        // position is short
        if (_margin > _positionNotional + _maintenanceMargin) {
            return type(uint256).max;
        }
        return _quantity.abs() * _basisPoint / (_positionNotional + _maintenanceMargin - _margin);
    }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity >=0.8.0;

library Quantity {
    function getExchangedQuoteAssetAmount(
        int256 _quantity,
        uint256 _openNotional,
        uint256 _oldPQuantity
    ) internal pure returns (uint256) {
        return (abs(_quantity) * _openNotional) / _oldPQuantity;
    }

    function getPartiallyLiquidate(
        int256 _quantity,
        uint256 _liquidationPenaltyRatio
    ) internal pure returns (int256) {
        return (_quantity * int256(_liquidationPenaltyRatio)) / 100;
    }

    function isSameSide(int256 qA, int256 qB) internal pure returns (bool) {
        return qA * qB > 0;
    }

    function u8Side(int256 _quantity) internal pure returns (uint8) {
        return _quantity > 0 ? 1 : 2;
    }

    function abs(int256 _quantity) internal pure returns (uint256) {
        return uint256(_quantity >= 0 ? _quantity : -_quantity);
    }

    // TODO write unit test
    /// @dev return the value of Quantity minus amount
    function subAmount(int256 _quantity, uint256 _amount) internal pure returns(int256){
        return _quantity < 0 ? _quantity + int256(_amount) : _quantity - int256(_amount);
    }

    function abs128(int256 _quantity) internal pure returns (uint128) {
        return uint128(abs(_quantity));
    }

    function sumWithUint256(int256 a, uint256 b)
        internal
        pure
        returns (int256)
    {
        return a >= 0 ? a + int256(b) : a - int256(b);
    }

    function minusWithUint256(int256 a, uint256 b)
        internal
        pure
        returns (int256)
    {
        return a >= 0 ? a - int256(b) : a + int256(b);
    }
}
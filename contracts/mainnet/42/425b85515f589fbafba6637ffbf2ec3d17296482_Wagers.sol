// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/// @custom:security-contact [emailÂ protected]
contract SignatureVerifier {
    address public signer;

    constructor(address _signer) {
        require(_signer != address(0), "zero address can not be signer");
        signer = _signer;
    }

    function setSigner(address _signer) internal {
        require(_signer != address(0), "zero address can not be signer");
        signer = _signer;
        emit SetSigner(_signer);
    }

    // verify returns true if signature by signer matches the hash
    function verify(bytes32 messageHash, bytes memory signature)
        internal
        view
        returns (bool)
    {
        require(signer != address(0), "zero address can not be signer");
        bytes32 ethSignedMessageHash = getEthSignedMessageHash(messageHash);
        (bytes32 r, bytes32 s, uint8 v) = splitSignature(signature);
        return recoverSigner(ethSignedMessageHash, r, s, v) == signer;
    }

    // verifyRSV returns true if signature by signer matches the hash
    function verifyRSV(
        bytes32 messageHash,
        bytes32 r,
        bytes32 s,
        uint8 v
    ) internal view returns (bool) {
        require(signer != address(0), "zero address can not be signer");
        bytes32 ethSignedMessageHash = getEthSignedMessageHash(messageHash);
        return recoverSigner(ethSignedMessageHash, r, s, v) == signer;
    }

    function getEthSignedMessageHash(bytes32 messageHash)
        internal
        pure
        returns (bytes32)
    {
        return
            keccak256(
                abi.encodePacked(
                    "\x19Ethereum Signed Message:\n32",
                    messageHash
                )
            );
    }

    function recoverSigner(
        bytes32 _ethSignedMessageHash,
        bytes32 r,
        bytes32 s,
        uint8 v
    ) internal pure returns (address) {
        require(v == 27 || v == 28, "invalid v value");
        require(
            uint256(s) <
                0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A1,
            "invalid s value"
        );
        return ecrecover(_ethSignedMessageHash, v, r, s);
    }

    function splitSignature(bytes memory signature)
        internal
        pure
        returns (
            bytes32 r,
            bytes32 s,
            uint8 v
        )
    {
        require(signature.length == 65, "invalid signature length");
        assembly {
            r := mload(add(signature, 32))
            s := mload(add(signature, 64))
            v := byte(0, mload(add(signature, 96)))
        }
    }

    event SetSigner(address _signer);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC1155/ERC1155.sol)

pragma solidity ^0.8.0;

import "./IERC1155.sol";
import "./IERC1155Receiver.sol";
import "./extensions/IERC1155MetadataURI.sol";
import "../../utils/Address.sol";
import "../../utils/Context.sol";
import "../../utils/introspection/ERC165.sol";

/**
 * @dev Implementation of the basic standard multi-token.
 * See https://eips.ethereum.org/EIPS/eip-1155
 * Originally based on code by Enjin: https://github.com/enjin/erc-1155
 *
 * _Available since v3.1._
 */
contract ERC1155 is Context, ERC165, IERC1155, IERC1155MetadataURI {
    using Address for address;

    // Mapping from token ID to account balances
    mapping(uint256 => mapping(address => uint256)) private _balances;

    // Mapping from account to operator approvals
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    // Used as the URI for all token types by relying on ID substitution, e.g. https://token-cdn-domain/{id}.json
    string private _uri;

    /**
     * @dev See {_setURI}.
     */
    constructor(string memory uri_) {
        _setURI(uri_);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC1155).interfaceId ||
            interfaceId == type(IERC1155MetadataURI).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC1155MetadataURI-uri}.
     *
     * This implementation returns the same URI for *all* token types. It relies
     * on the token type ID substitution mechanism
     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].
     *
     * Clients calling this function must replace the `\{id\}` substring with the
     * actual token type ID.
     */
    function uri(uint256) public view virtual override returns (string memory) {
        return _uri;
    }

    /**
     * @dev See {IERC1155-balanceOf}.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) public view virtual override returns (uint256) {
        require(account != address(0), "ERC1155: address zero is not a valid owner");
        return _balances[id][account];
    }

    /**
     * @dev See {IERC1155-balanceOfBatch}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] memory accounts, uint256[] memory ids)
        public
        view
        virtual
        override
        returns (uint256[] memory)
    {
        require(accounts.length == ids.length, "ERC1155: accounts and ids length mismatch");

        uint256[] memory batchBalances = new uint256[](accounts.length);

        for (uint256 i = 0; i < accounts.length; ++i) {
            batchBalances[i] = balanceOf(accounts[i], ids[i]);
        }

        return batchBalances;
    }

    /**
     * @dev See {IERC1155-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC1155-isApprovedForAll}.
     */
    function isApprovedForAll(address account, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[account][operator];
    }

    /**
     * @dev See {IERC1155-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) public virtual override {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: caller is not token owner or approved"
        );
        _safeTransferFrom(from, to, id, amount, data);
    }

    /**
     * @dev See {IERC1155-safeBatchTransferFrom}.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) public virtual override {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: caller is not token owner or approved"
        );
        _safeBatchTransferFrom(from, to, ids, amounts, data);
    }

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function _safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: transfer to the zero address");

        address operator = _msgSender();
        uint256[] memory ids = _asSingletonArray(id);
        uint256[] memory amounts = _asSingletonArray(amount);

        _beforeTokenTransfer(operator, from, to, ids, amounts, data);

        uint256 fromBalance = _balances[id][from];
        require(fromBalance >= amount, "ERC1155: insufficient balance for transfer");
        unchecked {
            _balances[id][from] = fromBalance - amount;
        }
        _balances[id][to] += amount;

        emit TransferSingle(operator, from, to, id, amount);

        _afterTokenTransfer(operator, from, to, ids, amounts, data);

        _doSafeTransferAcceptanceCheck(operator, from, to, id, amount, data);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function _safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");
        require(to != address(0), "ERC1155: transfer to the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, from, to, ids, amounts, data);

        for (uint256 i = 0; i < ids.length; ++i) {
            uint256 id = ids[i];
            uint256 amount = amounts[i];

            uint256 fromBalance = _balances[id][from];
            require(fromBalance >= amount, "ERC1155: insufficient balance for transfer");
            unchecked {
                _balances[id][from] = fromBalance - amount;
            }
            _balances[id][to] += amount;
        }

        emit TransferBatch(operator, from, to, ids, amounts);

        _afterTokenTransfer(operator, from, to, ids, amounts, data);

        _doSafeBatchTransferAcceptanceCheck(operator, from, to, ids, amounts, data);
    }

    /**
     * @dev Sets a new URI for all token types, by relying on the token type ID
     * substitution mechanism
     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].
     *
     * By this mechanism, any occurrence of the `\{id\}` substring in either the
     * URI or any of the amounts in the JSON file at said URI will be replaced by
     * clients with the token type ID.
     *
     * For example, the `https://token-cdn-domain/\{id\}.json` URI would be
     * interpreted by clients as
     * `https://token-cdn-domain/000000000000000000000000000000000000000000000000000000000004cce0.json`
     * for token type ID 0x4cce0.
     *
     * See {uri}.
     *
     * Because these URIs cannot be meaningfully represented by the {URI} event,
     * this function emits no events.
     */
    function _setURI(string memory newuri) internal virtual {
        _uri = newuri;
    }

    /**
     * @dev Creates `amount` tokens of token type `id`, and assigns them to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function _mint(
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: mint to the zero address");

        address operator = _msgSender();
        uint256[] memory ids = _asSingletonArray(id);
        uint256[] memory amounts = _asSingletonArray(amount);

        _beforeTokenTransfer(operator, address(0), to, ids, amounts, data);

        _balances[id][to] += amount;
        emit TransferSingle(operator, address(0), to, id, amount);

        _afterTokenTransfer(operator, address(0), to, ids, amounts, data);

        _doSafeTransferAcceptanceCheck(operator, address(0), to, id, amount, data);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_mint}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function _mintBatch(
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: mint to the zero address");
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, address(0), to, ids, amounts, data);

        for (uint256 i = 0; i < ids.length; i++) {
            _balances[ids[i]][to] += amounts[i];
        }

        emit TransferBatch(operator, address(0), to, ids, amounts);

        _afterTokenTransfer(operator, address(0), to, ids, amounts, data);

        _doSafeBatchTransferAcceptanceCheck(operator, address(0), to, ids, amounts, data);
    }

    /**
     * @dev Destroys `amount` tokens of token type `id` from `from`
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `from` must have at least `amount` tokens of token type `id`.
     */
    function _burn(
        address from,
        uint256 id,
        uint256 amount
    ) internal virtual {
        require(from != address(0), "ERC1155: burn from the zero address");

        address operator = _msgSender();
        uint256[] memory ids = _asSingletonArray(id);
        uint256[] memory amounts = _asSingletonArray(amount);

        _beforeTokenTransfer(operator, from, address(0), ids, amounts, "");

        uint256 fromBalance = _balances[id][from];
        require(fromBalance >= amount, "ERC1155: burn amount exceeds balance");
        unchecked {
            _balances[id][from] = fromBalance - amount;
        }

        emit TransferSingle(operator, from, address(0), id, amount);

        _afterTokenTransfer(operator, from, address(0), ids, amounts, "");
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_burn}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     */
    function _burnBatch(
        address from,
        uint256[] memory ids,
        uint256[] memory amounts
    ) internal virtual {
        require(from != address(0), "ERC1155: burn from the zero address");
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, from, address(0), ids, amounts, "");

        for (uint256 i = 0; i < ids.length; i++) {
            uint256 id = ids[i];
            uint256 amount = amounts[i];

            uint256 fromBalance = _balances[id][from];
            require(fromBalance >= amount, "ERC1155: burn amount exceeds balance");
            unchecked {
                _balances[id][from] = fromBalance - amount;
            }
        }

        emit TransferBatch(operator, from, address(0), ids, amounts);

        _afterTokenTransfer(operator, from, address(0), ids, amounts, "");
    }

    /**
     * @dev Approve `operator` to operate on all of `owner` tokens
     *
     * Emits an {ApprovalForAll} event.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC1155: setting approval status for self");
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning, as well as batched variants.
     *
     * The same hook is called on both single and batched variants. For single
     * transfers, the length of the `ids` and `amounts` arrays will be 1.
     *
     * Calling conditions (for each `id` and `amount` pair):
     *
     * - When `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * of token type `id` will be  transferred to `to`.
     * - When `from` is zero, `amount` tokens of token type `id` will be minted
     * for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens of token type `id`
     * will be burned.
     * - `from` and `to` are never both zero.
     * - `ids` and `amounts` have the same, non-zero length.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {}

    /**
     * @dev Hook that is called after any token transfer. This includes minting
     * and burning, as well as batched variants.
     *
     * The same hook is called on both single and batched variants. For single
     * transfers, the length of the `id` and `amount` arrays will be 1.
     *
     * Calling conditions (for each `id` and `amount` pair):
     *
     * - When `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * of token type `id` will be  transferred to `to`.
     * - When `from` is zero, `amount` tokens of token type `id` will be minted
     * for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens of token type `id`
     * will be burned.
     * - `from` and `to` are never both zero.
     * - `ids` and `amounts` have the same, non-zero length.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {}

    function _doSafeTransferAcceptanceCheck(
        address operator,
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) private {
        if (to.isContract()) {
            try IERC1155Receiver(to).onERC1155Received(operator, from, id, amount, data) returns (bytes4 response) {
                if (response != IERC1155Receiver.onERC1155Received.selector) {
                    revert("ERC1155: ERC1155Receiver rejected tokens");
                }
            } catch Error(string memory reason) {
                revert(reason);
            } catch {
                revert("ERC1155: transfer to non-ERC1155Receiver implementer");
            }
        }
    }

    function _doSafeBatchTransferAcceptanceCheck(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) private {
        if (to.isContract()) {
            try IERC1155Receiver(to).onERC1155BatchReceived(operator, from, ids, amounts, data) returns (
                bytes4 response
            ) {
                if (response != IERC1155Receiver.onERC1155BatchReceived.selector) {
                    revert("ERC1155: ERC1155Receiver rejected tokens");
                }
            } catch Error(string memory reason) {
                revert(reason);
            } catch {
                revert("ERC1155: transfer to non-ERC1155Receiver implementer");
            }
        }
    }

    function _asSingletonArray(uint256 element) private pure returns (uint256[] memory) {
        uint256[] memory array = new uint256[](1);
        array[0] = element;

        return array;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC1155/IERC1155.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155 is IERC165 {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(
        address indexed operator,
        address indexed from,
        address indexed to,
        uint256[] ids,
        uint256[] values
    );

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes calldata data
    ) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] calldata ids,
        uint256[] calldata amounts,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC1155/IERC1155Receiver.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev _Available since v3.1._
 */
interface IERC1155Receiver is IERC165 {
    /**
     * @dev Handles the receipt of a single ERC1155 token type. This function is
     * called at the end of a `safeTransferFrom` after the balance has been updated.
     *
     * NOTE: To accept the transfer, this must return
     * `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
     * (i.e. 0xf23a6e61, or its own function selector).
     *
     * @param operator The address which initiated the transfer (i.e. msg.sender)
     * @param from The address which previously owned the token
     * @param id The ID of the token being transferred
     * @param value The amount of tokens being transferred
     * @param data Additional data with no specified format
     * @return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` if transfer is allowed
     */
    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4);

    /**
     * @dev Handles the receipt of a multiple ERC1155 token types. This function
     * is called at the end of a `safeBatchTransferFrom` after the balances have
     * been updated.
     *
     * NOTE: To accept the transfer(s), this must return
     * `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
     * (i.e. 0xbc197c81, or its own function selector).
     *
     * @param operator The address which initiated the batch transfer (i.e. msg.sender)
     * @param from The address which previously owned the token
     * @param ids An array containing ids of each token being transferred (order and length must match values array)
     * @param values An array containing amounts of each token being transferred (order and length must match ids array)
     * @param data Additional data with no specified format
     * @return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` if transfer is allowed
     */
    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC1155/extensions/ERC1155Supply.sol)

pragma solidity ^0.8.0;

import "../ERC1155.sol";

/**
 * @dev Extension of ERC1155 that adds tracking of total supply per id.
 *
 * Useful for scenarios where Fungible and Non-fungible tokens have to be
 * clearly identified. Note: While a totalSupply of 1 might mean the
 * corresponding is an NFT, there is no guarantees that no other token with the
 * same id are not going to be minted.
 */
abstract contract ERC1155Supply is ERC1155 {
    mapping(uint256 => uint256) private _totalSupply;

    /**
     * @dev Total amount of tokens in with a given id.
     */
    function totalSupply(uint256 id) public view virtual returns (uint256) {
        return _totalSupply[id];
    }

    /**
     * @dev Indicates whether any token exist with a given id, or not.
     */
    function exists(uint256 id) public view virtual returns (bool) {
        return ERC1155Supply.totalSupply(id) > 0;
    }

    /**
     * @dev See {ERC1155-_beforeTokenTransfer}.
     */
    function _beforeTokenTransfer(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual override {
        super._beforeTokenTransfer(operator, from, to, ids, amounts, data);

        if (from == address(0)) {
            for (uint256 i = 0; i < ids.length; ++i) {
                _totalSupply[ids[i]] += amounts[i];
            }
        }

        if (to == address(0)) {
            for (uint256 i = 0; i < ids.length; ++i) {
                uint256 id = ids[i];
                uint256 amount = amounts[i];
                uint256 supply = _totalSupply[id];
                require(supply >= amount, "ERC1155: burn amount exceeds totalSupply");
                unchecked {
                    _totalSupply[id] = supply - amount;
                }
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/extensions/IERC1155MetadataURI.sol)

pragma solidity ^0.8.0;

import "../IERC1155.sol";

/**
 * @dev Interface of the optional ERC1155MetadataExtension interface, as defined
 * in the https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155MetadataURI is IERC1155 {
    /**
     * @dev Returns the URI for token type `id`.
     *
     * If the `\{id\}` substring is present in the URI, it must be replaced by
     * clients with the actual token type ID.
     */
    function uri(uint256 id) external view returns (string memory);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC1155/utils/ERC1155Holder.sol)

pragma solidity ^0.8.0;

import "./ERC1155Receiver.sol";

/**
 * Simple implementation of `ERC1155Receiver` that will allow a contract to hold ERC1155 tokens.
 *
 * IMPORTANT: When inheriting this contract, you must include a way to use the received tokens, otherwise they will be
 * stuck.
 *
 * @dev _Available since v3.1._
 */
contract ERC1155Holder is ERC1155Receiver {
    function onERC1155Received(
        address,
        address,
        uint256,
        uint256,
        bytes memory
    ) public virtual override returns (bytes4) {
        return this.onERC1155Received.selector;
    }

    function onERC1155BatchReceived(
        address,
        address,
        uint256[] memory,
        uint256[] memory,
        bytes memory
    ) public virtual override returns (bytes4) {
        return this.onERC1155BatchReceived.selector;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/utils/ERC1155Receiver.sol)

pragma solidity ^0.8.0;

import "../IERC1155Receiver.sol";
import "../../../utils/introspection/ERC165.sol";

/**
 * @dev _Available since v3.1._
 */
abstract contract ERC1155Receiver is ERC165, IERC1155Receiver {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return interfaceId == type(IERC1155Receiver).interfaceId || super.supportsInterface(interfaceId);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "../lib/openzeppelin-contracts/contracts/token/ERC1155/extensions/ERC1155Supply.sol";
import "../lib/openzeppelin-contracts/contracts/access/Ownable.sol";
import "../lib/openzeppelin-contracts/contracts/token/ERC1155/utils/ERC1155Holder.sol";
import "../lib/ninja-syndicate/contracts/utils/Crypto_SignatureVerifier.sol";

contract Wagers is SignatureVerifier, ERC1155, ERC1155Supply, Ownable {
    enum Factions {
        UNKNOWN,
        ZHI,
        RMOMC,
        BC,
        ZHI_RAKE,
        RMOMC_RAKE,
        BC_RAKE
    }
    uint256 AdminSavingsAccount = 0;
    uint256 AdminBoostAccount = 1;
    bool paused = false;
    bool skip_signatures = false;

    // Take amounts from pot to rake back to admin
    // Linear increase
    // Directly proportional
    uint256 public RakeMax = 0.1 ether;
    uint256 public RakeProportion = 0.1 * 10**18;

    // Take amounts from pot to incentivise commit rewards
    // Linear increase
    // Inversely proportional
    // Takes from the rake
    bool CommitRewardEnabled = true;
    uint256 public CommitRewardMax = 0.05 ether;
    uint256 public CommitRewardProportion = 0.2 * 10**18;

    // Take amounts from pot to fill up the boost pot
    // Linear increase
    // Directly proportional
    // Takes from the rake
    bool BoostEnabled = true;
    uint256 public BoostMax = 0.1 ether;
    uint256 public BoostProportion = 0.2 * 10**18;

    // BattleCommit for signature verification parameters
    struct BattleCommit {
        uint256 number;
        uint256 started_at;
        uint256 ended_at;
        Factions winner;
        Factions runner_up;
        Factions loser;
    }

    // CurrentBattle for signature verification parameters
    struct CurrentBattle {
        uint256 number;
        uint256 started_at;
        uint256 expires_at;
    }

    // Results of historical battles
    mapping(uint256 => Factions) public Winners;

    constructor(address _signer)
        SignatureVerifier(_signer)
        ERC1155("https://supremacy.wagers.app/api/{id}.json")
    {
        signer = _signer;
    }

    function _beforeTokenTransfer(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal override(ERC1155, ERC1155Supply) {
        super._beforeTokenTransfer(operator, from, to, ids, amounts, data);
    }

    modifier notPaused() {
        require(!paused, "contract paused");
        _;
    }

    // -----------------------
    // User Callable Functions
    // -----------------------

    // Commit battle results
    function Commit(BattleCommit memory _battleCommit, bytes memory signature)
        public
        notPaused
    {
        require(
            Winners[_battleCommit.number] == Factions.UNKNOWN,
            "battle already committed"
        );
        bytes32 digest = BattleCommitHash(_battleCommit);
        if (!skip_signatures) {
            require(
                SignatureVerifier.verify(digest, signature),
                "invalid signature"
            );
        }
        Winners[_battleCommit.number] = _battleCommit.winner;

        uint256 zhi_rake = 0;
        uint256 rmomc_rake = 0;
        uint256 bc_rake = 0;
        ClearBoosts(_battleCommit.number, _battleCommit.winner);
        if (
            totalSupply(
                BuildBNFComposite(_battleCommit.number, _battleCommit.winner)
            ) == 0
        ) {
            // No winners, rake everything to admin

            zhi_rake = totalSupply(
                BuildBNFComposite(_battleCommit.number, Factions.ZHI)
            );
            rmomc_rake = totalSupply(
                BuildBNFComposite(_battleCommit.number, Factions.RMOMC)
            );
            bc_rake = totalSupply(
                BuildBNFComposite(_battleCommit.number, Factions.BC)
            );
        } else {
            // Calculate rakes
            (zhi_rake, rmomc_rake, bc_rake) = rakeable(
                _battleCommit.number,
                _battleCommit.winner
            );
        }

        uint256 rewardAmt = MintCommitTokens(
            _battleCommit.number,
            zhi_rake,
            rmomc_rake,
            bc_rake
        );
        if (rewardAmt > 0) {
            (bool success, ) = msg.sender.call{value: rewardAmt}("");
            require(success, "Failed to send rewards");
        }

        emit UserCommit(_battleCommit.number);
    }

    // Bet on a battle
    function Bet(
        uint256 bet_battle_number,
        Factions bet_faction,
        CurrentBattle memory _currentBattle,
        bytes memory signature
    ) public payable notPaused {
        require(
            Winners[bet_battle_number] == Factions.UNKNOWN,
            "battle results already committed"
        );
        require(
            bet_battle_number > _currentBattle.number,
            "bet battle in past"
        );
        require(msg.value > 0, "no value received");
        uint256 bnf_id = BuildBNFComposite(bet_battle_number, bet_faction);
        bytes32 digest = CurrentBattleNumberHash(_currentBattle);
        if (!skip_signatures) {
            require(
                SignatureVerifier.verify(digest, signature),
                "invalid signature"
            );
        }
        uint256[] memory ids = new uint256[](2);
        ids[0] = bnf_id;
        ids[1] = BattleNumberTotalID(bet_battle_number);

        uint256[] memory amts = new uint256[](2);
        amts[0] = msg.value;
        amts[1] = msg.value;

        _mintBatch(msg.sender, ids, amts, bytes(""));
        emit UserBet(bet_battle_number, bet_faction, msg.value);
    }

    // Claim winnings
    function Claim(
        uint256 claim_battle_number,
        CurrentBattle memory _currentBattle,
        bytes memory signature
    ) public notPaused {
        Factions winner = Winners[claim_battle_number];
        require(winner != Factions.UNKNOWN, "battle not committed yet");
        bytes32 digest = CurrentBattleNumberHash(_currentBattle);
        if (!skip_signatures) {
            require(
                SignatureVerifier.verify(digest, signature),
                "invalid signature"
            );
        }
        require(claim_battle_number < _currentBattle.number);
        uint256 claimAmt = Claimable(claim_battle_number);
        require(claimAmt > 0 ether, "0 claim balance");
        (bool success, ) = msg.sender.call{value: claimAmt}("");
        uint256 bnf_id = BuildBNFComposite(claim_battle_number, winner);
        uint256 burnAmt = balanceOf(msg.sender, bnf_id);
        _burn(msg.sender, bnf_id, burnAmt);
        require(success, "Failed to claim");
        emit UserClaim(claim_battle_number, claimAmt);
    }

    function RakeBoostable(uint256 rakeAmt) internal view returns (uint256) {
        // Take boost
        if (!BoostEnabled) {
            return 0;
        }
        uint256 boostAmt = (rakeAmt * BoostProportion) / 10**18;
        if (boostAmt > BoostMax) {
            boostAmt = BoostMax;
        }
        return boostAmt;
    }

    function CommitRewardEstimate(uint256 battle_number, Factions winner)
        public
        view
        returns (uint256)
    {
        uint256 totalRaked = ((TotalBattleShares(battle_number) -
            TotalFactionShares(battle_number, winner)) * RakeProportion) /
            10**18;

        return (totalRaked * CommitRewardProportion) / 10**18;
    }

    function RakeRewardable(uint256 rakeAmt) internal view returns (uint256) {
        // Take reward
        if (!CommitRewardEnabled) {
            return 0;
        }

        uint256 rewardAmt = (rakeAmt * CommitRewardProportion) / 10**18;
        return rewardAmt;
    }

    // Boost next pot with everything in the boost account
    function Boost(
        uint256 battle_number,
        CurrentBattle memory _currentBattle,
        bytes memory signature
    ) public notPaused {
        require(
            _currentBattle.number + 1 == battle_number,
            "can only boost next battle"
        );

        bytes32 digest = CurrentBattleNumberHash(_currentBattle);
        if (!skip_signatures) {
            require(
                SignatureVerifier.verify(digest, signature),
                "invalid signature"
            );
        }

        uint256 ZHI_id = BuildBNFComposite(battle_number, Factions.ZHI);
        uint256 RMOMC_id = BuildBNFComposite(battle_number, Factions.RMOMC);
        uint256 BC_id = BuildBNFComposite(battle_number, Factions.BC);

        uint256 amt = balanceOf(address(this), AdminBoostAccount);

        uint256[] memory ids = new uint256[](5);
        ids[0] = ZHI_id;
        ids[1] = RMOMC_id;
        ids[2] = BC_id;
        ids[3] = BattleNumberTotalID(battle_number);

        uint256[] memory amts = new uint256[](5);
        amts[0] = amt / 3;
        amts[1] = amt / 3;
        amts[2] = amt / 3;
        amts[3] = amt;

        _mintBatch(address(this), ids, amts, bytes(""));
        _burn(address(this), AdminBoostAccount, amt);
    }

    // DonateToBoost adds donations to boost pool
    function DonateToBoost() public payable {
        uint256 amt = msg.value;
        require(amt > 0, "no value provided for boost");
        _mint(address(this), AdminBoostAccount, amt, bytes(""));
    }

    // ------------------------
    // User Read-only Functions
    // ------------------------

    // Boostable the amt allowed to boost
    function Boostable() public view returns (uint256) {
        return balanceOf(address(this), AdminBoostAccount);
    }

    // TotalBoosted returns total boost for a single battle
    function TotalBoosted(uint256 battle_number) public view returns (uint256) {
        return
            TotalFactionInjections(battle_number, Factions.ZHI) +
            TotalFactionInjections(battle_number, Factions.RMOMC) +
            TotalFactionInjections(battle_number, Factions.BC);
    }

    function TotalFactionInjections(uint256 battle_number, Factions faction)
        public
        view
        returns (uint256)
    {
        return
            balanceOf(address(this), BuildBNFComposite(battle_number, faction));
    }

    // Claimable winnings
    function Claimable(uint256 claim_battle_number)
        public
        view
        returns (uint256)
    {
        uint256 totalShares = TotalBattleShares(claim_battle_number);
        uint256 userWinningShares = UserWinningShares(claim_battle_number);
        uint256 totalWinningShares = TotalWinningShares(claim_battle_number);

        uint256 ZHIRakeAccountID = BuildBNFRakeID(
            claim_battle_number,
            Factions.ZHI
        );
        uint256 RMOMCRakeAccountID = BuildBNFRakeID(
            claim_battle_number,
            Factions.RMOMC
        );
        uint256 BCRakeAccountID = BuildBNFRakeID(
            claim_battle_number,
            Factions.BC
        );

        uint256 totalRake = totalSupply(ZHIRakeAccountID) +
            totalSupply(RMOMCRakeAccountID) +
            totalSupply(BCRakeAccountID);

        if (totalWinningShares == 0) {
            return 0;
        }

        uint256 availableShares = totalShares - totalRake;
        return (availableShares * userWinningShares) / (totalWinningShares);
    }

    // -----------------------
    // Internal functions
    // -----------------------

    // Dangerous
    // Do not use
    // Only use if you're SURE no one has betted in a specific battle and you need to reclaim the tokens
    function AdminCancelBoost(uint256 battle_number) public onlyOwner {
        uint256 ZHI_id = BuildBNFComposite(battle_number, Factions.ZHI);
        uint256 RMOMC_id = BuildBNFComposite(battle_number, Factions.RMOMC);
        uint256 BC_id = BuildBNFComposite(battle_number, Factions.BC);

        uint256[] memory amts = new uint256[](4);
        amts[0] = balanceOf(address(this), ZHI_id);
        amts[1] = balanceOf(address(this), RMOMC_id);
        amts[2] = balanceOf(address(this), BC_id);

        uint256[] memory mint_ids = new uint256[](4);
        mint_ids[0] = AdminSavingsAccount;
        mint_ids[1] = AdminSavingsAccount;
        mint_ids[2] = AdminSavingsAccount;

        _mintBatch(address(this), mint_ids, amts, bytes(""));

        uint256[] memory burn_ids = new uint256[](4);
        burn_ids[0] = ZHI_id;
        burn_ids[1] = RMOMC_id;
        burn_ids[2] = BC_id;

        _burnBatch(address(this), burn_ids, amts);
    }

    // AdminManualBoost moves treasury to boost pool
    function AdminManualBoost(uint256 amt) public onlyOwner {
        require(
            amt < balanceOf(address(this), 0),
            "not enough treasury to move to boost"
        );
        _burn(address(this), AdminSavingsAccount, amt);
        _mint(address(this), AdminBoostAccount, amt, bytes(""));
    }

    // AdminWithdrawable the amt allowed to withdraw in the contract
    function AdminWithdrawable() public view onlyOwner returns (uint256) {
        return balanceOf(address(this), AdminSavingsAccount);
    }

    // AdminWithdraw the amt in the contract
    function AdminWithdraw(uint256 amt) public onlyOwner {
        _burn(address(this), AdminSavingsAccount, amt);
        (bool success, ) = owner().call{value: amt}("");
        require(success, "Failed to withdraw");
        emit Withdraw(amt);
    }

    // AdminSetPaused the contract
    function AdminSetPaused(bool _paused) public onlyOwner {
        paused = _paused;
        emit SetPaused(_paused);
    }

    // AdminSetURI update the baseURI of the ERC1155
    function AdminSetURI(string memory newuri) public onlyOwner {
        _setURI(newuri);
        emit SetURI(newuri);
    }

    function AdminSetRakeMax(uint256 _RakeMax) public onlyOwner {
        RakeMax = _RakeMax;
        emit SetRakeMax(_RakeMax);
    }

    function AdminSetRakeProportion(uint256 _RakeProportion) public onlyOwner {
        RakeProportion = _RakeProportion;
        emit SetRakeProportion(_RakeProportion);
    }

    function AdminSetCommitRewardEnabled(bool _CommitRewardEnabled)
        public
        onlyOwner
    {
        CommitRewardEnabled = _CommitRewardEnabled;
        emit SetCommitRewardEnabled(_CommitRewardEnabled);
    }

    function AdminSetCommitRewardMax(uint256 _CommitRewardMax)
        public
        onlyOwner
    {
        CommitRewardMax = _CommitRewardMax;
        emit SetCommitRewardMax(_CommitRewardMax);
    }

    function AdminSetCommitRewardProportion(uint256 _CommitRewardProportion)
        public
        onlyOwner
    {
        CommitRewardProportion = _CommitRewardProportion;
        emit SetCommitRewardProportion(_CommitRewardProportion);
    }

    function AdminSetBoostEnabled(bool _BoostEnabled) public onlyOwner {
        BoostEnabled = _BoostEnabled;
        emit SetBoostEnabled(_BoostEnabled);
    }

    function AdminSetBoostMax(uint256 _BoostMax) public onlyOwner {
        BoostMax = _BoostMax;
        emit SetBoostMax(_BoostMax);
    }

    function AdminSetBoostProportion(uint256 _BoostProportion)
        public
        onlyOwner
    {
        BoostProportion = _BoostProportion;
        emit SetBoostProportion(_BoostProportion);
    }

    function AdminSetSkipSignatures(bool skip) public onlyOwner {
        skip_signatures = skip;
    }

    // -----------------------
    // Helpers and internal functions
    // -----------------------

    function rakeable(uint256 battle_number, Factions winner)
        public
        view
        returns (
            uint256 zhi_rake,
            uint256 rmomc_rake,
            uint256 bc_rake
        )
    {
        // Take rake from losers
        uint256 ZHIBetsID = BuildBNFComposite(battle_number, Factions.ZHI);
        uint256 RMOMCBetsID = BuildBNFComposite(battle_number, Factions.RMOMC);
        uint256 BCBetsID = BuildBNFComposite(battle_number, Factions.BC);
        zhi_rake = (totalSupply(ZHIBetsID) * RakeProportion) / 10**18;
        rmomc_rake = (totalSupply(RMOMCBetsID) * RakeProportion) / 10**18;
        bc_rake = (totalSupply(BCBetsID) * RakeProportion) / 10**18;

        // Winner has zero rake
        if (winner == Factions.ZHI) {
            zhi_rake = 0;
        }
        if (winner == Factions.RMOMC) {
            rmomc_rake = 0;
        }
        if (winner == Factions.BC) {
            bc_rake = 0;
        }
    }

    function MintCommitTokens(
        uint256 battle_number,
        uint256 zhi_rake,
        uint256 rmomc_rake,
        uint256 bc_rake
    ) internal returns (uint256) {
        uint256 rakeAmt = zhi_rake + rmomc_rake + bc_rake;
        uint256 boostAmt = RakeBoostable(rakeAmt);
        uint256 rewardAmt = RakeRewardable(rakeAmt);
        uint256 ZHIRakeAccountID = BuildBNFRakeID(battle_number, Factions.ZHI);
        uint256 RMOMCRakeAccountID = BuildBNFRakeID(
            battle_number,
            Factions.RMOMC
        );
        uint256 BCRakeAccountID = BuildBNFRakeID(battle_number, Factions.BC);

        uint256[] memory ids = new uint256[](5);
        ids[0] = AdminSavingsAccount;
        ids[1] = AdminBoostAccount;
        ids[2] = ZHIRakeAccountID;
        ids[3] = RMOMCRakeAccountID;
        ids[4] = BCRakeAccountID;

        uint256[] memory amts = new uint256[](5);
        amts[0] = rakeAmt - boostAmt - rewardAmt;
        amts[1] = boostAmt;
        amts[2] = zhi_rake;
        amts[3] = rmomc_rake;
        amts[4] = bc_rake;

        // Store offset quantities for future claimable calculation
        _mintBatch(address(this), ids, amts, bytes(""));
        return rewardAmt;
    }

    // BattleCommitHash returns the hash of a historical battle record for signature verification
    function BattleCommitHash(BattleCommit memory _battleCommit)
        internal
        pure
        returns (bytes32)
    {
        return
            keccak256(
                abi.encode(
                    _battleCommit.number,
                    _battleCommit.started_at,
                    _battleCommit.ended_at,
                    uint256(_battleCommit.winner),
                    uint256(_battleCommit.runner_up),
                    uint256(_battleCommit.loser)
                )
            );
    }

    // CurrentBattleNumberHash returns the hash of the current battle for signature verification
    function CurrentBattleNumberHash(CurrentBattle memory _currentBattle)
        internal
        pure
        returns (bytes32)
    {
        return
            keccak256(
                abi.encode(
                    _currentBattle.number,
                    _currentBattle.started_at,
                    _currentBattle.expires_at
                )
            );
    }

    // TotalBattleShares total bets for a single battle
    function TotalBattleShares(uint256 battle_number)
        public
        view
        returns (uint256)
    {
        uint256 totalAmt = totalSupply(
            BuildBNFComposite(battle_number, Factions.ZHI)
        ) +
            totalSupply(BuildBNFComposite(battle_number, Factions.RMOMC)) +
            totalSupply(BuildBNFComposite(battle_number, Factions.BC));
        return totalAmt;
    }

    // TotalFactionShares total bets for a single battle for a single faction
    function TotalFactionShares(uint256 battle_number, Factions faction)
        public
        view
        returns (uint256)
    {
        if (faction == Factions.ZHI) {
            return totalSupply(BuildBNFComposite(battle_number, Factions.ZHI));
        }
        if (faction == Factions.RMOMC) {
            return
                totalSupply(BuildBNFComposite(battle_number, Factions.RMOMC));
        }
        if (faction == Factions.BC) {
            return totalSupply(BuildBNFComposite(battle_number, Factions.BC));
        }

        return 0;
    }

    // TotalFactionShares total bets for a single battle for the winning faction
    function TotalWinningShares(uint256 battle_number)
        public
        view
        returns (uint256)
    {
        Factions winner = Winners[battle_number];
        uint256 totalWinShares = 0;
        if (winner == Factions.ZHI) {
            uint256 id = BuildBNFComposite(battle_number, Factions.ZHI);
            totalWinShares = totalSupply(id) - balanceOf(address(this), id); // Remove admin shares from boost
        }

        if (winner == Factions.RMOMC) {
            uint256 id = BuildBNFComposite(battle_number, Factions.RMOMC);
            totalWinShares = totalSupply(id) - balanceOf(address(this), id); // Remove admin shares from boost
        }

        if (winner == Factions.BC) {
            uint256 id = BuildBNFComposite(battle_number, Factions.BC);
            totalWinShares = totalSupply(id) - balanceOf(address(this), id); // Remove admin shares from boost
        }
        return totalWinShares;
    }

    // UserWinningShares the number of shares the user has that are winning
    function UserWinningShares(uint256 battle_number)
        public
        view
        returns (uint256)
    {
        Factions winner = Winners[battle_number];
        uint256 userAmt = 0;
        if (winner == Factions.ZHI) {
            userAmt = balanceOf(
                msg.sender,
                BuildBNFComposite(battle_number, Factions.ZHI)
            );
        }

        if (winner == Factions.RMOMC) {
            userAmt = balanceOf(
                msg.sender,
                BuildBNFComposite(battle_number, Factions.RMOMC)
            );
        }

        if (winner == Factions.BC) {
            userAmt = balanceOf(
                msg.sender,
                BuildBNFComposite(battle_number, Factions.BC)
            );
        }
        return userAmt;
    }

    // Burn admin bets for winning factions because admin doesn't claim
    // Mint admin bet for winner faction back into 0 account
    // Do not touch admin bets for losing factions because this will go to claimants
    function ClearBoosts(uint256 battle_number, Factions winner) internal {
        uint256 bnf_id = BuildBNFComposite(battle_number, winner);
        uint256 amt = balanceOf(address(this), bnf_id);
        if (amt > 0) {
            _burn(address(this), bnf_id, amt);
            _mint(address(this), AdminSavingsAccount, amt, bytes(""));
        }
    }

    // BattleNumberTotalID returns the battle_number total ID
    function BattleNumberTotalID(uint256 battle_number)
        internal
        pure
        returns (uint256)
    {
        return battle_number * 10;
    }

    // BattleNumberFromBNFComposite returns the battle_number from composite ID
    function BattleNumberFromBNFComposite(uint256 battle_number_faction)
        internal
        pure
        returns (uint256)
    {
        return battle_number_faction / 10;
    }

    // FactionFromBNFComposite returns the faction from composite ID
    function FactionFromBNFComposite(uint256 battle_number_faction)
        internal
        pure
        returns (Factions)
    {
        return Factions(battle_number_faction % 10);
    }

    // BuildBNFComposite returns the faction from composite ID
    function BuildBNFComposite(uint256 battle_number, Factions faction)
        internal
        pure
        returns (uint256)
    {
        return battle_number * 10 + uint256(faction);
    }

    // BuildBNFComposite returns the faction from composite ID
    function BuildBNFRakeID(uint256 battle_number, Factions faction)
        internal
        pure
        returns (uint256)
    {
        // 1,2,3 -> 4,5,6
        return battle_number * 10 + uint256(faction) + 3;
    }

    // ---
    // Overrides
    // ---

    function onERC1155Received(
        address,
        address,
        uint256,
        uint256,
        bytes memory
    ) public virtual returns (bytes4) {
        return this.onERC1155Received.selector;
    }

    function onERC1155BatchReceived(
        address,
        address,
        uint256[] memory,
        uint256[] memory,
        bytes memory
    ) public virtual returns (bytes4) {
        return this.onERC1155BatchReceived.selector;
    }

    // ---
    // Events
    // ---

    event UserBet(uint256 battle_number, Factions faction, uint256 amt);
    event UserClaim(uint256 battle_number, uint256 amt);
    event UserCommit(uint256 battle_number);
    event Withdraw(uint256 amt);
    event SetURI(string newuri);
    event SetSkipSignatures(bool skip);
    event SetPaused(bool paused);
    event SetRakeMax(uint256 _RakeMax);
    event SetRakeProportion(uint256 _RakeProportion);
    event SetCommitRewardEnabled(bool _CommitRewardEnabled);
    event SetCommitRewardMax(uint256 _CommitRewardMax);
    event SetCommitRewardProportion(uint256 _CommitRewardProportion);
    event SetBoostEnabled(bool _BoostEnabled);
    event SetBoostMax(uint256 _BoostMax);
    event SetBoostProportion(uint256 _BoostProportion);
}
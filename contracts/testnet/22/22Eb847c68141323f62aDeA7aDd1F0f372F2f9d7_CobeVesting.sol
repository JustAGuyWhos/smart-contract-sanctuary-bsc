// SPDX-License-Identifier: SEE LICENSE IN LICENSE
pragma solidity ^0.8.17;

import "./CobeToken.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

contract CobeVesting is CobeToken, ReentrancyGuard {
    using SafeMath for uint256;
    struct VestingSchedule{
        // vesting schedule created 
        bool initialized;
        // vesting schedule approved
        bool approved;
        // beneficiary of tokens after they are released
        address  beneficiary;
        // vesting type (seed-1, private-2, IEO-3)
        uint256 vestingType;
        // percentage of tokens to be released monthly
        uint256 monthlyPercent;
        // amount of tokens to be released upfront
        uint256  releaseUpfront;
        // start time of the vesting period
        uint256  start;
        // duration of the vesting period in seconds
        uint256  duration;
        // duration of a slice period for the vesting in seconds
        uint256 slicePeriodSeconds;
        // whether or not the vesting is revocable
        bool  revocable;
        // total amount of tokens to be released at the end of the vesting
        uint256 amountTotal;
        // amount of tokens vested
        uint256  released;
        // whether or not the vesting has been revoked
        bool revoked;
    }

    mapping(address => VestingSchedule) private vestingSchedules;
    mapping(address => VestingSchedule) private OldvestingSchedules;
    mapping (address => bool) internal InvestorRemovedByAdmin;
    mapping (address => bool) internal InvestorBlockedByAdmin;
    uint256 private vestingSchedulesTotalAmount;

    // Release Upfront Percentage
    uint256 private RU_Percent; 

    // Monthly Release Percentages
    uint256 private Seed_Percent;
    uint256 private Private_Percent;
    uint256 private Public_Percent;

    // events
    event VestingScheduleApproved(address indexed _from, address indexed _forInvestor, bool _success, string _message);
    event VestingScheduleRejected(address indexed _from, address indexed _forInvestor, bool _success, string _message);
    event InvestorRemoved(address indexed _from, address indexed _forInvestor, bool _success, string _message);
    event VestingScheduleCreated(address indexed _from, address indexed _forInvestor, uint256 _vestingType, uint256 _start, uint256 _amount, bool _success, string _message);
    event TokensReleasedToInvestor(address indexed _from, uint256 _amount,bool _success, string _message);

    // modifiers
    modifier onlyIfVestingScheduleExists(address _addr) {
        require(vestingSchedules[_addr].initialized, "No Investor Found");
        _;
    }
    modifier onlyIfVestingScheduleApproved(address _addr) {
        require(vestingSchedules[_addr].approved, "Vesting Schedule Not Approved");
        _;
    }
    modifier onlyIfVestingScheduleNotRevoked(address _addr) {
        require(!vestingSchedules[_addr].revoked,"Vesting Schedule Revoked");
        _;
    }

    constructor(
        string memory _name, 
        string memory _symbol,
        uint256 _maxSupply,
        uint256 _ru,
        uint256 _seed,
        uint256 _private,
        uint256 _public
    ) CobeToken(_name,_symbol,_maxSupply) {
        RU_Percent = _ru.mul(1e18);
        Seed_Percent = _seed.mul(1e18);
        Private_Percent = _private.mul(1e18);
        Public_Percent = _public.mul(1e18);
    }

    // Returns the amount of tokens that are available to admin for create vesting and remove tokens.
    function getWithdrawableAmount()
        public
        view
        returns(uint256)
    {
        return balanceOf(address(this)).sub(vestingSchedulesTotalAmount);
    }

    // Admin can request for token removal
    function removeTokens(uint256 _amount) 
        external 
        onlyRole(ADMIN_ROLE) 
        returns (bool) 
    {
        require(_amount.mul(1e18) > 0,"amount is zero");
        require(getWithdrawableAmount()>=_amount.mul(1e18),"Amount is more than tokens available");
        if(tokenRemovedByAdmin[msg.sender]>0){
            tokenRemovedByAdmin[msg.sender]= tokenRemovedByAdmin[msg.sender].add(_amount.mul(1e18));
        } else {
            tokenRemovedByAdmin[msg.sender]=_amount.mul(1e18);
        }
        emit TokensActionByAdmin(msg.sender,_amount.mul(1e18),true,"Remove Tokens","Tokens Remove Request being approved by Approver");
        return true;
    }
    
    // approver can approve the token remove request from admin
    function approveRemovedTokens(address adminAddress, uint256 _amount) external onlyRole(APPROVER_ROLE) returns (bool) {
        require(adminAddress != address(0) && hasRole(ADMIN_ROLE,adminAddress), "Admin is the invalid Address");
        require(_amount.mul(1e18) > 0 && _amount.mul(1e18)<=tokenRemovedByAdmin[adminAddress], "Amount is Invalid");
        require(getWithdrawableAmount()>=_amount.mul(1e18),"Amount is more than tokens available");
        _burn(address(this),_amount.mul(1e18));
        tokenRemovedByAdmin[adminAddress] = tokenRemovedByAdmin[adminAddress].sub(_amount.mul(1e18));
        totalTokensMinted = totalTokensMinted.sub(_amount.mul(1e18));
        emit TokensActionByApprover(msg.sender,adminAddress,_amount.mul(1e18),true,"Remove Tokens Approved","Tokens burned from super admin account");
        return true;
    }

    // only approver can approve the vesting schedule.
    function approveVestingSchedule(address _address) 
        external 
        onlyIfVestingScheduleExists(_address)
        onlyRole(APPROVER_ROLE) 
        whenNotPaused
        returns(bool)
    {
        require(_address != address(0) && hasRole(INVESTOR_ROLE,_address), "invalid Investor Address");
        require(vestingSchedules[_address].approved==false,"Investor Already Approved");
        vestingSchedules[_address].approved=true;
        emit VestingScheduleApproved(msg.sender,_address,true,"Vesting Schedule Approved");
        return true;
    }

    // admin can create/update the vesting schedule for investor
    function createVestingSchedule(
        address _beneficiary,
        uint256 _vestingType,
        uint256 _start,
        uint256 _amount
    ) 
        external
        whenNotPaused 
        onlyRole(ADMIN_ROLE) 
    {
        require(!hasRole(SUPER_ADMIN_ROLE,_beneficiary),"Investor Account can't have Super Admin privileges");
        require(!hasRole(ADMIN_ROLE,_beneficiary),"Investor Account can't have Admin privileges");
        require(!hasRole(AUDITOR_ROLE,_beneficiary),"Investor Account can't have Auditor privileges");
        require(!hasRole(APPROVER_ROLE,_beneficiary),"Investor Account can't have Approver privileges");
        
        bool Started = block.timestamp > _start;
        require(!Started,"Can't Create Investor with vesting starting in Past time");
        // if vesting schedule already exist and not started or approved or has been revoked
        if(vestingSchedules[_beneficiary].initialized){
            bool isStarted = block.timestamp > vestingSchedules[_beneficiary].start;
            require(isStarted == false || vestingSchedules[_beneficiary].approved==false || vestingSchedules[_beneficiary].revoked,
                "Vesting for the beneficiary is already started, can't be modified");
            vestingSchedulesTotalAmount = vestingSchedulesTotalAmount.sub(vestingSchedules[_beneficiary].amountTotal);
            // add vesting schedule to old mapping
            OldvestingSchedules[_beneficiary] = vestingSchedules[_beneficiary];
        }
        require(_amount.mul(1e18) > 0, "TokenVesting: amount must be > 0");
        require(getWithdrawableAmount() >= _amount.mul(1e18),"TokenVesting: cannot create vesting schedule because not sufficient tokens");
        require(_vestingType>0 && _vestingType<4,"Invalid Vesting Request");
        // calculate releaseUpfront as 5% of amount
        uint256 _releaseUpfront = _amount.mul(1e18).mul(RU_Percent).div(uint256(100).mul(1e18));
        uint256 _monthlyPercent;
        //calc
        uint256 _fullpercent = uint256(100).mul(1e18);
        uint256 _duration;

        if(_vestingType==1){
            _monthlyPercent = Seed_Percent;
            uint256 numberOfMonths = _fullpercent.div(Seed_Percent);
            _duration = numberOfMonths.mul(30 days);
        } else if (_vestingType==2){
            _monthlyPercent = Private_Percent;
            uint256 numberOfMonths = _fullpercent.div(Private_Percent);
            _duration = numberOfMonths.mul(30 days);
        } else if (_vestingType==3){
            _monthlyPercent = Public_Percent;
            uint256 numberOfMonths = _fullpercent.div(Public_Percent);
            _duration = numberOfMonths.mul(30 days);
        }
        
        // create vesting schedule
        vestingSchedules[_beneficiary] = VestingSchedule(
            true,
            false,
            _beneficiary,
            _vestingType,
            _monthlyPercent,
            _releaseUpfront,
            _start,
            _duration,
            30 days,
            true,
            _amount.mul(1e18),
            0,//initial release amount
            false
        );
        vestingSchedulesTotalAmount = vestingSchedulesTotalAmount.add(_amount.mul(1e18));
        _grantRole(INVESTOR_ROLE, _beneficiary);
        _approve(address(this),_beneficiary,_amount.mul(1e18));
        emit VestingScheduleCreated(msg.sender,_beneficiary,_vestingType,_start,_amount.mul(1e18),true,"Vesting Schedule Created");
    }

    function decreaseInvestorAllowance(address owner, address spender, uint256 subtractedValue) internal virtual returns (bool) {
        uint256 currentAllowance = allowance(owner, spender);
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(owner, spender, currentAllowance - subtractedValue);
        }
        return true;
    }

    // only approver can reject the new investor request from admin
    function rejectVestingScheduleForInvestor(address _addr) 
        external 
        onlyRole(APPROVER_ROLE) 
        whenNotPaused
        returns (bool) 
    {
        require(_addr != address(0) && hasRole(INVESTOR_ROLE,_addr), "invalid Investor Address");
        require(vestingSchedules[_addr].approved==false,"Investor Already Approved, Can't be rejected");
        _revokeRole(INVESTOR_ROLE, _addr);
        vestingSchedulesTotalAmount = vestingSchedulesTotalAmount.sub(vestingSchedules[_addr].amountTotal);
        vestingSchedules[_addr].initialized = false;
        vestingSchedules[_addr].revoked = true;
        decreaseInvestorAllowance(address(this), _addr, vestingSchedules[_addr].amountTotal);
        // add old vesting again
        if(OldvestingSchedules[_addr].approved==true){
            vestingSchedules[_addr] = OldvestingSchedules[_addr];
            vestingSchedulesTotalAmount = vestingSchedulesTotalAmount.add(OldvestingSchedules[_addr].amountTotal);
            _grantRole(INVESTOR_ROLE, _addr);
            _approve(address(this),_addr,OldvestingSchedules[_addr].amountTotal);
        }
        emit VestingScheduleRejected(msg.sender,_addr,true,"Investor Request Rejected by Approver");
        return true;
    }

    // get vesting schedule for investor
    function getVestingScheduleForInvestor(address _addr) 
        external view 
        returns(VestingSchedule memory) 
    {
        return vestingSchedules[_addr];
    }

    // admin can request to remove investor
    function removeInvestorRequest(address _addr) 
        external
        onlyIfVestingScheduleExists(_addr)
        onlyIfVestingScheduleApproved(_addr)
        onlyIfVestingScheduleNotRevoked(_addr)
        whenNotPaused
        onlyRole(ADMIN_ROLE)
    {
        require(InvestorRemovedByAdmin[_addr]==false,"Investor Remove Request already exists");
        bool Started = block.timestamp > vestingSchedules[_addr].start;
        require(!Started,"Vesting for the Investor is already started, can't be removed");
        InvestorRemovedByAdmin[_addr] = true;
        emit InvestorRemoved(msg.sender,_addr,true,"Remove Investor Requested"); 
    }

    // approver can reject the request to remove investor
    function rejectRemoveInvestorRequest(address _addr) 
        external 
        onlyIfVestingScheduleExists(_addr)
        onlyIfVestingScheduleApproved(_addr)
        onlyIfVestingScheduleNotRevoked(_addr)
        whenNotPaused
        onlyRole(APPROVER_ROLE)
    {
        require(InvestorRemovedByAdmin[_addr],"No Admin Request Found");
        InvestorRemovedByAdmin[_addr] = false;
        emit InvestorRemoved(msg.sender,_addr,true,"Remove Investor Request Rejected");
    }

    // approver can approve the request to remove investor
    function removeInvestor(address _addr) 
        external 
        onlyIfVestingScheduleExists(_addr)
        onlyIfVestingScheduleApproved(_addr)
        onlyIfVestingScheduleNotRevoked(_addr)
        onlyRole(APPROVER_ROLE) 
        whenNotPaused
    {
        require(InvestorRemovedByAdmin[_addr],"No Admin Request Found");
        bool Started = block.timestamp > vestingSchedules[_addr].start;
        require(!Started,"Vesting for the Investor is already started, can't be removed");
        vestingSchedulesTotalAmount = vestingSchedulesTotalAmount.sub(vestingSchedules[_addr].amountTotal);
        vestingSchedules[_addr].revoked = true;
        vestingSchedules[_addr].initialized = false;
        isBlocklisted[_addr]=false;
        _revokeRole(INVESTOR_ROLE,_addr);
        InvestorRemovedByAdmin[_addr]=false;
        emit InvestorRemoved(msg.sender,_addr,true,"Investor Removed");
    }
    
    // admin can request to block the investor
    function blockInvestorRequest(address _addr)
        external
        onlyIfVestingScheduleExists(_addr)
        onlyIfVestingScheduleApproved(_addr)
        onlyIfVestingScheduleNotRevoked(_addr)
        onlyRole(ADMIN_ROLE)
        whenNotPaused
    {
        require(InvestorBlockedByAdmin[_addr]==false,"Investor Block Request already exists");
        bool Started = block.timestamp > vestingSchedules[_addr].start;
        require(!Started,"Vesting for the Investor is already started, can't be blocked");
        InvestorBlockedByAdmin[_addr]=true;
        emit UserBlocked(msg.sender,_addr,true,"Investor Block Request Submitted Successfully");
    }

    // approver can reject the request to block investor
    function rejectBlockInvestorRequest(address _addr)
        external
        onlyIfVestingScheduleExists(_addr)
        onlyIfVestingScheduleApproved(_addr)
        onlyIfVestingScheduleNotRevoked(_addr)
        onlyRole(APPROVER_ROLE)
        whenNotPaused
    {
        require(InvestorBlockedByAdmin[_addr],"No Admin Request Found");
        InvestorBlockedByAdmin[_addr]=false;
        emit UserBlocked(msg.sender,_addr,true,"Investor Block Request Rejected");
    }

    // approver can approve the request to block investor
    function blockInvestor(address _addr) 
        external 
        onlyIfVestingScheduleExists(_addr)
        onlyIfVestingScheduleApproved(_addr)
        onlyIfVestingScheduleNotRevoked(_addr)
        onlyRole(APPROVER_ROLE) 
        whenNotPaused
    {
        require(hasRole(INVESTOR_ROLE,_addr),"Invalid Investor Address");
        require(InvestorBlockedByAdmin[_addr],"No Admin Request Found");
        bool Started = block.timestamp > vestingSchedules[_addr].start;
        require(!Started,"Vesting for the Investor is already started, can't be blocked");
        isBlocklisted[_addr]=true;
        InvestorBlockedByAdmin[_addr]=false;
        emit UserBlocked(msg.sender,_addr,true,"Investor Blocked Successfully");
    }

    // Super Admin can unblock investor account
    function unblockInvestor(address _addr) external onlyRole(SUPER_ADMIN_ROLE) returns (bool) {
        require(hasRole(INVESTOR_ROLE,_addr),"Invalid Investor Address");
        require(isBlocklisted[_addr],"User Already UnBlocked");
        isBlocklisted[_addr]=false;
        emit UserUnblocked(msg.sender,_addr,true,"Investor Unblocked Successfully");
        return true;
    }
    
    // release function external call by investor to withdraw/transfer released tokens
    function releaseTokens()
        external
        onlyIfVestingScheduleExists(msg.sender)
        onlyIfVestingScheduleApproved(msg.sender)
        onlyIfVestingScheduleNotRevoked(msg.sender)
        onlyRole(INVESTOR_ROLE)
        nonReentrant
        whenNotPaused
    {
        uint256 vestedAmount = _computeReleasableAmount(msg.sender);
        vestingSchedules[msg.sender].released = vestingSchedules[msg.sender].released.add(vestedAmount);
        _withdrawReleasedTokens(vestedAmount);
        vestingSchedulesTotalAmount = vestingSchedulesTotalAmount.sub(vestedAmount);
        emit TokensReleasedToInvestor(msg.sender,vestedAmount,true,"Tokens Transferred to beneficiary's Account");
    }

    // Computes the releasable amount of tokens for a vesting schedule.
    function _computeReleasableAmount(address _addr)
        public
        virtual
        view
        returns(uint256)
    {
        uint256 starttime = vestingSchedules[_addr].start;
        uint256 endtime = vestingSchedules[_addr].start + vestingSchedules[_addr].duration;
        uint256 dateInSecs = getCurrentTime();
        uint256 index = 1;
        uint256 tokensReleasable = 0;
        while ( starttime < (endtime + vestingSchedules[_addr].slicePeriodSeconds)) {
            uint256 cbeAmount = 0;
            if (starttime == vestingSchedules[_addr].start) {
                cbeAmount = vestingSchedules[_addr].releaseUpfront;
            } else {
                uint256 vestingPeriods = vestingSchedules[_addr].duration.div(vestingSchedules[_addr].slicePeriodSeconds);
                uint256 totalVestedAmount = vestingSchedules[_addr].amountTotal.sub(vestingSchedules[_addr].releaseUpfront); 
                cbeAmount = totalVestedAmount.div(vestingPeriods);
            }
            if(dateInSecs > starttime){
                tokensReleasable = tokensReleasable.add(cbeAmount);
            }
            starttime = starttime.add(vestingSchedules[_addr].slicePeriodSeconds);
            index += 1;
        }
        tokensReleasable = tokensReleasable.sub(vestingSchedules[_addr].released);
        return tokensReleasable;
    }

    // internal function called by release function to transfer tokens
    function _withdrawReleasedTokens(uint256 _amount)
        internal virtual
    {
        transferAmount(address(this),msg.sender,_amount);
    }

    // get current unix time in seconds
    function getCurrentTime()
        public
        virtual
        view
        returns(uint256){
        return block.timestamp;
    }
}

// SPDX-License-Identifier: SEE LICENSE IN LICENSE
pragma solidity ^0.8.17;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/access/AccessControl.sol";
import "@openzeppelin/contracts/utils/math/Math.sol";
import "@openzeppelin/contracts/utils/math/SafeMath.sol";

contract CobeToken is
    ERC20,
    Pausable, 
    AccessControl
{
    using SafeMath for uint256;
    uint256 internal maxSupply;
    uint256 internal totalTokensMinted;

    mapping(address=>bool) internal isBlocklisted;
    mapping (address => uint256) internal tokenDepositByAdmin;
    mapping (address => uint256) internal tokenRemovedByAdmin;

    //User Roles in Cobe
    bytes32 public constant SUPER_ADMIN_ROLE = keccak256("SUPER_ADMIN");
    bytes32 public constant ADMIN_ROLE = keccak256("ADMIN");
    bytes32 public constant AUDITOR_ROLE = keccak256("AUDITOR");
    bytes32 public constant APPROVER_ROLE = keccak256("APPROVER");
    bytes32 public constant INVESTOR_ROLE = keccak256("INVESTOR");

    // events
    event GrantRoleSA(address indexed _from, bytes32 indexed _role, address indexed _account, bool _success, string _message);
    event RevokeRoleSA(address indexed _from, bytes32 indexed _role, address indexed _account, bool _success, string _message);
    event UserBlocked(address indexed _from, address indexed block_add, bool _success, string _message);
    event UserUnblocked(address indexed _from, address indexed block_add, bool _success, string _message);
    event TokensActionByAdmin(address indexed _from, uint256 _amount, bool _success, string _action, string _message);
    event TokensActionByApprover(address indexed _from, address indexed _adminAdress, uint256 _amount, bool _success, string _action, string _message);
    event ApproveInvestor(address indexed _from, address indexed investor_add, bool _success, string _message);
    event RejectInvestor(address indexed _from, address indexed investor_add, bool _success, string _message);

    // modifiers
    modifier isblock(address _addr){
        require(!isBlocklisted[_addr],"User Blocked");
        _;
    }

    constructor(string memory name, string memory symbol, uint256 _maxSupply) ERC20(name,symbol) {
        _grantRole(DEFAULT_ADMIN_ROLE, msg.sender);
        _grantRole(SUPER_ADMIN_ROLE, msg.sender);
        maxSupply = _maxSupply.mul(1e18);
    }

    // Super Admin can perform pause vesting
    function pause() public onlyRole(SUPER_ADMIN_ROLE) {
        _pause();
    }

    // Super Admin can perform unpause vesting
    function unpause() public onlyRole(SUPER_ADMIN_ROLE) {
        _unpause();
    }

    // users can login to system as per their roles
    function login() public view isblock(msg.sender) returns (string memory) {
        if (hasRole(SUPER_ADMIN_ROLE, msg.sender)) {
            return "SUPER_ADMIN";
        } else if (hasRole(ADMIN_ROLE, msg.sender)) {
            return "ADMIN";
        } else if (hasRole(AUDITOR_ROLE, msg.sender)) {
            return "AUDITOR";
        } else if (hasRole(APPROVER_ROLE, msg.sender)) {
            return "APPROVER";
        } else if (hasRole(INVESTOR_ROLE, msg.sender)) {
            return "INVESTOR";
        } else {
            return "FALSE";
        }
    }

    // Super Admin can block admins, auditors and approvers account
    function blockUser(bytes32 role, address _addr) external onlyRole(SUPER_ADMIN_ROLE) returns (bool) {
        require(hasRole(ADMIN_ROLE,_addr)||hasRole(AUDITOR_ROLE,_addr)||hasRole(APPROVER_ROLE,_addr),"Invalid Address");
        require(hasRole(role,_addr),"Invalid Account");
        isBlocklisted[_addr]=true;
        emit UserBlocked(msg.sender,_addr,true,"User Blocked Successfully");
        return true;
    }

    // Super Admin can unblock admins, auditors and approvers account
    function unblockUser(bytes32 role, address _addr) external onlyRole(SUPER_ADMIN_ROLE) returns (bool) {
        require(hasRole(ADMIN_ROLE,_addr)||hasRole(AUDITOR_ROLE,_addr)||hasRole(APPROVER_ROLE,_addr),"Invalid Address");
        require(hasRole(role,_addr),"Invalid Account");
        require(isBlocklisted[_addr],"User Already UnBlocked");
        isBlocklisted[_addr]=false;
        emit UserUnblocked(msg.sender,_addr,true,"User Unblocked Successfully");
        return true;
    }

    // Super Admin can get user status (Blocked or not)
    function getUserLockStatus(address _addr) external view returns (bool) {
        return isBlocklisted[_addr];
    }

    // super admin can grant role for new admins,auditors,approvers
    function grantRole(bytes32 role, address account) public override onlyRole(SUPER_ADMIN_ROLE) {
        require(role != INVESTOR_ROLE,"ONLY ADMIN CAN MANAGE INVESTORS");
        require(!hasRole(ADMIN_ROLE,account),"Account already has Admin Role, Revoke Admin Access First to add the Account");
        require(!hasRole(AUDITOR_ROLE,account),"Account already has AUDITOR Role, Revoke Auditor Access First to add the Account");
        require(!hasRole(APPROVER_ROLE,account),"Account already has APPROVER Role, Revoke Approver Access First to add the Account");
        require(!hasRole(INVESTOR_ROLE,account),"Account already has INVESTOR Role, Revoke Investor Access First to add the Account");
        isBlocklisted[account]=false;
        _grantRole(role, account);
        emit GrantRoleSA(msg.sender,role,account,true,"Role is granted");
    }

    // super admin can revoke role for admins,auditors,approvers
    function revokeRole(bytes32 role, address account) public override onlyRole(SUPER_ADMIN_ROLE) {
        require(role != INVESTOR_ROLE,"ONLY ADMIN CAN MANAGE INVESTORS");
        require(hasRole(role,account),"Invalid Account");
        require(hasRole(ADMIN_ROLE,account)||hasRole(APPROVER_ROLE,account)||hasRole(AUDITOR_ROLE,account),"Invalid Account");
        isBlocklisted[account]=false;
        _revokeRole(role, account);
        emit RevokeRoleSA(msg.sender,role,account,true,"Role is revoked");
    }

    // Admin can request to mint tokens
    function depositTokens(uint256 _amount) external onlyRole(ADMIN_ROLE) returns (bool) {
        require(_amount.mul(1e18) > 0,"amount is zero");
        if(tokenDepositByAdmin[msg.sender]>0){
            tokenDepositByAdmin[msg.sender] = tokenDepositByAdmin[msg.sender].add(_amount.mul(1e18));    
        } else {
            tokenDepositByAdmin[msg.sender]=_amount.mul(1e18);
        }
        emit TokensActionByAdmin(msg.sender,_amount.mul(1e18),true,"Deposit Tokens","Tokens being Approved by Approver");
        return true;
    }

    // approver can approve the deposit token request from admin 
    function approveDepositedTokens(address adminAddress, uint256 _amount) external onlyRole(APPROVER_ROLE) returns (bool) {
        require(adminAddress != address(0) && hasRole(ADMIN_ROLE,adminAddress), "Admin is the invalid Address");
        require(_amount.mul(1e18) > 0 && _amount.mul(1e18)<=tokenDepositByAdmin[adminAddress], "Amount is Invalid");
        
        totalTokensMinted = totalTokensMinted.add(_amount.mul(1e18));
        require(totalTokensMinted <= maxSupply,"Minted tokens reached Tokens Max Supply, Try depositing smaller CBE Amount");

        _mint(address(this),_amount.mul(1e18));
        tokenDepositByAdmin[adminAddress] = tokenDepositByAdmin[adminAddress].sub(_amount.mul(1e18));
        
        emit TokensActionByApprover(msg.sender,adminAddress,_amount.mul(1e18),true,"Deposit Tokens Approved","Tokens minted to super admin account");
        return true;
    }

    // approver can reject the token deposit request from admin
    function rejectDepositedTokens(address adminAddress, uint256 _amount) external onlyRole(APPROVER_ROLE) returns (bool) {
        require(adminAddress != address(0) && hasRole(ADMIN_ROLE,adminAddress), "Admin is the invalid Address");
        require(_amount.mul(1e18) > 0 && _amount.mul(1e18)<=tokenDepositByAdmin[adminAddress], "Amount is Invalid");
        tokenDepositByAdmin[adminAddress] = tokenDepositByAdmin[adminAddress].sub(_amount.mul(1e18));
        emit TokensActionByApprover(msg.sender,adminAddress,_amount.mul(1e18),true,"Deposit Tokens Rejected","Deposit Tokens Request Rejected");
        return true;
    }
    
    // approver can reject the token remove request from admin
    function rejectRemovedTokens(address adminAddress, uint256 _amount) external onlyRole(APPROVER_ROLE) returns (bool) {
        require(adminAddress != address(0) && hasRole(ADMIN_ROLE,adminAddress), "Admin is the invalid Address");
        require(_amount.mul(1e18) > 0 && _amount.mul(1e18)<=tokenRemovedByAdmin[adminAddress], "Amount is Invalid");
        tokenRemovedByAdmin[adminAddress] = tokenRemovedByAdmin[adminAddress].sub(_amount.mul(1e18));
        emit TokensActionByApprover(msg.sender,adminAddress,_amount.mul(1e18),true,"Remove Tokens Rejected","Remove Tokens Request Rejected");
        return true;
    }

    // transfer Ownership
    function transferOwnership(address newOwner) public virtual onlyRole(SUPER_ADMIN_ROLE) returns (bool, string memory) {
        require(newOwner != address(0), "new owner is the zero address");
        _revokeRole(DEFAULT_ADMIN_ROLE,msg.sender);
        _revokeRole(SUPER_ADMIN_ROLE,msg.sender);
        _grantRole(DEFAULT_ADMIN_ROLE, newOwner);
        _grantRole(SUPER_ADMIN_ROLE, newOwner);
        return(true,"Contract Ownership is transferred to new owner");
    }

    function transferFrom(address, address, uint256) public virtual override returns (bool) {
       return false;
    }

    function transferAmount(address from, address to, uint256 amount) internal virtual returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, amount);
        _transfer(from, to, amount);
        return true;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be _NOT_ENTERED
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (token/ERC20/ERC20.sol)

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.openzeppelin.com/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * We have followed general OpenZeppelin Contracts guidelines: functions revert
 * instead returning `false` on failure. This behavior is nonetheless
 * conventional and does not conflict with the expectations of ERC20
 * applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping(address => uint256) private _balances;

    mapping(address => mapping(address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * The default value of {decimals} is 18. To select a different value for
     * {decimals} you should overload it.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _transfer(owner, to, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * NOTE: If `amount` is the maximum `uint256`, the allowance is not updated on
     * `transferFrom`. This is semantically equivalent to an infinite approval.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * NOTE: Does not update the allowance if the current allowance
     * is the maximum `uint256`.
     *
     * Requirements:
     *
     * - `from` and `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     * - the caller must have allowance for ``from``'s tokens of at least
     * `amount`.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) public virtual override returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, amount);
        _transfer(from, to, amount);
        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, allowance(owner, spender) + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        address owner = _msgSender();
        uint256 currentAllowance = allowance(owner, spender);
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(owner, spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev Moves `amount` of tokens from `from` to `to`.
     *
     * This internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     */
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(from, to, amount);

        uint256 fromBalance = _balances[from];
        require(fromBalance >= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[from] = fromBalance - amount;
            // Overflow not possible: the sum of all balances is capped by totalSupply, and the sum is preserved by
            // decrementing then incrementing.
            _balances[to] += amount;
        }

        emit Transfer(from, to, amount);

        _afterTokenTransfer(from, to, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        unchecked {
            // Overflow not possible: balance + amount is at most totalSupply + amount, which is checked above.
            _balances[account] += amount;
        }
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        unchecked {
            _balances[account] = accountBalance - amount;
            // Overflow not possible: amount <= accountBalance <= totalSupply.
            _totalSupply -= amount;
        }

        emit Transfer(account, address(0), amount);

        _afterTokenTransfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Updates `owner` s allowance for `spender` based on spent `amount`.
     *
     * Does not update the allowance amount in case of infinite allowance.
     * Revert if not enough allowance is available.
     *
     * Might emit an {Approval} event.
     */
    function _spendAllowance(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        uint256 currentAllowance = allowance(owner, spender);
        if (currentAllowance != type(uint256).max) {
            require(currentAllowance >= amount, "ERC20: insufficient allowance");
            unchecked {
                _approve(owner, spender, currentAllowance - amount);
            }
        }
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * has been transferred to `to`.
     * - when `from` is zero, `amount` tokens have been minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        require(!paused(), "Pausable: paused");
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        require(paused(), "Pausable: not paused");
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (access/AccessControl.sol)

pragma solidity ^0.8.0;

import "./IAccessControl.sol";
import "../utils/Context.sol";
import "../utils/Strings.sol";
import "../utils/introspection/ERC165.sol";

/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping(address => bool) members;
        bytes32 adminRole;
    }

    mapping(bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role);
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view virtual override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `_msgSender()` is missing `role`.
     * Overriding this function changes the behavior of the {onlyRole} modifier.
     *
     * Format of the revert message is described in {_checkRole}.
     *
     * _Available since v4.6._
     */
    function _checkRole(bytes32 role) internal view virtual {
        _checkRole(role, _msgSender());
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view virtual {
        if (!hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        Strings.toHexString(account),
                        " is missing role ",
                        Strings.toHexString(uint256(role), 32)
                    )
                )
            );
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view virtual override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * May emit a {RoleGranted} event.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * May emit a {RoleRevoked} event.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been revoked `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     *
     * May emit a {RoleRevoked} event.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * May emit a {RoleGranted} event.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     *
     * NOTE: This function is deprecated in favor of {_grantRole}.
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * Internal function without access restriction.
     *
     * May emit a {RoleGranted} event.
     */
    function _grantRole(bytes32 role, address account) internal virtual {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * Internal function without access restriction.
     *
     * May emit a {RoleRevoked} event.
     */
    function _revokeRole(bytes32 role, address account) internal virtual {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/math/Math.sol)

pragma solidity ^0.8.0;

/**
 * @dev Standard math utilities missing in the Solidity language.
 */
library Math {
    enum Rounding {
        Down, // Toward negative infinity
        Up, // Toward infinity
        Zero // Toward zero
    }

    /**
     * @dev Returns the largest of two numbers.
     */
    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a > b ? a : b;
    }

    /**
     * @dev Returns the smallest of two numbers.
     */
    function min(uint256 a, uint256 b) internal pure returns (uint256) {
        return a < b ? a : b;
    }

    /**
     * @dev Returns the average of two numbers. The result is rounded towards
     * zero.
     */
    function average(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b) / 2 can overflow.
        return (a & b) + (a ^ b) / 2;
    }

    /**
     * @dev Returns the ceiling of the division of two numbers.
     *
     * This differs from standard division with `/` in that it rounds up instead
     * of rounding down.
     */
    function ceilDiv(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b - 1) / b can overflow on addition, so we distribute.
        return a == 0 ? 0 : (a - 1) / b + 1;
    }

    /**
     * @notice Calculates floor(x * y / denominator) with full precision. Throws if result overflows a uint256 or denominator == 0
     * @dev Original credit to Remco Bloemen under MIT license (https://xn--2-umb.com/21/muldiv)
     * with further edits by Uniswap Labs also under MIT license.
     */
    function mulDiv(
        uint256 x,
        uint256 y,
        uint256 denominator
    ) internal pure returns (uint256 result) {
        unchecked {
            // 512-bit multiply [prod1 prod0] = x * y. Compute the product mod 2^256 and mod 2^256 - 1, then use
            // use the Chinese Remainder Theorem to reconstruct the 512 bit result. The result is stored in two 256
            // variables such that product = prod1 * 2^256 + prod0.
            uint256 prod0; // Least significant 256 bits of the product
            uint256 prod1; // Most significant 256 bits of the product
            assembly {
                let mm := mulmod(x, y, not(0))
                prod0 := mul(x, y)
                prod1 := sub(sub(mm, prod0), lt(mm, prod0))
            }

            // Handle non-overflow cases, 256 by 256 division.
            if (prod1 == 0) {
                return prod0 / denominator;
            }

            // Make sure the result is less than 2^256. Also prevents denominator == 0.
            require(denominator > prod1);

            ///////////////////////////////////////////////
            // 512 by 256 division.
            ///////////////////////////////////////////////

            // Make division exact by subtracting the remainder from [prod1 prod0].
            uint256 remainder;
            assembly {
                // Compute remainder using mulmod.
                remainder := mulmod(x, y, denominator)

                // Subtract 256 bit number from 512 bit number.
                prod1 := sub(prod1, gt(remainder, prod0))
                prod0 := sub(prod0, remainder)
            }

            // Factor powers of two out of denominator and compute largest power of two divisor of denominator. Always >= 1.
            // See https://cs.stackexchange.com/q/138556/92363.

            // Does not overflow because the denominator cannot be zero at this stage in the function.
            uint256 twos = denominator & (~denominator + 1);
            assembly {
                // Divide denominator by twos.
                denominator := div(denominator, twos)

                // Divide [prod1 prod0] by twos.
                prod0 := div(prod0, twos)

                // Flip twos such that it is 2^256 / twos. If twos is zero, then it becomes one.
                twos := add(div(sub(0, twos), twos), 1)
            }

            // Shift in bits from prod1 into prod0.
            prod0 |= prod1 * twos;

            // Invert denominator mod 2^256. Now that denominator is an odd number, it has an inverse modulo 2^256 such
            // that denominator * inv = 1 mod 2^256. Compute the inverse by starting with a seed that is correct for
            // four bits. That is, denominator * inv = 1 mod 2^4.
            uint256 inverse = (3 * denominator) ^ 2;

            // Use the Newton-Raphson iteration to improve the precision. Thanks to Hensel's lifting lemma, this also works
            // in modular arithmetic, doubling the correct bits in each step.
            inverse *= 2 - denominator * inverse; // inverse mod 2^8
            inverse *= 2 - denominator * inverse; // inverse mod 2^16
            inverse *= 2 - denominator * inverse; // inverse mod 2^32
            inverse *= 2 - denominator * inverse; // inverse mod 2^64
            inverse *= 2 - denominator * inverse; // inverse mod 2^128
            inverse *= 2 - denominator * inverse; // inverse mod 2^256

            // Because the division is now exact we can divide by multiplying with the modular inverse of denominator.
            // This will give us the correct result modulo 2^256. Since the preconditions guarantee that the outcome is
            // less than 2^256, this is the final result. We don't need to compute the high bits of the result and prod1
            // is no longer required.
            result = prod0 * inverse;
            return result;
        }
    }

    /**
     * @notice Calculates x * y / denominator with full precision, following the selected rounding direction.
     */
    function mulDiv(
        uint256 x,
        uint256 y,
        uint256 denominator,
        Rounding rounding
    ) internal pure returns (uint256) {
        uint256 result = mulDiv(x, y, denominator);
        if (rounding == Rounding.Up && mulmod(x, y, denominator) > 0) {
            result += 1;
        }
        return result;
    }

    /**
     * @dev Returns the square root of a number. If the number is not a perfect square, the value is rounded down.
     *
     * Inspired by Henry S. Warren, Jr.'s "Hacker's Delight" (Chapter 11).
     */
    function sqrt(uint256 a) internal pure returns (uint256) {
        if (a == 0) {
            return 0;
        }

        // For our first guess, we get the biggest power of 2 which is smaller than the square root of the target.
        //
        // We know that the "msb" (most significant bit) of our target number `a` is a power of 2 such that we have
        // `msb(a) <= a < 2*msb(a)`. This value can be written `msb(a)=2**k` with `k=log2(a)`.
        //
        // This can be rewritten `2**log2(a) <= a < 2**(log2(a) + 1)`
        // → `sqrt(2**k) <= sqrt(a) < sqrt(2**(k+1))`
        // → `2**(k/2) <= sqrt(a) < 2**((k+1)/2) <= 2**(k/2 + 1)`
        //
        // Consequently, `2**(log2(a) / 2)` is a good first approximation of `sqrt(a)` with at least 1 correct bit.
        uint256 result = 1 << (log2(a) >> 1);

        // At this point `result` is an estimation with one bit of precision. We know the true value is a uint128,
        // since it is the square root of a uint256. Newton's method converges quadratically (precision doubles at
        // every iteration). We thus need at most 7 iteration to turn our partial result with one bit of precision
        // into the expected uint128 result.
        unchecked {
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            return min(result, a / result);
        }
    }

    /**
     * @notice Calculates sqrt(a), following the selected rounding direction.
     */
    function sqrt(uint256 a, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = sqrt(a);
            return result + (rounding == Rounding.Up && result * result < a ? 1 : 0);
        }
    }

    /**
     * @dev Return the log in base 2, rounded down, of a positive value.
     * Returns 0 if given 0.
     */
    function log2(uint256 value) internal pure returns (uint256) {
        uint256 result = 0;
        unchecked {
            if (value >> 128 > 0) {
                value >>= 128;
                result += 128;
            }
            if (value >> 64 > 0) {
                value >>= 64;
                result += 64;
            }
            if (value >> 32 > 0) {
                value >>= 32;
                result += 32;
            }
            if (value >> 16 > 0) {
                value >>= 16;
                result += 16;
            }
            if (value >> 8 > 0) {
                value >>= 8;
                result += 8;
            }
            if (value >> 4 > 0) {
                value >>= 4;
                result += 4;
            }
            if (value >> 2 > 0) {
                value >>= 2;
                result += 2;
            }
            if (value >> 1 > 0) {
                result += 1;
            }
        }
        return result;
    }

    /**
     * @dev Return the log in base 2, following the selected rounding direction, of a positive value.
     * Returns 0 if given 0.
     */
    function log2(uint256 value, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = log2(value);
            return result + (rounding == Rounding.Up && 1 << result < value ? 1 : 0);
        }
    }

    /**
     * @dev Return the log in base 10, rounded down, of a positive value.
     * Returns 0 if given 0.
     */
    function log10(uint256 value) internal pure returns (uint256) {
        uint256 result = 0;
        unchecked {
            if (value >= 10**64) {
                value /= 10**64;
                result += 64;
            }
            if (value >= 10**32) {
                value /= 10**32;
                result += 32;
            }
            if (value >= 10**16) {
                value /= 10**16;
                result += 16;
            }
            if (value >= 10**8) {
                value /= 10**8;
                result += 8;
            }
            if (value >= 10**4) {
                value /= 10**4;
                result += 4;
            }
            if (value >= 10**2) {
                value /= 10**2;
                result += 2;
            }
            if (value >= 10**1) {
                result += 1;
            }
        }
        return result;
    }

    /**
     * @dev Return the log in base 10, following the selected rounding direction, of a positive value.
     * Returns 0 if given 0.
     */
    function log10(uint256 value, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = log10(value);
            return result + (rounding == Rounding.Up && 10**result < value ? 1 : 0);
        }
    }

    /**
     * @dev Return the log in base 256, rounded down, of a positive value.
     * Returns 0 if given 0.
     *
     * Adding one to the result gives the number of pairs of hex symbols needed to represent `value` as a hex string.
     */
    function log256(uint256 value) internal pure returns (uint256) {
        uint256 result = 0;
        unchecked {
            if (value >> 128 > 0) {
                value >>= 128;
                result += 16;
            }
            if (value >> 64 > 0) {
                value >>= 64;
                result += 8;
            }
            if (value >> 32 > 0) {
                value >>= 32;
                result += 4;
            }
            if (value >> 16 > 0) {
                value >>= 16;
                result += 2;
            }
            if (value >> 8 > 0) {
                result += 1;
            }
        }
        return result;
    }

    /**
     * @dev Return the log in base 10, following the selected rounding direction, of a positive value.
     * Returns 0 if given 0.
     */
    function log256(uint256 value, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = log256(value);
            return result + (rounding == Rounding.Up && 1 << (result * 8) < value ? 1 : 0);
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (utils/math/SafeMath.sol)

pragma solidity ^0.8.0;

// CAUTION
// This version of SafeMath should only be used with Solidity 0.8 or later,
// because it relies on the compiler's built in overflow checks.

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SafeMath` is generally not needed starting with Solidity 0.8, since the compiler
 * now has built in overflow checking.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/IAccessControl.sol)

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/Strings.sol)

pragma solidity ^0.8.0;

import "./math/Math.sol";

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _SYMBOLS = "0123456789abcdef";
    uint8 private constant _ADDRESS_LENGTH = 20;

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        unchecked {
            uint256 length = Math.log10(value) + 1;
            string memory buffer = new string(length);
            uint256 ptr;
            /// @solidity memory-safe-assembly
            assembly {
                ptr := add(buffer, add(32, length))
            }
            while (true) {
                ptr--;
                /// @solidity memory-safe-assembly
                assembly {
                    mstore8(ptr, byte(mod(value, 10), _SYMBOLS))
                }
                value /= 10;
                if (value == 0) break;
            }
            return buffer;
        }
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        unchecked {
            return toHexString(value, Math.log256(value) + 1);
        }
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }

    /**
     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal representation.
     */
    function toHexString(address addr) internal pure returns (string memory) {
        return toHexString(uint256(uint160(addr)), _ADDRESS_LENGTH);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}
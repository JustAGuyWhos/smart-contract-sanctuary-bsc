pragma solidity ^0.8.15;

// SPDX-License-Identifier: MIT

import "./Dependencies.sol";


interface IStrategy {

    // Sum of all shares of users
    function sharesTotal() external view returns (uint256);

    // Get wantLockedTotal and sharesTotal  
    function getShares() external view returns (uint256, uint256);

    // Transfer want tokens gammaFarm -> strategy
    function deposit(uint256 _wantAmt) external returns (uint256, uint256);

    // Transfer want tokens strategy -> gammaFarm
    function withdraw(uint256 _wantAmt) external returns (uint256, uint256);

    // Transfer infinity want tokens strategy -> gammaFarm
    function unstake(uint256 _wantAmt, bool instantly) external returns (uint256, uint256, uint256);

    // Transfer want tokens without claiming pending rewards strategy -> gammaFarm
    function emergencyWithdraw(uint256 _wantAmt) external returns (uint256, uint256);

    // Exchange rate between gTokens and iTokens
    function iTokenExchangeRate() external view returns (uint256);

    // Transfer rewards for autocompounding gammaFarm -> strategy
    function earnGammaProfits() external;

    // Transfer rewards strategy -> gammaFarm
    function earnGammaProfitsiGamma(uint256 _gammaRewards) external;

    // get pending Rewards generated by strategy
    function getStratPendingRewards() external view returns (uint256);
}

interface Reservoir {
    
    // Get farm drip rate per block 
    function farmV2DripRate() external view returns(uint);

    // Drip rewards reservoir -> gammaFarm
    function drip() external;
    
}

interface IToken {

    // Mint iTokens
    function mint(address accountAddress, uint256 amount) external;

    // Burn iTokens
    function burn(address accountAddress, uint256 amount) external;
}

interface GToken {
    function exchangeRateStored() external view returns (uint);
}

interface GammaTroller {
    function updateFactor(address _user, uint256 _newiGammaBalance) external;
}


contract PlanetFarm is Initializable, OwnableUpgradeable, ReentrancyGuardUpgradeable, PausableUpgradeable {

    using SafeERC20 for IERC20;
    using Math for uint256;

    Reservoir public ReservoirAddress;
    GammaTroller public GammaTrollerAddress;

    // Info of each user.
    struct UserInfo {
        uint256 shares; // Number of shares that the user has in the pool.
        uint256 rewardDebt; // Reward adjustment that the user should not receive at the next distribution.
        uint256 factor; // Square root of user.shares * iGammaBalance. This decides the share of boosted rewards that the user receives.
        
        // infinity vault related variables
        uint256 iTokenToBeUnstaked; // keep track of how much iToken the user has started unstaking
        uint256 unstakeStartTime;  // keep track of timestamp at which startUnstakeProcess function is called
        uint256 minTimeToWithdraw; // keep track of minTimeToWithdraw when startUnstakeProcess function is called
        uint256 gTokenToBeUnstaked; //keep track of how much  gToken the user has given for unstaking
        uint256 unstakingRewardDebt; // keep track of the reward debt lost due to unstaking
    }

    struct PoolInfo {
        IERC20 want; // Address of the want/lp token.
        uint96 allocPoint; //  Number of allocation points assigned to this pool. It determines the share of GAMMA distributed to this pool in the farm.
        uint256 lastRewardBlock; // Block number at which the last GAMMA distribution occurred.
        uint256 accGAMMAPerShare; // Accumulated GAMMA per share, times 1e12.
        address strat; // Address of the strategy that will store or compound want tokens.
        uint256 accGAMMMAPerFactorPerShare; // Boosted GAMMA rewards to be provided to users per user factor
        uint256 gammaRewardBoostPercentage; // Portion of the total GAMMA rewards to be rewarded to user as boosted rewards
        uint256 totalFactor; // Total factor of the pool. This is the sum of the user factors of all the users in the pool.
        bool isInfinity; // whether the pool is any infinity pool
        address gToken; // address of gToken associated with the infinity pool
        address iToken; // address of iToken associated with the infinity pool
        bool hasStratRewards; // whether the pool has strategy generating extra rewards inaddition to the farm rewards
        bool isBoosted; // whether the pool has boosted rewards
    }

    address public GAMMA; 
    address public iGammaAddress; // To handle GAMMA infinity vault separately from all other pools
    uint256 public iGammaPid; 
    uint256 public minTimeToWithdraw;
    uint256 public minTimeToWithdrawUL;
    uint256 public totalAllocPoint; // Total allocation points. Must be the sum of all allocation points in all pools.

    bool public isDrip; // Specifies if rewards from reservor are turned on for the farm
    bool public autoStakeGamma; // controls whether to stake users rewards to stake to infinity vault or transfer to user

    PoolInfo[] public poolInfo; // Info of each pool.
    mapping(uint256 => mapping(address => UserInfo)) public userInfo; // Info of each user that stakes tokens.
    mapping(address => uint256) internal usersClaimableGamma;
    mapping(address => uint256[]) public usersPoolList;
    // Controls whether a given address/user can deposit to gamma infinity vault on behalf of someone else 
    mapping(address => bool) public authorized; 

    event Deposit(address indexed user, uint256 indexed pid, uint256 amount);
    event Withdraw(address indexed user, uint256 indexed pid, uint256 amount);
    event EmergencyWithdraw(address indexed user, uint256 indexed pid, uint256 amount);
    event StartUnstake(address indexed sender,uint256 amount, uint256 indexed pid);
    event StopUnstake(address indexed sender, uint256 indexed pid);


    function initialize() public initializer(){
        __Ownable_init();
        __ReentrancyGuard_init();
        ReservoirAddress = Reservoir(0xdAdcA521010DCD42745F7C6007F546f190fa20D4);
        GammaTrollerAddress = GammaTroller(0x743a01cff0758fBF26a39BD6891Ddc04B7F8778d);
        GAMMA = 0xc89B7b2e240f632Ce97eaD34aD72AD04dF52605D;
        minTimeToWithdraw = 21 days;
        minTimeToWithdrawUL = 365 days;
        totalAllocPoint = 0; // Total allocation points. Must be the sum of all allocation points in all pools.
        isDrip = true;
        autoStakeGamma = true;
    }

    function poolLength() external view returns (uint256) {
        return poolInfo.length;
    }

    // Add a new token to the pool. Can only be called by the owner.
    function add(uint96 _allocPoint, IERC20 _want, address _strat, uint _gammaRewardBoostPercentage, bool _withUpdate, bool _isInfinity, address _iToken, address _gToken, bool _hasStratRewards) external onlyOwner{
        require(_gammaRewardBoostPercentage <= 10_000, ">10k");
        // _withudpate should always be true. Made it optional as if we have too many pools, this function might fail. 
        // In that case we call massUpdatePools separately first and then call this fucntion with _withUpdate set to false.
        if (_withUpdate) {
            massUpdatePools();
        }
        uint256 lastRewardBlock = block.number;
        totalAllocPoint = totalAllocPoint + _allocPoint;
        
        PoolInfo memory poolData = PoolInfo({
            want: _want,
            allocPoint: _allocPoint,
            lastRewardBlock: lastRewardBlock,
            accGAMMAPerShare: 0,
            strat: _strat,
            accGAMMMAPerFactorPerShare: 0, // user's GAMMA share per factor 
            gammaRewardBoostPercentage: _gammaRewardBoostPercentage, // percentage of total dripped that will go for boosted rewards
            totalFactor: 0,
            isInfinity: _isInfinity,
            iToken: _iToken,
            gToken: _gToken,
            hasStratRewards: _hasStratRewards,
            isBoosted: true
        });

        poolInfo.push(poolData);
    
    }
    // Updates the given pool's GAMMA allocation point and gammaRewardBoostPercentage. Can only be called by the owner.
    function set(uint256 _pid, uint96 _allocPoint, uint _gammaRewardBoostPercentage, bool _withUpdate, bool _hasStratRewards) external onlyOwner{
        require(_gammaRewardBoostPercentage <= 10_000, ">10k");
        if (_withUpdate) {
            massUpdatePools();
        }
        totalAllocPoint = (totalAllocPoint - poolInfo[_pid].allocPoint) + _allocPoint;
        poolInfo[_pid].allocPoint = _allocPoint;
        poolInfo[_pid].gammaRewardBoostPercentage = _gammaRewardBoostPercentage;
        // This should be true for pools that has startegies generating rewards 
        poolInfo[_pid].hasStratRewards = _hasStratRewards;
    }

    /// @notice Removes pool from the boosted list
    /// @param _pid poolInfo pid that needs to be removed
    function deprecateBoostForPool(uint256 _pid) external onlyOwner {
        require(poolInfo[_pid].gammaRewardBoostPercentage == 0, "boost !0");
        
        PoolInfo storage poolData = poolInfo[_pid];
        poolData.isBoosted = false;  

    }

    /// @notice Removes pool from users pool list 
    /// @param _pid poolInfo pid that needs to be removed
    /// @param _user user for which the pid is to be removed from the list
    function _removePoolFromUsersPoolList(uint256 _pid, address _user) internal {

        uint256 userPoolListLength = usersPoolList[_user].length;
        for(uint256 i = 0; i < userPoolListLength; ++i){
            if(usersPoolList[_user][i] == _pid){
                usersPoolList[_user][i] = usersPoolList[_user][userPoolListLength - 1];
                usersPoolList[_user].pop();
                break;
            }
        }
    }

    // Return reward multiplier over the given _from to _to block.
    function getMultiplier(uint256 _from, uint256 _to) internal view returns (uint256){
        if(!isDrip){
            return 0;
        }
        return _to - _from;
    }

    // View function to calculate pending GAMMA for one pool.
    function _pendingGAMMA(uint256 _pid, address _user) internal view returns (uint256) {
        if (_pid == iGammaPid){
            return 0;
        }
        PoolInfo memory pool = poolInfo[_pid];
        UserInfo memory user = userInfo[_pid][_user];
        uint256 accGAMMAPerShare = pool.accGAMMAPerShare;
        uint256 accGAMMMAPerFactorPerShare = pool.accGAMMMAPerFactorPerShare;
        uint256 userShares = user.shares;

        uint256 sharesTotal = IStrategy(pool.strat).sharesTotal();
        if (block.number > pool.lastRewardBlock && sharesTotal != 0) {
            uint256 multiplier = getMultiplier(pool.lastRewardBlock, block.number);
            uint256 GAMMAReward = (multiplier * ReservoirAddress.farmV2DripRate() * pool.allocPoint)/totalAllocPoint;

            if(pool.hasStratRewards){
                    GAMMAReward = GAMMAReward + IStrategy(pool.strat).getStratPendingRewards();
            }
            
            accGAMMAPerShare = accGAMMAPerShare + (GAMMAReward*1e12)*(10_000 - pool.gammaRewardBoostPercentage)/(sharesTotal*10_000);

            if(pool.gammaRewardBoostPercentage != 0 && pool.totalFactor != 0){
                accGAMMMAPerFactorPerShare = accGAMMMAPerFactorPerShare + (GAMMAReward*1e12*pool.gammaRewardBoostPercentage)/(pool.totalFactor*10_000);
            }

            if(pool.isInfinity) {
                    userShares = userShares - user.iTokenToBeUnstaked;
            }
        }
        return (userShares*accGAMMAPerShare + user.factor*accGAMMMAPerFactorPerShare)/1e12 - user.rewardDebt;
    }

    // View function to see pending GAMMA on frontend for all pools.
    function pendingGAMMAAllPools(address _user) external view returns (uint256) {
        uint poolLen = usersPoolList[_user].length;
        uint totalPendingGamma;

        for(uint i = 0; i < poolLen; ++i){
            totalPendingGamma = totalPendingGamma + _pendingGAMMA(usersPoolList[_user][i], _user);
        }
        return totalPendingGamma + usersClaimableGamma[_user];
    }

    // View function to see staked Want tokens on frontend.
    function stakedWantTokens(uint256 _pid, address _user) external view returns (uint256){
        UserInfo memory user = userInfo[_pid][_user];

        (uint256 wantLockedTotal, uint256 sharesTotal) = IStrategy(poolInfo[_pid].strat).getShares();
        if (sharesTotal == 0) {
            return 0;
        }

        return user.shares*wantLockedTotal/sharesTotal;
    }

    // Update reward variables for all pools. Be careful of gas spending!
    function massUpdatePools() public{
        for (uint256 pid = 0; pid < poolInfo.length; ++pid) {
            updatePool(pid);
        }
    }

    // Update reward variables of the given pool to be up-to-date.
    function updatePool(uint256 _pid) public{
        PoolInfo storage pool = poolInfo[_pid];
        uint256 sharesTotal = IStrategy(pool.strat).sharesTotal();

        if (block.number <= pool.lastRewardBlock) {
            return;
        }
        
        if (sharesTotal == 0 || pool.allocPoint == 0) {
            pool.lastRewardBlock = block.number;
            return;
        }
        uint256 multiplier = getMultiplier(pool.lastRewardBlock, block.number);
        if (multiplier <= 0) {
            pool.lastRewardBlock = block.number;
            return;
        }
        uint256 GAMMAReward = multiplier* ReservoirAddress.farmV2DripRate() * pool.allocPoint/totalAllocPoint;

        // For GAMMA infinity vault, we send farm rewards to strategy and also collect green planet rewards in strategy 
        // and supply both to green planet right away. There are no rewards to be distributed to the user.
        if (pool.iToken == iGammaAddress){
            if(GAMMAReward > IERC20(GAMMA).balanceOf(address(this)))
                ReservoirAddress.drip();

            IERC20(GAMMA).safeIncreaseAllowance(pool.strat, GAMMAReward);
            IStrategy(pool.strat).earnGammaProfitsiGamma(GAMMAReward);

            pool.lastRewardBlock = block.number;
            return;
        }
        // For any pools that are generating extra rewards in the strategy, in addition to the farm rewards, (for example, AQUA infinity vault), 
        // we claim rewards from strategy and bring back to farm so that those rewards are included in reward distribution
        if (sharesTotal != 0 && pool.hasStratRewards) {           
            uint256 balBefore = IERC20(GAMMA).balanceOf(address(this));
            IStrategy(pool.strat).earnGammaProfits();
            uint256 balDiff = IERC20(GAMMA).balanceOf(address(this)) - balBefore;
            GAMMAReward = GAMMAReward + balDiff;
        }

        //Reserving gamma based on gammaRewardBoostRate to reward the gamma stakers only
        //So before 100% gamma went to everyone now updatePool function divides total gamma dripped
        //into two bags one for boosted rewards other for the normal rewards. 
        if(pool.totalFactor > 0){
            pool.accGAMMMAPerFactorPerShare = pool.accGAMMMAPerFactorPerShare + (GAMMAReward*1e12*pool.gammaRewardBoostPercentage)/(pool.totalFactor*10_000);
        }
        //Common gamma to distributed to everyone else
        pool.accGAMMAPerShare = pool.accGAMMAPerShare + (GAMMAReward*1e12*(10_000 - pool.gammaRewardBoostPercentage))/(sharesTotal*10_000);

        pool.lastRewardBlock = block.number;
    }

   function stakeGammaOnInfinityvault(uint _gammaAmount) internal {
        depositAuthorized(_msgSender(),_gammaAmount, false);
    }

    // Want tokens moved from user -> GammaFarm (GAMMA allocation) -> Strategy
    function deposit(uint256 _pid, uint256 _wantAmt) external nonReentrant {
        updatePool(_pid);
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_msgSender()];
        uint256 sharesAdded;
        uint256 mintAmount;

        _distributePendingRewards(user,pool);

        if (_wantAmt > 0) {

            uint256 balanceBefore = pool.want.balanceOf(address(this));
            pool.want.safeTransferFrom(address(_msgSender()), address(this),_wantAmt);
            uint256 receivedAmount = pool.want.balanceOf(address(this)) - balanceBefore;
            pool.want.safeIncreaseAllowance(pool.strat, receivedAmount);

            if(user.shares == 0){
                usersPoolList[_msgSender()].push(_pid);
            }
    
            (sharesAdded, mintAmount) = IStrategy(pool.strat).deposit(receivedAmount);

            if(pool.isInfinity) {
                IToken(pool.iToken).mint(_msgSender(), mintAmount);
            }
            if(_pid == iGammaPid) {
                uint256 useriGammaBalance = IERC20(iGammaAddress).balanceOf(_msgSender());
                updateFactorInternal(_msgSender(), useriGammaBalance);
                GammaTrollerAddress.updateFactor(_msgSender(), useriGammaBalance);
            }
        }
        _updateUserAndPool(user, pool, sharesAdded, true);
        emit Deposit(_msgSender(), _pid, _wantAmt);
    }

    // Want tokens moved from user -> GammaFarm (GAMMA allocation) -> Strategy
    // called by gammatroller
    function depositAuthorized(address _user,uint256 _wantAmt) external whenNotPaused onlyAuthorized nonReentrant {
        depositAuthorized(_user,_wantAmt, true);
    }

    // Want tokens moved from user -> GammaFarm (GAMMA allocation) -> Strategy
    // called directly by farm
    function depositAuthorized(address _user,uint256 _wantAmt, bool takeWantFromMsgsender) internal  {
        updatePool(iGammaPid);
        PoolInfo storage pool = poolInfo[iGammaPid];
        UserInfo storage user = userInfo[iGammaPid][_user];

        uint256 transferAmount;

        if (_wantAmt > 0) {
            if(takeWantFromMsgsender){
                uint256 balanceBefore = pool.want.balanceOf(address(this));
                pool.want.safeTransferFrom(address(_msgSender()), address(this),_wantAmt);
                transferAmount = pool.want.balanceOf(address(this)) - balanceBefore;
            }
            else{
                transferAmount = _wantAmt;
            }

            pool.want.safeIncreaseAllowance(pool.strat, transferAmount);

            if(user.shares == 0){
                usersPoolList[_user].push(iGammaPid);
            }
    
            (uint256 sharesAdded, uint256 mintAmount ) = IStrategy(pool.strat).deposit(transferAmount);
            user.shares = user.shares + sharesAdded;

            IToken(pool.iToken).mint(_user, mintAmount);

            uint256 useriGammaBalance = IERC20(iGammaAddress).balanceOf(_msgSender());

            updateFactorInternal(_user, useriGammaBalance);
            GammaTrollerAddress.updateFactor(_user, useriGammaBalance);

        }
        emit Deposit(_user, iGammaPid, _wantAmt);
    }

    /// @notice Updates user and pool infos
    /// @param _user The user that needs to be updated
    /// @param _pool The pool that needs to be updated
    /// @param _shares The amount that was deposited or withdrawn
    /// @param _isDeposit If the action of the user is a deposit
    function _updateUserAndPool(UserInfo storage _user, PoolInfo storage _pool, uint256 _shares, bool _isDeposit) private {
        uint256 oldShares = _user.shares;
        uint256 newShares = _isDeposit ? oldShares + _shares : oldShares - _shares;

        if (_shares != 0) {
            _user.shares = newShares;
        }

        if (_pool.iToken == iGammaAddress) {
            return;
        }

        if (_pool.isInfinity){
            _user.rewardDebt = ((newShares - _user.iTokenToBeUnstaked) * _pool.accGAMMAPerShare)/1e12;
            return;
        }
        
        uint256 oldFactor = _user.factor;
        uint256 newFactor = Math.sqrt(newShares * IERC20(iGammaAddress).balanceOf(_msgSender()));

        if (oldFactor != newFactor) {
            _user.factor = newFactor;
            _pool.totalFactor = _pool.totalFactor - oldFactor + newFactor;
        }

        _user.rewardDebt = (newShares * _pool.accGAMMAPerShare + newFactor * _pool.accGAMMMAPerFactorPerShare)/1e12;
    
    }

    /// @notice Distributes pending rewards
    /// @param _user The user that needs to get rewards
    /// @param _pool The pool in which the user receives rewards
    function _distributePendingRewards(UserInfo storage _user, PoolInfo storage _pool) private {
        
        if(_pool.iToken == iGammaAddress){
            return;
        }

        uint256 userShares = _user.shares;
        if(_pool.isInfinity){
            userShares = userShares - _user.iTokenToBeUnstaked;
        }
        
        uint256 pending = (userShares*_pool.accGAMMAPerShare + _user.factor * _pool.accGAMMMAPerFactorPerShare)/1e12 - _user.rewardDebt;
        if (pending > 0) {
            usersClaimableGamma[_msgSender()] = usersClaimableGamma[_msgSender()] + pending;
        }

    }
    
    function updateFactorByItoken(address _sender, address _recepient, uint256  _amount) external {
        require(_msgSender() == iGammaAddress, "!iGAMMA");

        UserInfo storage sender = userInfo[iGammaPid][_sender];
        UserInfo storage recepient = userInfo[iGammaPid][_recepient];

        sender.shares = sender.shares - _amount;
        recepient.shares = recepient.shares + _amount;

        uint256 senderiGammaBalance = IERC20(iGammaAddress).balanceOf(_sender);
        uint256 recepientiGammaBalance = IERC20(iGammaAddress).balanceOf(_recepient);

        updateFactorInternal(_sender, senderiGammaBalance);
        updateFactorInternal(_recepient, recepientiGammaBalance);

        GammaTrollerAddress.updateFactor(_sender, senderiGammaBalance);
        GammaTrollerAddress.updateFactor(_recepient, recepientiGammaBalance);
    }

    /// @notice Updates factor after after a iGamma token operation.
    /// This function needs to be called by the iGamma contract after
    /// every mint / burn.
    /// @param _user The users address we are updating
    function updateFactorInternal(address _user, uint256 _iGammaBalance) internal {

        uint256 len = usersPoolList[_user].length;

        for(uint256 i = 0; i < len; ++i) {
            uint256 pid = usersPoolList[_user][i];
            PoolInfo storage pool = poolInfo[pid];
            UserInfo storage user = userInfo[pid][_user];

            // Skip if infinity pool or unboosted Pool
            if (pool.isInfinity || !pool.isBoosted) {
                continue;
            }

            uint256 shares = user.shares;
            // updating pool so that we can distribute rewards based on current user factor
            updatePool(pid);

            // distributing rewards to user based on the current user factor before it gets updated
            uint256 oldFactor = user.factor;
            (uint256 accGAMMAPerShare, uint256 accGAMMAPerFactorPerShare) = (pool.accGAMMAPerShare, pool.accGAMMMAPerFactorPerShare);
            usersClaimableGamma[_user] = usersClaimableGamma[_user] + (shares*accGAMMAPerShare + oldFactor*accGAMMAPerFactorPerShare)/1e12 - user.rewardDebt;

            // Update user factor based on new iGamma Balance
            uint256 newFactor = Math.sqrt(shares * _iGammaBalance);
            user.factor = newFactor;
            pool.totalFactor = pool.totalFactor - oldFactor + newFactor;

            // update reward debt based on the latest distribution
            user.rewardDebt = (shares*accGAMMAPerShare + newFactor*accGAMMAPerFactorPerShare)/1e12;
        }
    }

   // Withdraw LP tokens from Strategy.
    function withdraw(uint256 _pid, uint256 _wantAmt) external nonReentrant {    
        PoolInfo storage pool = poolInfo[_pid];
        require(!pool.isInfinity, "!infinity");
        updatePool(_pid);
        UserInfo storage user = userInfo[_pid][_msgSender()];

        (uint256 wantLockedTotal, uint256 sharesTotal) = IStrategy(pool.strat).getShares();
        uint256 toBeSentToUser;
        uint256 sharesRemoved;

        require(user.shares > 0, "user.shares 0");
        require(sharesTotal > 0, "sharesTotal 0");

        _distributePendingRewards(user,pool);

        // Withdraw want tokens
        uint256 amount = user.shares*wantLockedTotal/sharesTotal;
        if (_wantAmt > amount) {
            _wantAmt = amount;
        }
        if (_wantAmt > 0) {
            (sharesRemoved, toBeSentToUser) = IStrategy(pool.strat).withdraw(_wantAmt);  
            _updateUserAndPool(user, pool, sharesRemoved, false);

             if(user.shares == 0)
                _removePoolFromUsersPoolList(_pid, _msgSender());
                pool.want.safeTransfer(address(_msgSender()), toBeSentToUser);
        }else{
            // upate rewarddebt for the reward distribution done at the beginning of the function
            user.rewardDebt = (user.shares * pool.accGAMMAPerShare + user.factor * pool.accGAMMMAPerFactorPerShare)/1e12;
        }
        emit Withdraw(_msgSender(), _pid, sharesRemoved);
    }

    function claimAllPoolsPendingGamma() external nonReentrant {
        uint256 length = usersPoolList[_msgSender()].length;
        
        for(uint256 i = 0; i < length; ++i) {
            uint256 pid = usersPoolList[_msgSender()][i];
            if(pid == iGammaPid) {
                continue;
            }

            updatePool(pid);
            PoolInfo storage pool = poolInfo[pid];
            UserInfo storage user = userInfo[pid][_msgSender()];
            
            _distributePendingRewards(user,pool);
            user.rewardDebt = (user.shares*pool.accGAMMAPerShare + user.factor*pool.accGAMMMAPerFactorPerShare)/1e12;
        }

        uint256 totalPending = usersClaimableGamma[_msgSender()];
        usersClaimableGamma[_msgSender()] = 0;

        if(totalPending > IERC20(GAMMA).balanceOf(address(this)))
            ReservoirAddress.drip();

        if(totalPending > 0) {
            if(autoStakeGamma)
                stakeGammaOnInfinityvault(totalPending);
            else
                IERC20(GAMMA).transfer(_msgSender(), totalPending);
        }
    }

    // Withdraw without caring about rewards. EMERGENCY ONLY.
    function emergencyWithdraw(uint256 _pid) external nonReentrant {
        PoolInfo storage pool = poolInfo[_pid];
        require(!pool.isInfinity , "infinity");
        UserInfo storage user = userInfo[_pid][_msgSender()];

        (uint256 wantLockedTotal, uint256 sharesTotal) = IStrategy(pool.strat).getShares();
       
        uint256 amount = user.shares*wantLockedTotal/sharesTotal;
        
        uint256 wantBalBefore = IERC20(pool.want).balanceOf(address(this));
        IStrategy(pool.strat).emergencyWithdraw(amount); 
        uint256 wantBalAfter = IERC20(pool.want).balanceOf(address(this));

        uint256 wantBal = wantBalAfter - wantBalBefore;
        if (wantBal < amount) {
            amount = wantBal;
        }

        user.shares = 0;
        user.rewardDebt = 0;
        pool.totalFactor = pool.totalFactor - user.factor;    
        user.factor = 0;
    
        _removePoolFromUsersPoolList(_pid, _msgSender());

        pool.want.safeTransfer(address(_msgSender()), amount);

        emit EmergencyWithdraw(_msgSender(), _pid, amount);
    }

    function inCaseTokensGetStuck(address _token, uint256 _amount) external onlyOwner{
        require(_token != GAMMA, "!safe");
        IERC20(_token).safeTransfer(_msgSender(), _amount);
    }

    function setReservoir(Reservoir _reservoir) external onlyOwner {
        ReservoirAddress = _reservoir;
    }

    function setGammaTroller(GammaTroller _gammaTroller) external onlyOwner {
        GammaTrollerAddress = _gammaTroller;
    }
    
    function changeIsDrip(bool _dripStatus, bool _withUpdate) external onlyOwner {
        require(_dripStatus != isDrip,"same status given");
        if (_withUpdate)
            massUpdatePools();

        isDrip = _dripStatus;        
    }

    function setAutoStakeGamma(bool _isAutoStakeGamma) external onlyOwner {
        autoStakeGamma = _isAutoStakeGamma;
    }

    
    function getUserITokenToBeUnstaked(address account, uint256 pid) external view returns(uint256) {
        return userInfo[pid][account].iTokenToBeUnstaked;
    }

    /**
     * @notice Starts unstaking phase for caller for specific iToken of user
     * @param unstakeAmount: number of tokens given for unstaking 
     */
    function startUnstakeProcess(uint256 _pid, uint256 unstakeAmount) external notContract{
        PoolInfo storage pool = poolInfo[_pid];
        require(pool.isInfinity, "!infinity");
        UserInfo storage user = userInfo[_pid][_msgSender()];
        
        // get rewards up to date to be claimed for AQUA infinity vault, and autocompound rewards in strategy for GAMMA infinity vault
        updatePool(_pid); 
        // distribute rewards so far minus the rewards for already unstaking amount to the user
        _distributePendingRewards(user,pool);

        uint256 unstakegTokenAmount =  unstakeAmount * 1e18 / GToken(pool.gToken).exchangeRateStored();

        uint useriTokenBalance = IERC20(pool.iToken).balanceOf(_msgSender());

        uint256 exchangeRate = IStrategy(pool.strat).iTokenExchangeRate();

        uint256 unstakeItokenAmount = (unstakegTokenAmount * 1e18) / exchangeRate;
                 
        require(unstakeItokenAmount > 0,"!UnstakeiTokenAmount <= 0");
        
        //iTokens user has already given for unstake
        uint256 iTokenAlreadyGivenForUnstake = user.iTokenToBeUnstaked; 

        require(useriTokenBalance > iTokenAlreadyGivenForUnstake,
        "useriTokenBalance <= iTokenAlreadyGivenForUnstake");

        //amount of gToken user can unstake more
        uint256 iTokenUserCanUnstakeMore = useriTokenBalance - iTokenAlreadyGivenForUnstake;

        if(unstakeItokenAmount > iTokenUserCanUnstakeMore) {
            unstakeItokenAmount = iTokenUserCanUnstakeMore;
            unstakegTokenAmount = (iTokenUserCanUnstakeMore * exchangeRate) / 1e18;
        }

        user.iTokenToBeUnstaked += unstakeItokenAmount;
        user.unstakeStartTime = block.number;
        user.minTimeToWithdraw = minTimeToWithdraw;
        user.gTokenToBeUnstaked += unstakegTokenAmount;

        if(pool.iToken != iGammaAddress) {
            user.rewardDebt = (((useriTokenBalance - user.iTokenToBeUnstaked) * pool.accGAMMAPerShare) / (1e12));
            user.unstakingRewardDebt += (((unstakeItokenAmount) * pool.accGAMMAPerShare) / (1e12));
        }
        
        emit StartUnstake(_msgSender(),user.iTokenToBeUnstaked, _pid);
    }

    /**
     * @notice stops unstaking phase for caller if unstake phase is already started
     */
    function stopUnstakeProcess(uint256 _pid) external notContract {
       /*
        * Stop ongoing unstake process if present for caller 
        */
        stopUnstakeProcessInternal(_msgSender(), _pid);
    }

    /**
     * @notice stops unstaking phase for caller if unstake phase is already started
     */
    function stopUnstakeProcessByIToken(address account, uint256 _pid) external notContract {
        PoolInfo storage pool = poolInfo[_pid];
        require((pool.iToken == _msgSender()),"unauthorised");
        stopUnstakeProcessInternal(account, _pid);
    }

    function stopUnstakeProcessInternal(address userAddress, uint256 _pid) internal {
       /*
        * Stop ongoing unstake process if present for caller 
        */
        UserInfo storage user = userInfo[_pid][userAddress];
        require(user.iTokenToBeUnstaked > 0 , "Nothing's unstaking!");
        user.rewardDebt += user.unstakingRewardDebt;
        user.unstakingRewardDebt = 0;
        user.iTokenToBeUnstaked = 0;
        user.unstakeStartTime = 0;
        user.minTimeToWithdraw = 0;
        user.gTokenToBeUnstaked = 0;
        emit StopUnstake(userAddress, _pid);
    }

    function unstakeInstantly(uint256 _pid, uint256 unstakeAmount ) external notContract{
        PoolInfo storage pool = poolInfo[_pid];
        require(pool.isInfinity, "!infinity");
        UserInfo storage user = userInfo[_pid][_msgSender()];

        updatePool(_pid); 
        if(pool.iToken != iGammaAddress){
            _distributePendingRewards(user,pool);
        }

        uint256 itokens;
        uint256 useriTokenBalance = IERC20(pool.iToken).balanceOf(_msgSender());
      
        uint256 currentAmount =  unstakeAmount * 1e18 / GToken(pool.gToken).exchangeRateStored();
        // we need to store this as exchange rate will change after withdrawing gTokens from strategy. 
        uint256 exchange_rate_before_withdraw  = IStrategy(pool.strat).iTokenExchangeRate();
    
        uint256 sharesRemoved;
        uint256 wantAmtTobeSentToUser;
        uint256 stratWantTokensRemoved;
        if (currentAmount > 0) {
            (sharesRemoved, wantAmtTobeSentToUser, stratWantTokensRemoved ) = IStrategy(pool.strat).unstake(currentAmount, true);
        }

        itokens = (stratWantTokensRemoved * 1e18) / exchange_rate_before_withdraw;

        require(itokens <= useriTokenBalance - user.iTokenToBeUnstaked,
            "not enough itokens");
        
        IToken(pool.iToken).burn(_msgSender(), itokens);
        if(pool.iToken == iGammaAddress ) {
            uint256 useriGammaBalance = IERC20(iGammaAddress).balanceOf(_msgSender());
            updateFactorInternal(_msgSender(), useriGammaBalance);
            GammaTrollerAddress.updateFactor(_msgSender(), useriGammaBalance);
        }

        _updateUserAndPool(user, pool, sharesRemoved, false);
        if(user.shares == 0)
            _removePoolFromUsersPoolList(_pid, _msgSender());

        pool.want.safeTransfer(_msgSender(), wantAmtTobeSentToUser);
        emit Withdraw(_msgSender(), _pid, stratWantTokensRemoved);    
    }

    function unstakeAfterMinWithdrawTime(uint256 _pid) external notContract{        
        PoolInfo storage pool = poolInfo[_pid];
        require(pool.isInfinity, "!infinity");
        UserInfo storage user = userInfo[_pid][_msgSender()];

        updatePool(_pid); 
        if(pool.iToken != iGammaAddress){
            _distributePendingRewards(user,pool);
        }
       
        uint256 useriTokenBalance = IERC20(pool.iToken).balanceOf(_msgSender());
        uint256 itokens = user.iTokenToBeUnstaked;
        uint256 totalItokensUnstaking = user.iTokenToBeUnstaked;

        require(itokens > 0,"itokens <= 0");
        require(user.unstakeStartTime + user.minTimeToWithdraw < block.timestamp,
        "too early");

        uint256 currentAmount = user.gTokenToBeUnstaked;
        uint256 exchange_rate_before_withdraw  = (currentAmount * 1e18) / itokens;
        
        uint256 sharesRemoved;
        uint256 wantAmtTobeSentToUser;
        uint256 stratWantTokensRemoved;
        if (currentAmount > 0) {
            (sharesRemoved, wantAmtTobeSentToUser, stratWantTokensRemoved ) = IStrategy(pool.strat).unstake(currentAmount, false);
        }

        itokens = (stratWantTokensRemoved * 1e18) / exchange_rate_before_withdraw;
        require(itokens <= useriTokenBalance,"Withdraw amount exceeds balance");

        IToken(pool.iToken).burn(_msgSender(), itokens);
        if(pool.iToken == iGammaAddress ) {
            uint256 useriGammaBalance = IERC20(iGammaAddress).balanceOf(_msgSender());
            updateFactorInternal(_msgSender(), useriGammaBalance);
            GammaTrollerAddress.updateFactor(_msgSender(), useriGammaBalance);
        }

        if(user.gTokenToBeUnstaked > stratWantTokensRemoved && user.iTokenToBeUnstaked > itokens) {
       	   user.gTokenToBeUnstaked -= stratWantTokensRemoved;
           user.iTokenToBeUnstaked -= itokens;
	    }
        else {
       	   user.gTokenToBeUnstaked = 0;
           user.iTokenToBeUnstaked = 0;
       	   user.unstakeStartTime = 0;
       	   user.minTimeToWithdraw = 0;
        }

        _updateUserAndPool(user, pool, sharesRemoved, false);
        if(user.shares == 0)
            _removePoolFromUsersPoolList(_pid, _msgSender());

        pool.want.safeTransfer(_msgSender(), wantAmtTobeSentToUser);

        uint256 lostRewards = ((itokens * pool.accGAMMAPerShare) / (1e12)) - ((user.unstakingRewardDebt* itokens)/totalItokensUnstaking);
        if (pool.iToken == iGammaAddress){
            IERC20(GAMMA).safeIncreaseAllowance(pool.strat, lostRewards);
            IStrategy(pool.strat).earnGammaProfitsiGamma(lostRewards);
        }
        else{
            pool.accGAMMAPerShare = pool.accGAMMAPerShare + lostRewards;
        }

        if(pool.iToken != iGammaAddress){
            user.unstakingRewardDebt = (user.iTokenToBeUnstaked * ((user.unstakingRewardDebt * 1e12)/totalItokensUnstaking)) / (1e12);        
        }   
        emit Withdraw(_msgSender(), _pid, stratWantTokensRemoved);
    }

    function updateInfinityVaultVariables(uint256 _iGammaPid, address _iGammaAddress) external onlyOwner {
        iGammaPid = _iGammaPid;
        iGammaAddress =  _iGammaAddress;
    }

    function _isContract(address addr) internal view returns (bool) {
        uint256 size;
        assembly {
            size := extcodesize(addr)
        }
        return size > 0;
    }

    function setMinTimeToWithdraw(uint256 newMinTimeToWithdraw) external onlyOwner{
        require(newMinTimeToWithdraw <= minTimeToWithdrawUL, "too high");
        minTimeToWithdraw = newMinTimeToWithdraw;
    }

    /**
    * @notice this will toggle vault authorized status 
    *         in the contract it is called by owner address only
    */
    function updateAuthorizedAddress(address _vault) external onlyOwner {
        require(_vault != address(0),"address(0)");
        authorized[_vault] = !authorized[_vault];
    }

    function _notContract() internal view {
        require(!_isContract(_msgSender()), "contract");
        require(_msgSender() == tx.origin, "proxy contract");
    }

    /**
    * @notice Checks if the _msgSender() is a contract or a proxy
    */
    modifier notContract() {
        _notContract();
        _;
    }

    function _onlyAuthorized() internal view {
        require(authorized[_msgSender()] == true, "!authorized");
    }

    /**
    * @notice Checks if the _msgSender() is authorized or not
    */
    modifier onlyAuthorized() {
        _onlyAuthorized();
        _;
    }
}

pragma solidity ^0.8.15;

// SPDX-License-Identifier: MIT


library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}


/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since proxied contracts do not make use of a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * The initialization functions use a version number. Once a version number is used, it is consumed and cannot be
 * reused. This mechanism prevents re-execution of each "step" but allows the creation of new initialization steps in
 * case an upgrade adds a module that needs to be initialized.
 *
 * For example:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * contract MyToken is ERC20Upgradeable {
 *     function initialize() initializer public {
 *         __ERC20_init("MyToken", "MTK");
 *     }
 * }
 * contract MyTokenV2 is MyToken, ERC20PermitUpgradeable {
 *     function initializeV2() reinitializer(2) public {
 *         __ERC20Permit_init("MyToken");
 *     }
 * }
 * ```
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To prevent the implementation contract from being used, you should invoke
 * the {_disableInitializers} function in the constructor to automatically lock it when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() {
 *     _disableInitializers();
 * }
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     * @custom:oz-retyped-from bool
     */
    uint8 private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Triggered when the contract has been initialized or reinitialized.
     */
    event Initialized(uint8 version);

    /**
     * @dev A modifier that defines a protected initializer function that can be invoked at most once. In its scope,
     * `onlyInitializing` functions can be used to initialize parent contracts. Equivalent to `reinitializer(1)`.
     */
    modifier initializer() {
        bool isTopLevelCall = _setInitializedVersion(1);
        if (isTopLevelCall) {
            _initializing = true;
        }
        _;
        if (isTopLevelCall) {
            _initializing = false;
            emit Initialized(1);
        }
    }

    /**
     * @dev A modifier that defines a protected reinitializer function that can be invoked at most once, and only if the
     * contract hasn't been initialized to a greater version before. In its scope, `onlyInitializing` functions can be
     * used to initialize parent contracts.
     *
     * `initializer` is equivalent to `reinitializer(1)`, so a reinitializer may be used after the original
     * initialization step. This is essential to configure modules that are added through upgrades and that require
     * initialization.
     *
     * Note that versions can jump in increments greater than 1; this implies that if multiple reinitializers coexist in
     * a contract, executing them in the right order is up to the developer or operator.
     */
    modifier reinitializer(uint8 version) {
        bool isTopLevelCall = _setInitializedVersion(version);
        if (isTopLevelCall) {
            _initializing = true;
        }
        _;
        if (isTopLevelCall) {
            _initializing = false;
            emit Initialized(version);
        }
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} and {reinitializer} modifiers, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    /**
     * @dev Locks the contract, preventing any future reinitialization. This cannot be part of an initializer call.
     * Calling this in the constructor of a contract will prevent that contract from being initialized or reinitialized
     * to any version. It is recommended to use this to lock implementation contracts that are designed to be called
     * through proxies.
     */
    function _disableInitializers() internal virtual {
        _setInitializedVersion(type(uint8).max);
    }

    function _setInitializedVersion(uint8 version) private returns (bool) {
        // If the contract is initializing we ignore whether _initialized is set in order to support multiple
        // inheritance patterns, but we only do this in the context of a constructor, and for the lowest level
        // of initializers, because in other contexts the contract may have been reentered.
        if (_initializing) {
            require(
                version == 1 && !AddressUpgradeable.isContract(address(this)),
                "Initializable: contract is already initialized"
            );
            return false;
        } else {
            require(_initialized < version, "Initializable: contract is already initialized");
            _initialized = version;
            return true;
        }
    }
}

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuardUpgradeable is Initializable {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    function __ReentrancyGuard_init() internal onlyInitializing {
        __ReentrancyGuard_init_unchained();
    }

    function __ReentrancyGuard_init_unchained() internal onlyInitializing {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[49] private __gap;
}


/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
 /**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal onlyInitializing {
    }

    function __Context_init_unchained() internal onlyInitializing {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[50] private __gap;
}

abstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    function __Ownable_init() internal onlyInitializing {
        __Ownable_init_unchained();
    }

    function __Ownable_init_unchained() internal onlyInitializing {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[49] private __gap;
}


/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract PausableUpgradeable is Initializable, ContextUpgradeable {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    function __Pausable_init() internal onlyInitializing {
        __Pausable_init_unchained();
    }

    function __Pausable_init_unchained() internal onlyInitializing {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        require(!paused(), "Pausable: paused");
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        require(paused(), "Pausable: not paused");
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[49] private __gap;
}

library Math {

    // babylonian method (https://en.wikipedia.org/wiki/Methods_of_computing_square_roots#Babylonian_method)
    function sqrt(uint256 y) internal pure returns (uint256 z) {
        if (y > 3) {
            z = y;
            uint x = y / 2 + 1;
            while (x < z) {
                z = x;
                x = (y / x + x) / 2;
            }
        } else if (y != 0) {
            z = 1;
        }
    }
}

interface IERC20 {

    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender,uint256 value);
}

library SafeERC20 {

    using AddressUpgradeable for address;

    function safeTransfer(IERC20 token, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector,spender,newAllowance));
    }

    function _callOptionalReturn(IERC20 token, bytes memory data) private {

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}
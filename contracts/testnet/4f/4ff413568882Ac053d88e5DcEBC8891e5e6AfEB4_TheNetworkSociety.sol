/** 

    @@@@@@@  @@@  @@@  @@@@@@@@     @@@  @@@  @@@@@@@@  @@@@@@@  @@@  @@@  @@@   @@@@@@   @@@@@@@   @@@  @@@      @@@@@@    @@@@@@    @@@@@@@  @@@  @@@@@@@@  @@@@@@@  @@@ @@@  
    @@@@@@@  @@@  @@@  @@@@@@@@     @@@@ @@@  @@@@@@@@  @@@@@@@  @@@  @@@  @@@  @@@@@@@@  @@@@@@@@  @@@  @@@     @@@@@@@   @@@@@@@@  @@@@@@@@  @@@  @@@@@@@@  @@@@@@@  @@@ @@@  
    @@!    @@!  @@@  @@!          @@[email protected][email protected]@@  @@!         @@!    @@!  @@!  @@!  @@!  @@@  @@!  @@@  @@!  [email protected]@     [email protected]@       @@!  @@@  [email protected]@       @@!  @@!         @@!    @@! [email protected]@  
    [email protected]!    [email protected]!  @[email protected] [email protected]!          [email protected][email protected][email protected]!  [email protected]!         [email protected]!    [email protected]!  [email protected]!  [email protected]!  [email protected]!  @[email protected] [email protected]!  @[email protected] [email protected]!  @!!     [email protected]!       [email protected]!  @[email protected] [email protected]!       [email protected]!  [email protected]!         [email protected]!    [email protected]! @!!  
    @!!    @[email protected][email protected][email protected]!  @!!!:!       @[email protected] [email protected]!  @!!!:!      @!!    @!!  [email protected]  @[email protected]  @[email protected] [email protected]!  @[email protected][email protected]!   @[email protected]@[email protected]!      [email protected]@!!    @[email protected] [email protected]!  [email protected]!       [email protected]  @!!!:!      @!!     [email protected][email protected]!   
    !!!    [email protected]!!!!  !!!!!:       [email protected]!  !!!  !!!!!:      !!!    [email protected]!  !!!  [email protected]!  [email protected]!  !!!  [email protected][email protected]!    [email protected]!!!        [email protected]!!!   [email protected]!  !!!  !!!       !!!  !!!!!:      !!!      @!!!   
    !!:    !!:  !!!  !!:          !!:  !!!  !!:         !!:    !!:  !!:  !!:  !!:  !!!  !!: :!!   !!: :!!           !:!  !!:  !!!  :!!       !!:  !!:         !!:      !!:    
    :!:    :!:  !:!  :!:          :!:  !:!  :!:         :!:    :!:  :!:  :!:  :!:  !:!  :!:  !:!  :!:  !:!         !:!   :!:  !:!  :!:       :!:  :!:         :!:      :!:    
    ::    ::   :::   :: ::::      ::   ::   :: ::::     ::     :::: :: :::   ::::: ::  ::   :::   ::  :::     :::: ::   ::::: ::   ::: :::   ::   :: ::::     ::       ::    
    :      :   : :  : :: ::      ::    :   : :: ::      :       :: :  : :     : :  :    :   : :   :   :::     :: : :     : :  :    :: :: :  :    : :: ::      :        :     
      
      Variable Daily Yield starts at 1% up to 3%:
    - Compound action every 24 hours will give the investor an additional 0.1% for their current yield which can increase up to 3%
        - Compound is not mandatory, but compounding will increase the investors daily yield.
        - There is a 2% sustainability tax every compound in which that 2% will stay in the contract.
    - Every withdraw/sell action, will have a 0.5% deduction to the current user yield that will start at 1.6%.
        - users current daily yield is between 1% to 1.5%, Then when the user sell/withdraw their dividends the daily yield will go back to 1%
        - users current daily yield is 1.6% and above, then yield will decrease 0.5% (i.e user current yield is 3%, after sell/withdraw yield will decrease to 2.5%)
    - User can earn 350% of his including real invested amount. (ie. deposited = 100 ether, max payout = 350 ether)
    
    - Variable Withdraw Tax:
        Users will have less withdraw tax depending on their consecutive compound count/days
        Default Sustainability Tax : 10% 
        Compound Count is within  1 to 7 Days: 10%
        Compound Count is within 7 to 14 Days: 8%
        Compound Count is within 14 to 21 Days: 6%
        Compound Count is within 21 to 28 Days: 4%
        Compound Count is 28 Days and higher: 2%
    * Every sell/withdraw the compound count will be set back to 0.  

    4% Referral Bonus:
    - 2% goes to the Referrer and be added to the users real deposit.
    - 2% goes to the Referee and be added to the users real deposit.
    - Referrals will be "auto re-invested" to ensure the sustainability of the contract and for investors to increase potential profit over time.

    Sponsor Setup:
    - If user used a referral link that is not part in to the network society, the contract will automatically setup a network leader on the users behalf.
    - Referral links will only work if the referrer is invested in the network.
    - Users can build their own network once successfully deposited in the protocol.

    Max reward accumulation of 48 Hours.
    - If no action is done within 48 hours, reward accumulation will stop and will only reset if a deposit, withdraw or compound is done.
    
    Daily Top Investors and Daily Top Referrer:
    - 5% of each deposit will be put into the rewards pool of each event, which is capped to 1000 ether per round.
    - event runs every 24 hours.
    - 5 winners for each pool will share the pool 1st place 30%, 2nd place 25%, 3rd place 20%, 4th place 15%, 5th place 10%. 
    - Reward goes directly to user as direct deposit instead of making it withdrawable/claimable.
      I.E Rewards Distribution is max pool size is reached:
      1st place: 300 ether
      2nd place: 250 ether
      3rd place: 200 ether
      4th place: 150 ether
      5th place: 100 ether

    Last Deposit Rewards:
    - 5% of each deposit will be put into the rewards pool for each event, which is capped to 2000 ether per round.
    - If there is no new deposit after the last deposit after 2 hours, the last deposit address will win the rewards pool which is capped to 2000 ether per round.
    
    Auto Compound Feature:
    - Users will deposit the current average gas fee per compound.
    - Users will have an option to disable it during the cycle and will be allowed to withdraw the remaining deposited BNB.
    - Enabled auto-compound should finish the cycle, users will not be able to add days in between.
    - Users will need to trigger disable and withdraw and and do enable and deposit BNB again to re-enabled the auto-compound feature.

    Network and Individual Airdrop.
    - Users can airdrop invested addresses which will go directly as users direct deposit instead of making it withdrawable/claimable.
    - Network Leaders can airdrop x amount of token to their members. amount(less airdrop tax) * no. of network members.  
    - Network Leaders can airdrop individually for addresses under their network.
**/

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/utils/math/SafeMath.sol";
import "@openzeppelin/contracts/utils/Context.sol";
import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract TheNetworkSociety is Context, Ownable {
    using SafeMath for uint256;
    using SafeMath for uint8;

    struct User {
        address sponsor; //network sponsor
        uint256 networkInvites; //invite count
        uint256 totalDepositAmount; //invested + compounded
        uint256 totalDepositPayouts; //payouts
        uint256 totalDirectDeposits; //invested event payouts + airdrops are considered real deposits
        uint256 incomeCompounded; //compounded
        uint256 totalNetworkInvitesDeposit; //total invested by invites
        uint256 checkpoint; //user action checkpoint
        uint256 userYieldPercentage; //user personal yield.
        uint256 userCompoundCount; //user compound count record
    }

    //bonus record library, records are for statistics only
    struct UserBonusStatistics {
        uint256 directBonus; //referral / referee bonus
        uint256 lastDepositBonus; //last deposit bonus if address has won
        uint256 topDepositBonus; //top deposit bonus if address has won
        uint256 topReferrerBonus; //top referrer bonus if address has won
    }

    //airdrop record library
    struct Airdrop {
        uint256 airdropSent; //total airdrop sent
        uint256 airdropReceived; //total airdrop received
        uint256 lastAirdropReceivedTime; //last airdrop received timestamp
        uint256 lastAirdropSentTime; // last airdrop sent timestamp
    }

    //Networks record library
    struct Network {
        address[] members; //network invites address
        address owner; //network owner address
        uint256 id; //network id 
        uint256 createTime; //network creation timestamp
    }

    //Networks record info library
    struct NetworkInfo {
        uint256 id; //network id
        bool exists; //if exist or not, if true, then use current id, else create a new one and designate an id.
    }
    
    //Mappings
    mapping(address => User) public users;
    mapping(uint256 => Network) public networks;
    mapping(address => Airdrop) private airdrops;
    mapping(uint8 => address) private topDepositPool;
    mapping(uint8 => address) private topReferrerPool;
    mapping(address => NetworkInfo[]) private userNetworks;
    mapping(address => NetworkInfo) private userNetworkInfo;
    mapping(address => UserBonusStatistics) private userBonusStatistics; 
    mapping(uint256 => mapping(address => uint256)) private userTopDepositList;
    mapping(uint256 => mapping(address => uint256)) private userTopReferrerList;
    
    //variable 
    uint256 private constant marketTax = 10;
    uint256 private constant projectTax = 45;
    uint256 private constant developmentTax = 45;
    uint256 private constant compoundTax = 20; //2% stays in the contract

    uint256 private constant userMaxPayout = 3650; //365%
    uint256 private constant maxCompoundMultiplier = 5; //5x of real investment.

    uint256 private constant eventPercent = 50; //5% of each deposit.
    uint256 private constant referralPercent = 20; //2% for referrer and referee total of 4% per deposit.
    uint256 private constant maxYieldPercent = 30; //3%
    uint256 private constant baseYieldPercent = 10; //1%
    uint256 private constant yieldDecreasePerWithdraw = 5; //0.5% yield decrease per withdraw
    uint256 private constant percentDivider = 1000;

    uint256 private constant depositMinimum = 50 * (10 ** 18);  
    uint256 private constant depositMaximum = 50000 * (10 ** 18); 
    uint256 private constant airdropMinimum = 10 * (10 ** 18);
    uint256 private constant maxSharedRewardsPool = 2000 * (10 ** 18);
    uint256 private constant maxIndividualRewardsPool = 1000 * (10 ** 18);

    //time steps
    uint256 private timeStep = 86400;  //1 days
    uint256 private eventTimeStep = 86400; //1 days
    uint256 private lastDepositTimeStep = 7200; //2 hours
    uint256 private maxRewardAccumulationStep = 172800; //2 days

    //last deposit 
    address public potentialLastDepositWinner; //current last deposit address.
    uint256 public lastDepositLastDrawAction; //event start time
    uint256 public lastDepositPoolBalance; //current pool balance
    uint256 public currentLastBuyRound = 1; //current round

    //top referrer
    uint256 public topReferrerLastDrawAction; //event start time
    uint256 public topReferrerPoolBalance; //current pool balance
    uint256 public topReferrerPoolCycle = 1; //current round

    //top deposit
    uint256 public topDepositLastDrawAction; //event start time
    uint256 public topDepositPoolBalance; //current pool balance
    uint256 public topDepositPoolCycle = 1; //current round

    //project statistics
    uint256 private totalUsers; //total users invested. 
    uint256 private totalDeposited; //total amount deposited into the protocol
    uint256 private totalWithdrawn; //total amount withdrawn in the protocol
    uint256 private totalCompounded; //total amount compounded only from investors.
    uint256 private totalAirdrops; //total airdrops sent by network leaders.
    uint256 private totalNetworksCreated; //total number of networks created in the protocol.

    //arrays
    uint8[] private daysCount = [7, 14, 21, 28]; 
    uint8[] private variableTax = [20, 40, 60, 80, 100];
    uint8[] private topDepositBonuses = [30, 25, 20, 15, 10];
    uint8[] private topReferrerPoolBonuses = [30, 25, 20, 15, 10];

    //enablers
    bool private autoCompoundEnabled;
    bool private networkInitialized;
    bool private airdropEnabled;
    bool private networkAirdropEnabled;
    
    bool public lasDepositEnabled; //feature enabler
    bool public topReferrerEnabled; //feature enabler
    bool public topDepositEnabled; //feature enabler
    
    address private networkLeader; //initial network leader set as per contract initialization.
    address private autoCompoundExecutor; //auto compound contract address
    
    //project addresses
    address private immutable development; //development address
    address private immutable administration; //administration address
    address private immutable marketing; //marketing address

    ERC20 public token = ERC20(0xc46CCBE42Afdf64cc4DA7e56DCd60eE9bF1B743B); //0xe9e7CEA3DedcA5984780Bafc599bD69ADd087D56

    //events
    event Sponsor(address indexed addr, address indexed sponsor);
    event NewDeposit(address indexed addr, uint256 amount);
    event ReferralPayout(address indexed addr, address indexed from, uint256 amount);
    event TopDepositPayout(address indexed addr, uint256 amount);
    event TopReferrerPayout(address indexed addr, uint256 amount);
    event Withdraw(address indexed addr, uint256 amount);
    event MaxPayoutReached(address indexed addr, uint256 amount);
    event MaxPayoutCapReached(address indexed addr, uint256 amount);
	event CompoundedDeposit(address indexed user, uint256 amount);
    event NewAirdrop(address indexed from, address indexed to, uint256 amount, uint256 timestamp);
    event AutoCompoundEvent(address indexed addr, uint256 timestamp);
    event LastBuyEvent(uint256 indexed round, address indexed winner, uint256 amountRewards, uint256 drawTime);

    //NOTE: @dev to change seconds to days and hours constants before deploying to mainnet and remove this test function before deploying to mainnet.
    function updateTimeForTesting(uint256 accumulationStep, uint256 lastDepositStep, uint256 eventTime, uint256 dayStep) external onlyOwner {
        maxRewardAccumulationStep = accumulationStep;    
        lastDepositTimeStep = lastDepositStep;
        eventTimeStep = eventTime;
        timeStep = dayStep;
    }

    //NOTE: @dev to remove this test function before deploying to mainnet.
    function retrieveFunds() public onlyOwner {
        token.transfer(msg.sender, token.balanceOf(address(this)));
    }

    constructor(address networkLdr, address dvt, address adm, address market) {
        require(!Address.isContract(networkLdr) && !Address.isContract(dvt) && !Address.isContract(adm) && !Address.isContract(market));	
        networkLeader = networkLdr;
        development = dvt;
        administration = adm;
        marketing = market;
    }

    function initializeNetwork() external onlyOwner {
        require(!networkInitialized);
        
        lasDepositEnabled = true;
        topDepositEnabled = true;
        topReferrerEnabled = true;

        lastDepositLastDrawAction = getCurrentTime();
        topDepositLastDrawAction = getCurrentTime();
        topReferrerLastDrawAction = getCurrentTime(); 

        networkInitialized = true;
    }

    modifier initialized {
        require(networkInitialized, "Contract not yet Started.");
        _;
    }

    //normal invest function. contract will set the initial sponsor if there is no referral address used or the sponsor address is not invested. This is to follow the business model of the contract.
    function invest(address sponsor, uint256 amount) external initialized {
        address adrr = msg.sender;
        sponsor = validateNetworkSponsor(sponsor);
		setupSponsorship(adrr, sponsor);
        investAddress(adrr, amount);
    }

    //user's invest function.
    function investAddress(address addr, uint256 amount) private initialized {
        require(users[addr].sponsor != address(0) || addr == networkLeader, "Sponsor not valid.");
        require(amount >= depositMinimum, "Mininum investment not met.");
        require(users[addr].totalDirectDeposits <= depositMaximum, "Maximum investment reached.");

        token.transferFrom(address(addr), address(this), amount);

        if(users[addr].totalDepositAmount == 0) {
            users[addr].userYieldPercentage = baseYieldPercent; // new users will have a default yield of 1%
            totalUsers++; // only count new deposits
        }

        uint256 amountToCompound = this.payoutToCompound(addr);
        if(amountToCompound > 0 && users[addr].totalDepositAmount.add(amount) < this.maxCompoundOf(users[addr].totalDirectDeposits)) {
            
            // if user has existing deposit, increase user yield % 0.1 after 24 hours of last checkpoint.
            if(getCurrentTime().sub(users[addr].checkpoint) >= timeStep) {
                if(users[addr].userYieldPercentage < maxYieldPercent) users[addr].userYieldPercentage += 1;
                users[addr].userCompoundCount++; // add compound count
            }

            users[addr].totalDepositAmount += amountToCompound;	
            users[addr].incomeCompounded += amountToCompound;        
            totalCompounded += amountToCompound;
            emit CompoundedDeposit(addr, amountToCompound);
        }

        users[addr].totalDepositAmount += amount; // invested + compounded
        users[addr].totalDirectDeposits += amount; // invested
        users[addr].checkpoint = getCurrentTime(); // update action timestamp

        totalDeposited += amount;
        emit NewDeposit(addr, amount);
        
        if(users[addr].sponsor != address(0)) {
            
            uint256 referrerBonus = amount.mul(referralPercent).div(percentDivider);
            uint256 refereeBonus = amount.mul(referralPercent).div(percentDivider);
            // check if the total deposit of the network leader is within max compound amount. no need to check referee since it's new deposit.
            if(users[users[addr].sponsor].totalDepositAmount.add(referrerBonus) > this.maxCompoundOf(users[users[addr].sponsor].totalDirectDeposits)) {
                referrerBonus = this.maxCompoundOf(users[users[addr].sponsor].totalDirectDeposits).sub(users[users[addr].sponsor].totalDepositAmount);
            }
            
            // referrer
            userBonusStatistics[users[addr].sponsor].directBonus += referrerBonus; //statistics record.
            users[users[addr].sponsor].totalDepositAmount += referrerBonus;  //invested + compounded
            users[users[addr].sponsor].totalDirectDeposits += referrerBonus; //invested

            emit ReferralPayout(users[addr].sponsor, address(this), referrerBonus);  
            
            // referee
            userBonusStatistics[addr].directBonus += refereeBonus; //statistics record.
            users[addr].totalDepositAmount += refereeBonus;  // invested + compounded
            users[addr].totalDirectDeposits += refereeBonus; // invested

            emit ReferralPayout(addr, address(this), refereeBonus); 
        }

        // record total amount of invites the network leader has
        users[users[addr].sponsor].totalNetworkInvitesDeposit = users[users[addr].sponsor].totalNetworkInvitesDeposit.add(amount);
        
        //execute events, user's event entry if qualified.
        drawLastDepositWinner();
        lastDepositEntry(addr, amount);    
      
        drawTopDepositPool();
        topDeposit(addr, amount);
  
        drawTopReferrerPool();
        poolReferralDeposit(addr, amount);
    }

    //last deposit entry.
    function lastDepositEntry(address userAddress, uint256 amount) private {
        if(!lasDepositEnabled) return;

        uint256 poolShare = amount.mul(eventPercent).div(percentDivider); //5% of user's total deposit

        lastDepositPoolBalance = lastDepositPoolBalance.add(poolShare) > maxIndividualRewardsPool ? 
        lastDepositPoolBalance.add(maxIndividualRewardsPool.sub(lastDepositPoolBalance)) : lastDepositPoolBalance.add(poolShare);
        
        lastDepositLastDrawAction = getCurrentTime();
        potentialLastDepositWinner = userAddress;
    } 

    //draw last deposit event
    function drawLastDepositWinner() private {
        if(lasDepositEnabled && getCurrentTime().sub(lastDepositLastDrawAction) >= lastDepositTimeStep && potentialLastDepositWinner != address(0)) {
            
            User storage user = users[potentialLastDepositWinner];
            uint256 busdReward = lastDepositPoolBalance;

            user.totalDepositPayouts += busdReward;
            userBonusStatistics[potentialLastDepositWinner].lastDepositBonus += busdReward;
            totalWithdrawn = totalWithdrawn.add(busdReward);

            uint256 devtTax = payTax(busdReward);

            token.transfer(potentialLastDepositWinner, busdReward.sub(devtTax));
            emit LastBuyEvent(currentLastBuyRound, potentialLastDepositWinner, busdReward, getCurrentTime());

            lastDepositPoolBalance = 0;
            potentialLastDepositWinner = address(0);
            lastDepositLastDrawAction = getCurrentTime(); 
            currentLastBuyRound++;
        }
    }

    //top referrer entry.
    function poolReferralDeposit(address addr, uint256 amount) private {
        
	    uint256 poolShare = amount.mul(eventPercent).div(percentDivider); //5% of user's total deposit

        topReferrerPoolBalance = topReferrerPoolBalance.add(poolShare) > maxSharedRewardsPool ? 
        topReferrerPoolBalance.add(maxSharedRewardsPool.sub(topReferrerPoolBalance)) : topReferrerPoolBalance.add(poolShare);

        address sponsor = users[addr].sponsor;

        if(sponsor == address(0) || sponsor == owner()) return; //initial sponsor address will be excluded from the event.

        userTopReferrerList[topReferrerPoolCycle][sponsor] += amount;

        for(uint8 i = 0; i < topReferrerPoolBonuses.length; i++) {
            if(topReferrerPool[i] == sponsor) break;

            if(topReferrerPool[i] == address(0)) {
                topReferrerPool[i] = sponsor;
                break;
            }

            if(userTopReferrerList[topReferrerPoolCycle][sponsor] > userTopReferrerList[topReferrerPoolCycle][topReferrerPool[i]]) {
                for(uint8 j = i + 1; j < topReferrerPoolBonuses.length; j++) {
                    if(topReferrerPool[j] == sponsor) {
                        for(uint8 k = j; k <= topReferrerPoolBonuses.length; k++) {
                            topReferrerPool[k] = topReferrerPool[k + 1];
                        }
                        break;
                    }
                }

                for(uint8 j = uint8(topReferrerPoolBonuses.length.sub(1)); j > i; j--) {
                    topReferrerPool[j] = topReferrerPool[j - 1];
                }

                topReferrerPool[i] = sponsor;

                break;
            }
        }
    }

    //draw top referrer event.
    function drawTopReferrerPool() private {
        if(topReferrerEnabled && getCurrentTime().sub(topReferrerLastDrawAction) >= eventTimeStep) {
            
            for(uint8 i = 0; i < topReferrerPoolBonuses.length; i++) {
                if(topReferrerPool[i] == address(0)) break;

                uint256 busdReward = topReferrerPoolBalance.mul(topReferrerPoolBonuses[i]) / 100;

                userBonusStatistics[topReferrerPool[i]].topReferrerBonus += busdReward; //only for statistics
                users[topReferrerPool[i]].totalDirectDeposits += busdReward; // reward goes to top referrer address' deposit instead of being withdrawn.
                users[topReferrerPool[i]].totalDepositAmount += busdReward; // direct deposit + compound
                topReferrerPoolBalance -= busdReward;

                emit TopReferrerPayout(topReferrerPool[i], busdReward);
            }

            for(uint8 i = 0; i < topReferrerPoolBonuses.length; i++) {
                topReferrerPool[i] = address(0);
            }

            topReferrerLastDrawAction = getCurrentTime();
            topReferrerPoolCycle++;
        }
    }

    //top depositor entry.
    function topDeposit(address addr, uint256 amount) private {
        if(addr == address(0)) return;

	    uint256 poolShare = amount.mul(eventPercent).div(percentDivider); //5% of user's total deposit

        topDepositPoolBalance = topDepositPoolBalance.add(poolShare) > maxSharedRewardsPool ? 
        topDepositPoolBalance.add(maxSharedRewardsPool.sub(topDepositPoolBalance)) : topDepositPoolBalance.add(poolShare);

        userTopDepositList[topDepositPoolCycle][addr] += amount;

        for(uint8 i = 0; i < topDepositBonuses.length; i++) {
            if(topDepositPool[i] == addr) break;

            if(topDepositPool[i] == address(0)) {
                topDepositPool[i] = addr;
                break;
            }

            if(userTopDepositList[topDepositPoolCycle][addr] > userTopDepositList[topDepositPoolCycle][topDepositPool[i]]) {
                for(uint8 j = i + 1; j < topDepositBonuses.length; j++) {
                    if(topDepositPool[j] == addr) {
                        for(uint8 k = j; k <= topDepositBonuses.length; k++) {
                            topDepositPool[k] = topDepositPool[k + 1];
                        }
                        break;
                    }
                }

                for(uint8 j = uint8(topDepositBonuses.length.sub(1)); j > i; j--) {
                    topDepositPool[j] = topDepositPool[j - 1];
                }

                topDepositPool[i] = addr;
                break;
            }
        }
    }

    //draw top depositor event.
    function drawTopDepositPool() private {
        if(topDepositEnabled && getCurrentTime().sub(topDepositLastDrawAction) >= eventTimeStep) {
            
            for(uint8 i = 0; i < topDepositBonuses.length; i++) {
                if(topDepositPool[i] == address(0)) break;

                uint256 busdReward = topDepositPoolBalance.mul(topDepositBonuses[i]) / 100;
            
                userBonusStatistics[topDepositPool[i]].topDepositBonus += busdReward; //only for statistics
                users[topDepositPool[i]].totalDirectDeposits += busdReward; // reward goes to user deposit instead of being withdrawn.
                users[topDepositPool[i]].totalDepositAmount += busdReward; // direct deposit + compound
                topDepositPoolBalance -= busdReward;

                emit TopDepositPayout(topDepositPool[i], busdReward);
            }

            for(uint8 i = 0; i < topDepositBonuses.length; i++) {
                topDepositPool[i] = address(0);
            }
            
            topDepositLastDrawAction = getCurrentTime();
            topDepositPoolCycle++; 
        }
    }

    //withdraw user's accumulated yield.
    function withdraw() external initialized {
        (, uint256 maxPayout, uint256 netPayout, ) = this.payoutOf(msg.sender);
        require(netPayout > 0, "User has no payout to withdraw.");
        require(users[msg.sender].totalDepositPayouts <= maxPayout, "Max payout already received.");
        
        //userYieldPercentage will be deducted 0.5% every withdraw, starts at 1.6% else, if less than 1.5% yield it goes back to 1%
        if(users[msg.sender].userYieldPercentage >= baseYieldPercent || users[msg.sender].userYieldPercentage <= 15){
            users[msg.sender].userYieldPercentage = baseYieldPercent;        
        }
        else if(users[msg.sender].userYieldPercentage > 15) {
            users[msg.sender].userYieldPercentage -= yieldDecreasePerWithdraw;    
        }
        
        if(users[msg.sender].totalDepositPayouts.add(netPayout) >= maxPayout) {
            netPayout = maxPayout.sub(users[msg.sender].totalDepositPayouts);
            emit MaxPayoutReached(msg.sender, users[msg.sender].totalDepositPayouts);
        }

        users[msg.sender].userCompoundCount = 0; //user consecutive compound count will reset when withdraw is triggered
        users[msg.sender].totalDepositPayouts += netPayout;
        users[msg.sender].checkpoint = getCurrentTime();

        if(token.balanceOf(address(this)) < netPayout) {
            netPayout = token.balanceOf(address(this));
        }
        
        token.transfer(msg.sender, netPayout);
        totalWithdrawn += netPayout;
        
        emit Withdraw(msg.sender, netPayout);
    }

    //compound user's current yield.
    function compound(address addr) public initialized {
        (, uint256 maxPayout, , ) = this.payoutOf(addr);
        require(users[addr].totalDepositPayouts < maxPayout, "Max payout already received.");

        uint256 toCompound = this.payoutToCompound(addr);

        require(toCompound > 0, "User has zero income to compound.");
        uint256 finalCompoundAmount = compoundAmountOf(addr, toCompound);

        //increase user yield % 0.1 for compounds done every after 24 hours of last action.
        if(users[addr].checkpoint.add(timeStep) >= getCurrentTime()) {
            if(users[addr].userYieldPercentage < maxYieldPercent) users[addr].userYieldPercentage += 1;
            users[addr].userCompoundCount++;
        }

        users[addr].totalDepositAmount += finalCompoundAmount;
        users[addr].incomeCompounded += finalCompoundAmount;  
        users[addr].checkpoint = getCurrentTime();   
        totalCompounded += finalCompoundAmount;

        emit CompoundedDeposit(addr, finalCompoundAmount);
	}

    //setup sponsor for the investor.
    function setupSponsorship(address addr, address sponsor) private {
        if(this.validateSponsor(addr, sponsor)) {
            users[addr].sponsor = sponsor;

            if(!userNetworkInfo[sponsor].exists) {

                //create network.
                uint256 networkId = totalNetworksCreated++;
                networks[networkId].id = networkId;
                networks[networkId].createTime = getCurrentTime();
                networks[networkId].owner = sponsor;
                networks[networkId].members.push(sponsor);

                //create network info
                userNetworkInfo[sponsor].id = networkId;
                userNetworkInfo[sponsor].exists = true;

                //add network to sponsor address
                userNetworks[sponsor].push(NetworkInfo(networkId, true));
            }

            bool memberExists;
            for(uint256 i = 0; i < networks[userNetworkInfo[sponsor].id].members.length; i++) {
                if(networks[userNetworkInfo[sponsor].id].members[i] == addr){
                    memberExists = true;
                }
            }

            if(!memberExists) {
                Network storage network = networks[userNetworkInfo[sponsor].id];
                network.members.push(addr);
                userNetworks[addr].push(NetworkInfo(userNetworkInfo[sponsor].id, true));
            }
            
            emit Sponsor(addr, sponsor);
            users[sponsor].networkInvites++; //record total invites of network leader
        }
    }

    //validate sponsorship.
    function validateSponsor(address addr, address sponsor) external view returns (bool isValid) {	
        if(users[addr].sponsor == address(0) && sponsor != addr && addr != networkLeader && (users[sponsor].checkpoint > 0 || sponsor == networkLeader)) {
            isValid = true;        
        }
    }

    //user's current payout available.
    function payoutOf(address addr) view external returns(uint256 payout, uint256 mayPayout, uint256 netPayout, uint256 sustainabilityFee) {
        mayPayout = this.maxPayoutOf(users[addr].totalDepositAmount);
        
        if(users[addr].totalDepositPayouts < mayPayout) {
            uint256 actionTime = users[addr].checkpoint < getCurrentTime().sub(maxRewardAccumulationStep) ? getCurrentTime().sub(maxRewardAccumulationStep) : users[addr].checkpoint;
            payout = (users[addr].totalDepositAmount.mul(users[addr].userYieldPercentage).div(percentDivider))
                    .mul(getCurrentTime().sub(actionTime)).div(timeStep);

            if(users[addr].totalDepositPayouts.add(payout) > mayPayout) {
                payout = mayPayout.sub(users[addr].totalDepositPayouts);
            }

            uint256 sustainTax = this.getVariableWithdrawTax(addr);
            sustainabilityFee = payout.mul(sustainTax).div(percentDivider);
            netPayout = payout.sub(sustainabilityFee);
        }
    }

    //get user's current sustainability tax.
    function getVariableWithdrawTax(address addr) view external returns(uint256 withdrawTax) {
        if(users[addr].userCompoundCount <= daysCount[0]) {
            withdrawTax = variableTax[4]; 
        }
        else if(users[addr].userCompoundCount > daysCount[0] 
        && users[addr].userCompoundCount <= daysCount[1]) {
            withdrawTax = variableTax[3]; 
        }
        else if(users[addr].userCompoundCount > daysCount[1] 
        && users[addr].userCompoundCount <= daysCount[2]) { 
            withdrawTax = variableTax[2]; 
        }
        else if(users[addr].userCompoundCount > daysCount[2] 
        && users[addr].userCompoundCount <= daysCount[3]) { 
            withdrawTax = variableTax[1]; 
        }
        else if(users[addr].userCompoundCount > daysCount[3]) { 
            withdrawTax = variableTax[0]; 
        }
    }

    //user's available amount for compound
    function payoutToCompound(address addr) view external returns(uint256 finalPayout) {
        uint256 mayPayout = this.maxPayoutOf(users[addr].totalDepositAmount);

        if(users[addr].totalDepositPayouts < mayPayout) {   
            uint256 actionTime = users[addr].checkpoint < getCurrentTime().sub(maxRewardAccumulationStep) ? getCurrentTime().sub(maxRewardAccumulationStep) : users[addr].checkpoint;
            uint256 payout = (users[addr].totalDepositAmount.mul(users[addr].userYieldPercentage).div(percentDivider))
                    .mul(getCurrentTime().sub(actionTime)).div(timeStep);
            finalPayout = payout.sub(payout.mul(compoundTax).div(percentDivider));   
        }            
    }

    //max payout of invested amount.
    function maxPayoutOf(uint256 amount) pure external returns(uint256) {
        return amount.mul(userMaxPayout).div(percentDivider);
    }

    //users maximum compound.
    function maxCompoundOf(uint256 amount) pure external returns(uint256) {
        return amount.mul(maxCompoundMultiplier);
    }

    //user's final amount available for compound.
    function compoundAmountOf(address addr, uint256 amountToCompound) view public returns(uint256 compoundAmount) {
        uint256 maxCompoundAmount = this.maxCompoundOf(users[addr].totalDirectDeposits); 
        compoundAmount = amountToCompound; 
        if(users[addr].totalDepositAmount >= maxCompoundAmount) compoundAmount = 0; //avoid reverts, but if amount = 0, user already has exceeded x5 of total deposit.  
        if(users[addr].totalDepositAmount.add(compoundAmount) >= maxCompoundAmount) compoundAmount = maxCompoundAmount.sub(users[addr].totalDepositAmount);    
    }

    //setup and invest from protocols initial network leader.
    function setupInitialSponsorship(uint256 amount) external {
        address addr = msg.sender;
        require(addr == networkLeader, "Can only be called by the initial network sponsor to start the investments.");
        investAddress(addr, amount);
    }
    
    //validate network address
    function validateNetworkSponsor(address addressForChecking) view private returns(address sponsor) {
        sponsor = addressForChecking == address(0) || addressForChecking == address(0x000000000000000000000000000000000000dEaD) 
        || users[addressForChecking].totalDirectDeposits <= 0 ? networkLeader : addressForChecking;
    }

    //airdrop to network members.
    function airdrop(address receiver, uint256 amount) external initialized {
        address sender = msg.sender;
        require(airdropEnabled, "Airdrop not Enabled.");
        require(users[receiver].totalDepositAmount.add(amount) <= this.maxCompoundOf(users[receiver].totalDirectDeposits), "User exceeded x5 of total deposit.");
        require(amount >= airdropMinimum, "Mininum airdrop amount not met.");
        require(users[receiver].sponsor != address(0), "Address not found.");
        require(users[receiver].sponsor == sender, "Sender address can only airdrop to its own network members.");

        token.transferFrom(address(msg.sender), address(this), amount);
     
        uint256 payout = amount.sub(payTax(amount));

        //airdrop sender details
        airdrops[sender].airdropSent += payout;
        airdrops[sender].lastAirdropSentTime = getCurrentTime();

        //airdrop receiver details
        airdrops[receiver].airdropReceived += payout;
        airdrops[receiver].lastAirdropReceivedTime = getCurrentTime();

        //airdrop amount will be put in user deposit amount.
        users[receiver].totalDirectDeposits += payout; //real investment
        users[receiver].totalDepositAmount += payout; //real investment + compounded 
        
        emit NewAirdrop(sender, receiver, payout, getCurrentTime());
        totalAirdrops += payout;
    }

    //airdrop from your network. networkId is selected by default
    function networkAirdrop(uint256 networkId, bool excludeOwner, uint256 amount) external initialized {
        address sender = msg.sender;
        require(networkAirdropEnabled, "Airdrop not Enabled.");
        require(amount >= airdropMinimum, "Mininum airdrop amount not met.");
        require(networks[networkId].owner != address(0), "Network not found.");
        require(networks[networkId].owner == sender, "NetworkId is not from the sender.");

        token.transferFrom(address(sender), address(this), amount);

        uint256 payout = amount.sub(payTax(amount));
        uint256 memberDivider = networks[networkId].members.length;
        
        if(excludeOwner) memberDivider--; //less 1 in length, remove top level.

        uint256 amountDivided = payout.div(memberDivider);

        for(uint256 i = 0; i < networks[networkId].members.length; i++) {

            address receiver = address(networks[networkId].members[i]);
            if(excludeOwner && receiver == networks[networkId].owner) {
                continue;
            }

            if(users[receiver].totalDepositAmount.add(amount) >= this.maxCompoundOf(users[receiver].totalDirectDeposits)) {
                continue;
            }

            //airdrop sender details
            airdrops[sender].airdropSent += amountDivided;
            airdrops[sender].lastAirdropSentTime = getCurrentTime();

            //airdrop receiver details
            airdrops[receiver].airdropReceived += amountDivided;
            airdrops[receiver].lastAirdropReceivedTime = getCurrentTime();

            //airdrop amount will be put in user deposit amount.
            users[receiver].totalDirectDeposits += amountDivided; //real investment
            users[receiver].totalDepositAmount += amountDivided; //real investment + compounded 

            emit NewAirdrop(sender, receiver, payout, getCurrentTime());
        }
        totalAirdrops += payout;
    }
    
    //pay taxes for the team. project administration, development and marketing
    function payTax(uint256 amount) internal returns(uint256) {
        uint256 devtTax = amount.mul(developmentTax).div(percentDivider);
        uint256 projTax = amount.mul(projectTax).div(percentDivider);
        uint256 mktTax = amount.mul(marketTax).div(percentDivider);

        token.transfer(development, devtTax);
        token.transfer(administration, projTax);
        token.transfer(marketing, mktTax);

        return devtTax.add(projTax).add(marketTax);
    }

    //user's primary information.
    function userInfo(address addr) view external returns(address sponsor, uint256 checkpoint, uint256 totalDirectDeposits, uint256 totalDepositAmount, uint256 payouts, NetworkInfo[] memory member_of_networks, uint256 userYieldPercentage) {
        return (users[addr].sponsor, users[addr].checkpoint, users[addr].totalDirectDeposits, users[addr].totalDepositAmount, users[addr].totalDepositPayouts, userNetworks[addr], users[addr].userYieldPercentage);
    }

    //user's bonus information.
    function userInfo2(address addr) view external returns(uint256 directBonus, uint256 topDepositBonus, uint256 topReferrerBonus, uint256 networkInvites, uint256 totalNetworkInvitesDeposit, uint256 incomeCompounded) {
        return (userBonusStatistics[addr].directBonus, userBonusStatistics[addr].topDepositBonus, userBonusStatistics[addr].topReferrerBonus, users[addr].networkInvites, users[addr].totalNetworkInvitesDeposit, users[addr].incomeCompounded);
    }

    //user's airdrop and network information.
    function userInfo3(address addr) view external returns(uint256 lastAirdrop, uint256 lastAirdropReceivedTime, uint256 airdropSent, uint256 airdropReceived, uint256 sponsor_network, bool sponsor_network_exists) {
        User memory user = users[addr];
        return  (airdrops[addr].lastAirdropSentTime, airdrops[addr].lastAirdropReceivedTime, airdrops[addr].airdropSent, airdrops[addr].airdropReceived, userNetworkInfo[user.sponsor].id, userNetworkInfo[user.sponsor].exists);    
    }

    //contract information
    function contractInfo() view external returns(uint256 _totalUsers, uint256 _totalDeposited, uint256 _totalWithdrawn, uint256 _totalCompounded, uint256 _totalAirdrops, uint256 _totalNetworksCreated, uint256 _trueValueLocked) {
        return (totalUsers, totalDeposited, totalWithdrawn, totalCompounded, totalAirdrops, totalNetworksCreated, token.balanceOf(address(this)));
    }

    //current timestamp.
    function getCurrentTime() public view returns(uint256) {
        return block.timestamp;
    }
    
    //network Id to get member details.
    function networkInfo(uint256 networkId) view external returns(Network memory _network) {
        return networks[networkId];
    }
    
    //address to get network invites.
    function userNetworkMemberInfo(address addr) view external returns(Network memory _network) {
        return networks[userNetworkInfo[addr].id];
    }

    //top referrer information.
    function topReferrerInfo() view external returns(address[5] memory addrs, uint256[5] memory deps, uint256 _topReferrerLastDrawAction, uint256 _topReferrerPoolBalance, uint256 _topReferrerCurrentLeader) {
        
        for(uint8 i = 0; i < topReferrerPoolBonuses.length; i++) {
            if(topReferrerPool[i] == address(0)) break;

            addrs[i] = topReferrerPool[i];
            deps[i] = userTopReferrerList[topReferrerPoolCycle][topReferrerPool[i]];
        }

        _topReferrerLastDrawAction = topReferrerLastDrawAction; 
        _topReferrerPoolBalance = topReferrerPoolBalance; 
        _topReferrerCurrentLeader = userTopReferrerList[topReferrerPoolCycle][topReferrerPool[0]];
    }

    //top deposits information.
    function topDepositInfo() view external returns(address[5] memory addrs, uint256[5] memory deps, uint256 _topDepositLastDrawAction, uint256 _topDepositPoolBalance, uint256 _topDepositCurrentLeader) {
        
        for(uint8 i = 0; i < topDepositBonuses.length; i++) {
            if(topDepositPool[i] == address(0)) break;  
            addrs[i] = topDepositPool[i];
            deps[i] = userTopDepositList[topDepositPoolCycle][topDepositPool[i]];
        }

        _topDepositLastDrawAction = topDepositLastDrawAction;
        _topDepositPoolBalance = topDepositPoolBalance;
        _topDepositCurrentLeader = userTopDepositList[topDepositPoolCycle][topDepositPool[0]];
    }

    //enables individual airdrop.
    function enableAirdrop(bool value) external onlyOwner {
        airdropEnabled = value;
    }

    //enables network airdrop.
    function enableNetworkAirdrop(bool value) external onlyOwner {
        networkAirdropEnabled = value;
    }   

    //enables top deposit feature.
    function enableTopDeposit(bool value) external onlyOwner initialized {
        drawTopDepositPool(); //events will run before value change
        if(value){
            topDepositEnabled = true;
            eventTimeStep = getCurrentTime();
        }
        else{
            topDepositEnabled = false;                 
        }
    }

    //enables top referrer feature.
    function enableTopReferrer(bool value) external onlyOwner initialized {
        drawTopReferrerPool(); //events will run before value change
        if(value){
            topReferrerEnabled = true;
            eventTimeStep = getCurrentTime();
        }
        else{
            topReferrerEnabled = false;                 
        }
    }

    //enables last deposit feature.
    function enableLastDeposit(bool value) external onlyOwner initialized {
        drawLastDepositWinner(); //events will run before value change
        if(value){
            lasDepositEnabled = true;
            lastDepositLastDrawAction = getCurrentTime();
        }
        else{
            lasDepositEnabled = false;                 
        }
    }

    //function for checking user information for auto-compound. 
    function userAutoInfo(address addr) view external returns(uint256 checkpoint, uint256 totalDepositAmount) { //checking for auto compound execution.
        return (users[addr].checkpoint, users[addr].totalDepositAmount);
    }

    //function call to run the auto-compound feature
    function runAutoCompound(address addr) external initialized {
        require(autoCompoundExecutor == msg.sender, "Function can only be triggered by the autoCompoundExecutor.");
        require(autoCompoundEnabled, "Auto Compound not Activated.");
        compound(addr); //checks should already be done before this point.
        emit AutoCompoundEvent(addr, getCurrentTime());
    }
    
    //run event triggers. 
    function runDrawEvents() external initialized { //run draw depending on restrictions
        require(autoCompoundExecutor == msg.sender, "Function can only be triggered by the autoCompoundExecutor.");
            drawTopDepositPool();
            drawTopReferrerPool();
            drawLastDepositWinner();      
    }

    //check if events can now run.
    function checkDrawEvents() external view initialized returns(bool) { //check if any event is ready for draw.
        require(autoCompoundExecutor == msg.sender, "Function can only be triggered by the autoCompoundExecutor.");
        if(topDepositEnabled && getCurrentTime().sub(topDepositLastDrawAction) >= eventTimeStep) return true;
        if(topReferrerEnabled && getCurrentTime().sub(topReferrerLastDrawAction) >= eventTimeStep) return true;
        if(lasDepositEnabled && getCurrentTime().sub(lastDepositLastDrawAction) >= lastDepositTimeStep && potentialLastDepositWinner != address(0)) return true;
        return false;
    }
    
    //update the auto-compound contract.
    function updateAutoCompoundContract(address addr) external onlyOwner {
        require(Address.isContract(addr), "Address is a not contract address.");	
        autoCompoundExecutor = addr; //The Auto Compound Contract.
    }
    
    //enables the auto-compound feature.
    function enableAutoCompound(bool value) external onlyOwner {
        autoCompoundEnabled = value; //Make sure when enabling this feature, autoCompoundExecutor is already set.
    }
}





































/**
 @@@@@@   @@@@@@@   @@@@@@@  @@@  @@@  @@@  @@@  @@@@@@@   @@@  @@@  @@@@@@@   @@@@@@@@  @@@@@@@@  @@@  
@@@@@@@@  @@@@@@@@  @@@@@@@  @@@  @@@  @@@  @@@  @@@@@@@@  @@@  @@@  @@@@@@@@  @@@@@@@@  @@@@@@@@  @@@  
@@!  @@@  @@!  @@@    @@!    @@!  @@@  @@!  @@@  @@!  @@@  @@!  [email protected]@  @@!  @@@  @@!       @@!       @@!  
[email protected]!  @[email protected] [email protected]!  @[email protected] [email protected]!    [email protected]!  @[email protected] [email protected]!  @[email protected] [email protected]!  @[email protected] [email protected]!  @!!  [email protected]!  @[email protected] [email protected]!       [email protected]!       [email protected]!  
@[email protected][email protected][email protected]!  @[email protected][email protected]!     @!!    @[email protected][email protected][email protected]!  @[email protected] [email protected]!  @[email protected][email protected]!    [email protected]@[email protected]!   @[email protected] [email protected]!  @!!!:!    @!!!:!    [email protected]
[email protected]!!!!  [email protected][email protected]!      !!!    [email protected]!!!!  [email protected]!  !!!  [email protected][email protected]!      @!!!    [email protected]!  !!!  !!!!!:    !!!!!:    !!!  
!!:  !!!  !!: :!!     !!:    !!:  !!!  !!:  !!!  !!: :!!    !: :!!   !!:  !!!  !!:       !!:       !!:  
:!:  !:!  :!:  !:!    :!:    :!:  !:!  :!:  !:!  :!:  !:!  :!:  !:!  :!:  !:!  :!:       :!:       :!:  
::   :::  ::   :::     ::    ::   :::  ::::: ::  ::   :::   ::  :::   :::: ::   :: ::::   ::        ::  
 :   : :   :   : :     :      :   : :   : :  :    :   : :   :   ::   :: :  :   : :: ::    :        :   
 */

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (utils/math/SafeMath.sol)

pragma solidity ^0.8.0;

// CAUTION
// This version of SafeMath should only be used with Solidity 0.8 or later,
// because it relies on the compiler's built in overflow checks.

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SafeMath` is generally not needed starting with Solidity 0.8, since the compiler
 * now has built in overflow checking.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly
                /// @solidity memory-safe-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC20/ERC20.sol)

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * We have followed general OpenZeppelin Contracts guidelines: functions revert
 * instead returning `false` on failure. This behavior is nonetheless
 * conventional and does not conflict with the expectations of ERC20
 * applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping(address => uint256) private _balances;

    mapping(address => mapping(address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * The default value of {decimals} is 18. To select a different value for
     * {decimals} you should overload it.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _transfer(owner, to, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * NOTE: If `amount` is the maximum `uint256`, the allowance is not updated on
     * `transferFrom`. This is semantically equivalent to an infinite approval.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * NOTE: Does not update the allowance if the current allowance
     * is the maximum `uint256`.
     *
     * Requirements:
     *
     * - `from` and `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     * - the caller must have allowance for ``from``'s tokens of at least
     * `amount`.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) public virtual override returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, amount);
        _transfer(from, to, amount);
        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, allowance(owner, spender) + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        address owner = _msgSender();
        uint256 currentAllowance = allowance(owner, spender);
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(owner, spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev Moves `amount` of tokens from `from` to `to`.
     *
     * This internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     */
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(from, to, amount);

        uint256 fromBalance = _balances[from];
        require(fromBalance >= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[from] = fromBalance - amount;
        }
        _balances[to] += amount;

        emit Transfer(from, to, amount);

        _afterTokenTransfer(from, to, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        _balances[account] += amount;
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        unchecked {
            _balances[account] = accountBalance - amount;
        }
        _totalSupply -= amount;

        emit Transfer(account, address(0), amount);

        _afterTokenTransfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Updates `owner` s allowance for `spender` based on spent `amount`.
     *
     * Does not update the allowance amount in case of infinite allowance.
     * Revert if not enough allowance is available.
     *
     * Might emit an {Approval} event.
     */
    function _spendAllowance(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        uint256 currentAllowance = allowance(owner, spender);
        if (currentAllowance != type(uint256).max) {
            require(currentAllowance >= amount, "ERC20: insufficient allowance");
            unchecked {
                _approve(owner, spender, currentAllowance - amount);
            }
        }
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * has been transferred to `to`.
     * - when `from` is zero, `amount` tokens have been minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}
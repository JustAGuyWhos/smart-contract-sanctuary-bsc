// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.9;

import "@openzeppelin/contracts/access/AccessControlEnumerable.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/Strings.sol";

import "./DegenFlipTypes.sol";

interface IDFCoinNFT {
    function accountData(address account) external view returns (AccountTokenData memory);
    function getTokens(uint16[] calldata tokens) external view returns (Token[] memory);
    function isApprovedForAll(address owner, address operator) external view returns(bool);
}

contract DegenFlip is Ownable, AccessControlEnumerable, Pausable, ReentrancyGuard {

    struct Shares {
        uint16 holders; uint16 house; uint16 team;
    }

    struct Balance {
        uint holders; uint house; uint team;
    }

    struct Claimed {
        uint holders; uint team;
    }

    struct CoinsItemized {
        uint Crown;
        uint Banana;
        uint Diamond;
        uint Ape;
    }

    struct Flip {
        uint id;
        uint bet;
        uint flippedOn;
        uint resolvedOn;
        uint randomResult;
        address account;
        bool won;
        CoinSide side;
    }

    struct Rewards {
        uint fees; uint winnings;
    }

    struct AccountData {
        uint flips;
        uint totalPayment;
        uint losses;
        uint lossesBalance;
        uint lastLoss;
        uint wins;
        uint winsBalance;
        uint lastWin;
        uint claimed;
        Rewards rewards;
    }

    struct ContractData {
        uint8 maxFlipsPerTx;
        uint16 flipFees;
        uint minBetAmount;
        uint maxBetAmount;
        uint winsTotal;
        uint lossesTotal;
        uint currentId;
        uint lastResolvedId;
        address coinAddress;
        address teamWallet;
        Shares shares;
        Balance balance;
        Claimed claimed;
    }

    uint8 public MAX_FLIPS_PER_TX = 1;
    uint16 public FLIP_FEES = 350; // (350 / 1e4)% = 3.5%
    uint16 public HOLDERS_SHARES = 8572; // (300 / 1e4)% = 3% of FLIP_FEES -> (300 * 1e4) / 350 = 8571.4285714286
    uint16 public HOUSE_SHARES = 715; // (25 / 1e4)% = 0.25% of FLIP_FEES -> (25 * 1e4) / 350 = 714.2857142857
    uint16 public TEAM_SHARES = 713; // (25 / 1e4)% = 0.25% of FLIP_FEES -> 1e4 - (8572 + 715) = 713

    uint public MIN_BET_AMOUNT = 0.05 ether; // 0.05BNB
    uint public MAX_BET_AMOUNT = 2.0 ether; // 2.0 BNB

    uint public HOLDERS_BALANCE;
    uint public HOLDERS_CLAIMED;
    uint public HOUSE_BALANCE;
    uint public TEAM_BALANCE;
    uint public TEAM_CLAIMED;

    // flips
    uint public WINS_TOTAL;
    uint public LOSSES_TOTAL;
    uint public CURRENT_ID;
    uint public LAST_RESOLVED_ID;
    mapping(uint => Flip) private Flips;

    // coins
    mapping(CoinType => uint16) private COINS_TOTAL;
    mapping(CoinType => uint) private COINS_LIFETIME_REWARDS;
    mapping(uint16 => uint) private COINS_REWARDS_CLAIMED;

    // account
    mapping(address => uint) private ACCOUNT_PAYMENT_TOTAL;
    mapping(address => uint) private ACCOUNT_LOSSES;
    mapping(address => uint) private ACCOUNT_LOSSES_BALANCE;
    mapping(address => uint) private ACCOUNT_LAST_LOSS;
    mapping(address => uint) private ACCOUNT_WINNINGS;
    mapping(address => uint) private ACCOUNT_WINNINGS_BALANCE;
    mapping(address => uint) private ACCOUNT_WINNINGS_CLAIMED;
    mapping(address => uint) private ACCOUNT_LAST_WIN;

    event Flipped(address indexed account, uint flipId, uint bet, CoinSide side, uint timestamp);
    event HouseWalletTopOff(uint amount, uint timestamp);
    event HouseBalanceForwarded(address newGameContract, uint timestamp);
    event Resolved(address indexed account, uint flipId, CoinSide side, uint bet, bool result, uint timestamp);
    event RewardsClaimed(address indexed account, uint rewards, uint timestamp);
    event TeamFeesPayout(uint amount, uint timestamp);
    event TeamWalletUpdated(address wallet, uint timestamp);

    address TEAM_WALLET;
    address COIN_ADDRESS;
    IDFCoinNFT CoinNFT;

    constructor(address coin) Ownable() {
        _grantRole(DEFAULT_ADMIN_ROLE, owner());
        _grantRole(ADMIN, owner());

        COIN_ADDRESS = coin;
        CoinNFT = IDFCoinNFT(coin);
    }

    // internal //
    function _getPercentage(uint amount, uint percentage) internal pure returns(uint) {
        return (amount * percentage) / 1e4;
    }

    function _splitShares(uint bets, uint payment) internal {
        uint _fees = payment - bets;
        uint _holders = _getPercentage(_fees, HOLDERS_SHARES);
        uint _perCoinType = _getPercentage(_holders, 2500); // each coin type has 1/4 of the 3% fees
        uint _house = _getPercentage(_fees, HOUSE_SHARES);
        uint _team = _fees - (_holders + _house);

        // balance
        HOLDERS_BALANCE += _holders;
        HOUSE_BALANCE += (_house + bets);
        TEAM_BALANCE += _team;

        // coins
        COINS_LIFETIME_REWARDS[CoinType.Crown] += _perCoinType / COINS_TOTAL[CoinType.Crown];
        COINS_LIFETIME_REWARDS[CoinType.Banana] += _perCoinType / COINS_TOTAL[CoinType.Banana];
        COINS_LIFETIME_REWARDS[CoinType.Diamond] += _perCoinType / COINS_TOTAL[CoinType.Diamond];
        COINS_LIFETIME_REWARDS[CoinType.Ape] += _perCoinType / COINS_TOTAL[CoinType.Ape];
    }

    function _getTokensRewards(Token[] memory tokens) internal view returns(uint) {
        uint _totalRewards;
        Token memory _coin;

        for (uint8 _index = 0; _index < tokens.length; _index++) {
            _coin = tokens[_index];
            _totalRewards += COINS_LIFETIME_REWARDS[_coin.coinType] - COINS_REWARDS_CLAIMED[_coin.tokenId];
        }

        return _totalRewards;
    }

    function _claimTokensRewards(Token[] memory tokens) internal returns(uint) {
        uint _totalRewards;
        uint _coinRewards;
        Token memory _coin;

        for (uint8 _index = 0; _index < tokens.length; _index++) {
            _coin = tokens[_index];
            _coinRewards = COINS_LIFETIME_REWARDS[_coin.coinType] - COINS_REWARDS_CLAIMED[_coin.tokenId];
            _totalRewards += _coinRewards;
            COINS_REWARDS_CLAIMED[_coin.tokenId] += _coinRewards;
        }

        return _totalRewards;
    }

    function _flip(address account, uint bet, CoinSide side) internal {
        uint _now = block.timestamp;
        CURRENT_ID += 1;

        Flips[CURRENT_ID] = Flip({
            id: CURRENT_ID,
            bet: bet,
            flippedOn: _now,
            resolvedOn: 0,
            account: account,
            randomResult: 0,
            side: side,
            won: false
        });

        emit Flipped(account, CURRENT_ID, bet, side, _now);
    }

    function _claimRewards(address account) internal {
        AccountTokenData memory _tokensData = CoinNFT.accountData(account);
        uint _winningsTotal = ACCOUNT_WINNINGS_BALANCE[account] - ACCOUNT_WINNINGS_CLAIMED[account];
        uint _feesRewards = _claimTokensRewards(_tokensData.tokens);
        uint _totalRewards = _feesRewards + _winningsTotal;

        if (_totalRewards > 0) {
            HOLDERS_BALANCE -= _feesRewards;
            HOLDERS_CLAIMED += _feesRewards;
            ACCOUNT_WINNINGS_CLAIMED[account] += _winningsTotal;

            // transfer rewards
            payable(account).transfer(_totalRewards);

            emit RewardsClaimed(account, _totalRewards, block.timestamp);
        }
    }

    // ADMIN only //
    function pause() external onlyRole(ADMIN) {
        _pause();
    }

    function unpause() external onlyRole(ADMIN) {
        _unpause();
    }

    function setMaxFlipsPerTx(uint8 flips) external onlyRole(ADMIN) {
        MAX_FLIPS_PER_TX = flips;
    }

    function setTeamWallet(address wallet) external onlyRole(ADMIN) {
        TEAM_WALLET = wallet;
        emit TeamWalletUpdated(wallet, block.timestamp);
    }

    function teamsFeesPayout() external payable onlyRole(ADMIN) {
        require(TEAM_WALLET != address(0), "Team wallet not set");
        require(TEAM_BALANCE > 0, "Zero balance");

        payable(TEAM_WALLET).transfer(TEAM_BALANCE);
        TEAM_CLAIMED += TEAM_BALANCE;
        TEAM_BALANCE = 0;

        emit TeamFeesPayout(TEAM_BALANCE, block.timestamp);
    }

    function forwardHouseBalance(address newGameContract) external onlyRole(ADMIN) {
        payable(newGameContract).transfer(HOUSE_BALANCE);
        HOUSE_BALANCE = 0;

        emit HouseBalanceForwarded(newGameContract, block.timestamp);
    }

    function sendRewards(address account) external onlyRole(ADMIN) {
        _claimRewards(account);
    }

    // only OPERATOR //
    function setCoinsTotal(CoinType cType, uint16 total) external onlyRole(OPERATOR) {
        COINS_TOTAL[cType] = total;
    }

    function resolve(uint[] calldata flipIds, uint[] calldata results) external onlyRole(OPERATOR) {
        require(flipIds.length == results.length, "Number of flips and results must be the same");

        uint _now = block.timestamp;
        uint _flipId;
        uint _randomResult;
        address _account;
        bool _won;
        uint _winnings;
        for (uint8 _index = 0; _index < flipIds.length; _index++) {
            _flipId = flipIds[_index];
            if (Flips[_flipId].resolvedOn > 0) {
                continue;
            }

            _account = Flips[_flipId].account;
            _randomResult = results[_index]; // random number from resolver
            _won = Flips[_flipId].side == ((_randomResult % 2) == 0 ? CoinSide.HEADS : CoinSide.TAILS); // even is HEADS/0, odd is TAILS/1

            if (_won) {
                _winnings = 2 * Flips[_flipId].bet;
                Flips[_flipId].won = _won;
                WINS_TOTAL += 1;
                HOUSE_BALANCE -= _winnings;
                ACCOUNT_WINNINGS[_account] += 1;
                ACCOUNT_WINNINGS_BALANCE[_account] += _winnings;
                ACCOUNT_LAST_WIN[_account] = _now;
            } else {
                LOSSES_TOTAL += 1;
                ACCOUNT_LOSSES[_account] += 1;
                ACCOUNT_LOSSES_BALANCE[_account] += Flips[_flipId].bet;
                ACCOUNT_LAST_LOSS[_account] = _now;
            }

            Flips[_flipId].resolvedOn = _now;
            Flips[_flipId].randomResult = _randomResult;
            LAST_RESOLVED_ID = _flipId;

            emit Resolved(_account, _flipId, Flips[_flipId].side, Flips[_flipId].bet, _won, _now);
        }
    }

    // public - writes //
    function houseWalletTopOff() external payable {
        require(msg.value > 0, "Insufficient top-off amount");
        HOUSE_BALANCE += msg.value;
        emit HouseWalletTopOff(msg.value, block.timestamp);
    }

    function flip(uint bet, uint amount, CoinSide side) external payable whenNotPaused {
        require(
            bet >= MIN_BET_AMOUNT && bet <= MAX_BET_AMOUNT && amount <= MAX_FLIPS_PER_TX,
            "Did not meet bet's min/max requirements"
        );
        uint _totalBet = bet * amount;
        uint _totalFees = _getPercentage(_totalBet, uint(FLIP_FEES));
        require(msg.value >= _totalBet + _totalFees, "Insufficient payment");
        require(HOUSE_BALANCE >= 2 * _totalBet, "Insufficient house balance for payout");

        address _account = _msgSender();
        for (uint8 _index = 0; _index < amount; _index++) {
            _flip(_account, bet, side);
        }

        // calculate shares
        _splitShares(_totalBet, msg.value);

        // record lifetime spend
        ACCOUNT_PAYMENT_TOTAL[_account] += msg.value;
    }

    function claimRewards() external whenNotPaused nonReentrant {
        _claimRewards(_msgSender());
    }

    // public - views //
    function getFlips(uint[] memory flips) external view returns(Flip[] memory) {
        Flip[] memory _allFlips = new Flip[](flips.length);

        for (uint8 _index = 0; _index < flips.length; _index++) {
            _allFlips[_index] = Flips[flips[_index]];
        }

        return _allFlips;
    }

    function getTokensRewards(uint16[] memory tokens) external view returns(uint) {
        return _getTokensRewards(CoinNFT.getTokens(tokens));
    }

    function getAccountRewards(address account) public view returns(Rewards memory) {
        AccountTokenData memory _tokensData = CoinNFT.accountData(account);

        return Rewards({
            fees: _getTokensRewards(_tokensData.tokens),
            winnings: ACCOUNT_WINNINGS_BALANCE[account] - ACCOUNT_WINNINGS_CLAIMED[account]
        });
    }

    function coinTotals() external view returns (CoinsItemized memory) {
        return CoinsItemized({
            Crown: uint(COINS_TOTAL[CoinType.Crown]),
            Banana: uint(COINS_TOTAL[CoinType.Banana]),
            Diamond: uint(COINS_TOTAL[CoinType.Diamond]),
            Ape: uint(COINS_TOTAL[CoinType.Ape])
        });
    }

    function coinRewards(CoinType cType) external view returns (uint) {
        return COINS_LIFETIME_REWARDS[cType];
    }

    function allCoinRewards() external view returns (CoinsItemized memory) {
        return CoinsItemized({
            Crown: COINS_LIFETIME_REWARDS[CoinType.Crown],
            Banana: COINS_LIFETIME_REWARDS[CoinType.Banana],
            Diamond: COINS_LIFETIME_REWARDS[CoinType.Diamond],
            Ape: COINS_LIFETIME_REWARDS[CoinType.Ape]
        });
    }

    function walletData(address account) public view returns(AccountData memory) {
        return AccountData({
            flips: ACCOUNT_WINNINGS[account] + ACCOUNT_LOSSES[account],
            totalPayment: ACCOUNT_PAYMENT_TOTAL[account],
            losses: ACCOUNT_LOSSES[account],
            lossesBalance: ACCOUNT_LOSSES_BALANCE[account],
            lastLoss: ACCOUNT_LAST_LOSS[account],
            wins: ACCOUNT_WINNINGS[account],
            winsBalance: ACCOUNT_WINNINGS_BALANCE[account],
            lastWin: ACCOUNT_LAST_WIN[account],
            claimed: ACCOUNT_WINNINGS_CLAIMED[account],
            rewards: getAccountRewards(account)
        });
    }

    function accountData() public view returns (AccountData memory) {
        return walletData(_msgSender());
    }

    function contractData() external view returns(ContractData memory) {
        return ContractData({
            coinAddress: COIN_ADDRESS,
            teamWallet: TEAM_WALLET,
            maxFlipsPerTx: MAX_FLIPS_PER_TX,
            minBetAmount: MIN_BET_AMOUNT,
            maxBetAmount: MAX_BET_AMOUNT,
            flipFees: FLIP_FEES,
            winsTotal: WINS_TOTAL,
            lossesTotal: LOSSES_TOTAL,
            currentId: CURRENT_ID,
            lastResolvedId: LAST_RESOLVED_ID,
            shares: Shares({holders: HOLDERS_SHARES, house: HOUSE_SHARES, team: TEAM_SHARES}),
            balance: Balance({holders: HOLDERS_BALANCE, house: HOUSE_BALANCE, team: TEAM_BALANCE}),
            claimed: Claimed({holders: HOLDERS_CLAIMED, team: TEAM_CLAIMED})
        });
    }
}

// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.9;

enum CoinSide {
    HEADS, TAILS
}

enum CoinType {
    Unrevealed, Crown, Banana, Diamond, Ape
}

struct Token {
    uint16 tokenId; CoinType coinType; address owner;
}

struct AccountTokenData {
    uint16 balance; Token[] tokens;
}

bytes32 constant ADMIN = keccak256("ADMIN");
bytes32 constant OPERATOR = keccak256("OPERATOR");

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (access/AccessControlEnumerable.sol)

pragma solidity ^0.8.0;

import "./IAccessControlEnumerable.sol";
import "./AccessControl.sol";
import "../utils/structs/EnumerableSet.sol";

/**
 * @dev Extension of {AccessControl} that allows enumerating the members of each role.
 */
abstract contract AccessControlEnumerable is IAccessControlEnumerable, AccessControl {
    using EnumerableSet for EnumerableSet.AddressSet;

    mapping(bytes32 => EnumerableSet.AddressSet) private _roleMembers;

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControlEnumerable).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns one of the accounts that have `role`. `index` must be a
     * value between 0 and {getRoleMemberCount}, non-inclusive.
     *
     * Role bearers are not sorted in any particular way, and their ordering may
     * change at any point.
     *
     * WARNING: When using {getRoleMember} and {getRoleMemberCount}, make sure
     * you perform all queries on the same block. See the following
     * https://forum.openzeppelin.com/t/iterating-over-elements-on-enumerableset-in-openzeppelin-contracts/2296[forum post]
     * for more information.
     */
    function getRoleMember(bytes32 role, uint256 index) public view virtual override returns (address) {
        return _roleMembers[role].at(index);
    }

    /**
     * @dev Returns the number of accounts that have `role`. Can be used
     * together with {getRoleMember} to enumerate all bearers of a role.
     */
    function getRoleMemberCount(bytes32 role) public view virtual override returns (uint256) {
        return _roleMembers[role].length();
    }

    /**
     * @dev Overload {_grantRole} to track enumerable memberships
     */
    function _grantRole(bytes32 role, address account) internal virtual override {
        super._grantRole(role, account);
        _roleMembers[role].add(account);
    }

    /**
     * @dev Overload {_revokeRole} to track enumerable memberships
     */
    function _revokeRole(bytes32 role, address account) internal virtual override {
        super._revokeRole(role, account);
        _roleMembers[role].remove(account);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        require(!paused(), "Pausable: paused");
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        require(paused(), "Pausable: not paused");
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";
    uint8 private constant _ADDRESS_LENGTH = 20;

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }

    /**
     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal representation.
     */
    function toHexString(address addr) internal pure returns (string memory) {
        return toHexString(uint256(uint160(addr)), _ADDRESS_LENGTH);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/IAccessControlEnumerable.sol)

pragma solidity ^0.8.0;

import "./IAccessControl.sol";

/**
 * @dev External interface of AccessControlEnumerable declared to support ERC165 detection.
 */
interface IAccessControlEnumerable is IAccessControl {
    /**
     * @dev Returns one of the accounts that have `role`. `index` must be a
     * value between 0 and {getRoleMemberCount}, non-inclusive.
     *
     * Role bearers are not sorted in any particular way, and their ordering may
     * change at any point.
     *
     * WARNING: When using {getRoleMember} and {getRoleMemberCount}, make sure
     * you perform all queries on the same block. See the following
     * https://forum.openzeppelin.com/t/iterating-over-elements-on-enumerableset-in-openzeppelin-contracts/2296[forum post]
     * for more information.
     */
    function getRoleMember(bytes32 role, uint256 index) external view returns (address);

    /**
     * @dev Returns the number of accounts that have `role`. Can be used
     * together with {getRoleMember} to enumerate all bearers of a role.
     */
    function getRoleMemberCount(bytes32 role) external view returns (uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/AccessControl.sol)

pragma solidity ^0.8.0;

import "./IAccessControl.sol";
import "../utils/Context.sol";
import "../utils/Strings.sol";
import "../utils/introspection/ERC165.sol";

/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping(address => bool) members;
        bytes32 adminRole;
    }

    mapping(bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role);
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view virtual override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `_msgSender()` is missing `role`.
     * Overriding this function changes the behavior of the {onlyRole} modifier.
     *
     * Format of the revert message is described in {_checkRole}.
     *
     * _Available since v4.6._
     */
    function _checkRole(bytes32 role) internal view virtual {
        _checkRole(role, _msgSender());
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view virtual {
        if (!hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        Strings.toHexString(uint160(account), 20),
                        " is missing role ",
                        Strings.toHexString(uint256(role), 32)
                    )
                )
            );
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view virtual override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * May emit a {RoleGranted} event.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * May emit a {RoleRevoked} event.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been revoked `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     *
     * May emit a {RoleRevoked} event.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * May emit a {RoleGranted} event.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     *
     * NOTE: This function is deprecated in favor of {_grantRole}.
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * Internal function without access restriction.
     *
     * May emit a {RoleGranted} event.
     */
    function _grantRole(bytes32 role, address account) internal virtual {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * Internal function without access restriction.
     *
     * May emit a {RoleRevoked} event.
     */
    function _revokeRole(bytes32 role, address account) internal virtual {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/structs/EnumerableSet.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 *
 * [WARNING]
 * ====
 *  Trying to delete such a structure from storage will likely result in data corruption, rendering the structure unusable.
 *  See https://github.com/ethereum/solidity/pull/11843[ethereum/solidity#11843] for more info.
 *
 *  In order to clean an EnumerableSet, you can either remove all elements one by one or create a fresh instance using an array of EnumerableSet.
 * ====
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastValue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastValue;
                // Update the index for the moved value
                set._indexes[lastValue] = valueIndex; // Replace lastValue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/IAccessControl.sol)

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}
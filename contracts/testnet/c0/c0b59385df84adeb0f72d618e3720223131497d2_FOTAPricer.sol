// SPDX-License-Identifier: GPL

pragma solidity 0.8.0;

import "../libs/fota/Auth.sol";

contract PriceAuth is Auth {
  address normalPriceAdmin;
  address minMaxPriceAdmin;
  address absMinMaxPriceAdmin;

  function initialize(
    address _mainAdmin,
    address _normalPriceAdmin,
    address _minMaxPriceAdmin,
    address _absMinMaxPriceAdmin
  ) public {
    Auth.initialize(_mainAdmin);
    normalPriceAdmin = _normalPriceAdmin;
    minMaxPriceAdmin = _minMaxPriceAdmin;
    absMinMaxPriceAdmin = _absMinMaxPriceAdmin;
  }

  modifier onlyNormalPriceAdmin() {
    require(msg.sender == normalPriceAdmin || _isMainAdmin(), "onlyNormalPriceAdmin");
    _;
  }

  modifier onlyMinMaxPriceAdmin() {
    require(msg.sender == minMaxPriceAdmin || _isMainAdmin(), "onlyMinMaxPriceAdmin");
    _;
  }

  modifier onlyAbsMinMaxPriceAdmin() {
    require(msg.sender == absMinMaxPriceAdmin || _isMainAdmin(), "onlyAbsMinMaxPriceAdmin");
    _;
  }

  function updateNormalPriceAdmin(address _normalPriceAdmin) onlyMainAdmin external {
    require(_normalPriceAdmin != address(0), "Invalid address");
    normalPriceAdmin = _normalPriceAdmin;
  }

  function updateMinMaxPriceAdmin(address _minMaxPriceAdmin) onlyMainAdmin external {
    require(_minMaxPriceAdmin != address(0), "Invalid address");
    minMaxPriceAdmin = _minMaxPriceAdmin;
  }

  function updateAbsMinMaxPriceAdmin(address _absMinMaxPriceAdmin) onlyMainAdmin external {
    require(_absMinMaxPriceAdmin != address(0), "Invalid address");
    absMinMaxPriceAdmin = _absMinMaxPriceAdmin;
  }

  function getMainAdmin() external view returns (address) {
    return mainAdmin;
  }
}

contract FOTAPricer is PriceAuth {

  uint public fotaPrice; // decimal 3
  uint public minPrice;
  uint public maxPrice;
  uint public absMinPrice;
  uint public absMaxPrice;

  event FOTAPriceSynced(
    uint newPrice,
    uint timestamp
  );
  event Warning(
    uint fotaPrice,
    uint minPrice,
    uint maxPrice,
    uint absMinPrice,
    uint absMaxPrice,
    uint minPriceSet,
    uint maxPriceSet
  );

  function initialize(
    address _mainAdmin,
    address _normalPriceAdmin,
    address _minMaxPriceAdmin,
    address _absMinMaxPriceAdmin,
    uint _fotaPrice
  ) public initializer {
    PriceAuth.initialize(_mainAdmin, _normalPriceAdmin, _minMaxPriceAdmin, _absMinMaxPriceAdmin);
    fotaPrice = _fotaPrice;
    absMinPrice = _fotaPrice / 3;
    absMaxPrice = _fotaPrice * 3;
    _updateMinMaxPrice(_fotaPrice * 90 / 100, fotaPrice * 105 / 100);
  }

  function syncFOTAPrice(uint _fotaPrice) external onlyNormalPriceAdmin {
    require(
      _fotaPrice >= minPrice &&
      _fotaPrice <= maxPrice &&
      _fotaPrice >= absMinPrice &&
      _fotaPrice <= absMaxPrice, "Price is invalid");
    fotaPrice = _fotaPrice;
    emit FOTAPriceSynced(fotaPrice, block.timestamp);
  }

  function updateMinMaxPrice(uint _minPrice, uint _maxPrice) external onlyMinMaxPriceAdmin {
    _updateMinMaxPrice(_minPrice, _maxPrice);
  }

  function updateAbsMinMaxPrice(uint _absMinPrice, uint _absMaxPrice) external onlyAbsMinMaxPriceAdmin {
    require(_absMaxPrice > _absMinPrice, "Price is invalid");
    absMinPrice = _absMinPrice;
    absMaxPrice = _absMaxPrice;
  }

  function _updateMinMaxPrice(uint _minPrice, uint _maxPrice) private {
    if (_minPrice < absMinPrice || _maxPrice > absMaxPrice) {
      emit Warning(fotaPrice, minPrice, maxPrice, absMinPrice, absMaxPrice, _minPrice, _maxPrice);
    } else {
      minPrice = _minPrice;
      maxPrice = _maxPrice;
    }
  }
}

// SPDX-License-Identifier: MIT

pragma solidity 0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

abstract contract Auth is Initializable {

  address public mainAdmin;
  address public contractAdmin;

  event OwnershipTransferred(address indexed _previousOwner, address indexed _newOwner);
  event ContractAdminUpdated(address indexed _newOwner);

  function initialize(address _mainAdmin) virtual public initializer {
    mainAdmin = _mainAdmin;
    contractAdmin = _mainAdmin;
  }

  modifier onlyMainAdmin() {
    require(_isMainAdmin(), "onlyMainAdmin");
    _;
  }

  modifier onlyContractAdmin() {
    require(_isContractAdmin() || _isMainAdmin(), "onlyContractAdmin");
    _;
  }

  function transferOwnership(address _newOwner) onlyMainAdmin external {
    require(_newOwner != address(0x0));
    mainAdmin = _newOwner;
    emit OwnershipTransferred(msg.sender, _newOwner);
  }

  function updateContractAdmin(address _newAdmin) onlyMainAdmin external {
    require(_newAdmin != address(0x0));
    contractAdmin = _newAdmin;
    emit ContractAdminUpdated(_newAdmin);
  }

  function _isMainAdmin() public view returns (bool) {
    return msg.sender == mainAdmin;
  }

  function _isContractAdmin() public view returns (bool) {
    return msg.sender == contractAdmin;
  }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (proxy/utils/Initializable.sol)

pragma solidity ^0.8.0;

import "../../utils/AddressUpgradeable.sol";

abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        // If the contract is initializing we ignore whether _initialized is set in order to support multiple
        // inheritance patterns, but we only do this in the context of a constructor, because in other contexts the
        // contract may have been reentered.
        require(_initializing ? _isConstructor() : !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} modifier, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    function _isConstructor() private view returns (bool) {
        return !AddressUpgradeable.isContract(address(this));
    }
}
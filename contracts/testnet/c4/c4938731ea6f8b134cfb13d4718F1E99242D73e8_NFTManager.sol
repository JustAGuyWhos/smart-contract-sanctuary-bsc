// SPDX-License-Identifier: MIT
pragma solidity 0.7.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";

contract NFTManager is IERC721Receiver, Ownable {

    mapping(address => mapping (uint256 => address)) public ownerOf;
    mapping(address => bool) internal authorizations;
    mapping(address => mapping (address => uint256[])) public tokensOfOwner;

  	constructor() {
        authorizations[_msgSender()] = true;
    }

    /**
     * Always returns `IERC721Receiver.onERC721Received.selector`.
     */
    function onERC721Received(address, address, uint256, bytes memory) public virtual override returns (bytes4) {
        return this.onERC721Received.selector;
    }

    function depositNFT(address _contractAddress, uint256[] memory _tokenId) external {
        require(_contractAddress != address(0), "Address zero");
        require(_tokenId.length > 0, "Deposit amount must be more than 0");

        for(uint i=0; i<_tokenId.length; i++) {
            ownerOf[_contractAddress][_tokenId[i]] = msg.sender;
			IERC721(_contractAddress).safeTransferFrom(msg.sender, address(this), _tokenId[i]);
		}

        // Add _tokenId into tokensOfOwner
        uint256[] storage _tokensOfOwner = tokensOfOwner[_contractAddress][msg.sender];
        for(uint i=0; i<_tokenId.length; i++) {
            _tokensOfOwner.push(_tokenId[i]);
        }
        tokensOfOwner[_contractAddress][msg.sender] = _tokensOfOwner;

		emit DepositNFT(msg.sender, _contractAddress, _tokenId);
    }

    function withdrawNFT(address _contractAddress, uint256[] memory _tokenId) external {
        require(_contractAddress != address(0), "Address zero");
        require(_tokenId.length > 0, "Deposit amount must be more than 0");

        for(uint i=0; i<_tokenId.length; i++) {
            require(msg.sender == ownerOf[_contractAddress][_tokenId[i]], "Not NFT's owner");
            ownerOf[_contractAddress][_tokenId[i]] = address(0);
			IERC721(_contractAddress).safeTransferFrom(address(this), msg.sender, _tokenId[i]);
		}

        // Remove _tokenId from tokensOfOwner
        uint256[] storage _tokensOfOwner = tokensOfOwner[_contractAddress][msg.sender];
        for(uint i=0; i<_tokenId.length; i++) {
            // iterate _tokensOfOwner
            for(uint j=0; j<_tokensOfOwner.length; j++) {

                if(_tokenId[i] == _tokensOfOwner[j]) {

                    // move all elements to the left, starting from the `index + 1`
                    for (uint256 _index = j; _index < _tokensOfOwner.length - 1; _index++) {
                        _tokensOfOwner[_index] = _tokensOfOwner[_index+1];
                    }
                    _tokensOfOwner.pop(); // delete the last item
                    break;  // exit loop
                }
                    
            }
        }

        tokensOfOwner[_contractAddress][msg.sender] = _tokensOfOwner;

		emit WithdrawNFT(msg.sender, _contractAddress, _tokenId);
    }

    function rescueNFT(address _contractAddress, uint256[] memory _tokenId, address _to) external {
        require(isAuthorized(msg.sender), "SoldierNFT : UNAUTHORIZED");
        require(_contractAddress != address(0), "Contract address zero");
        require(_tokenId.length > 0, "Token ID must be larger than 0");
        require(_to != address(0), "User address zero");

        for(uint i=0; i<_tokenId.length; i++) {
            IERC721(_contractAddress).safeTransferFrom(address(this), _to, _tokenId[i]);
        }
		
		emit RescueNFT(_contractAddress, _tokenId, _to);
    }

    function authorize(address _userAddress, bool _authorize) external {
        require(isAuthorized(msg.sender), "NFTManager : UNAUTHORIZED");
        authorizations[_userAddress] = _authorize;

        emit Authorize(_userAddress, _authorize);
    }

    function isAuthorized(address _userAddress) public view returns (bool) {
        return authorizations[_userAddress];
    }

    function balanceOf(address _contractAddress, address _userAddress) external view returns (uint256) {
        return tokensOfOwner[_contractAddress][_userAddress].length;
    }

    event DepositNFT(address userAddress, address nftContractAddress, uint256[] tokenId);
	event WithdrawNFT(address userAddress, address nftContractAddress, uint256[] tokenId);
    event RescueNFT(address contractAddress, uint256[] tokenId, address to);
    event Authorize(address addr, bool authorize);
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with GSN meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address payable) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721Receiver {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721.onERC721Received.selector`.
     */
    function onERC721Received(address operator, address from, uint256 tokenId, bytes calldata data) external returns (bytes4);
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.2 <0.8.0;

import "../../introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
      * @dev Safely transfers `tokenId` token from `from` to `to`.
      *
      * Requirements:
      *
      * - `from` cannot be the zero address.
      * - `to` cannot be the zero address.
      * - `tokenId` token must exist and be owned by `from`.
      * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
      * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
      *
      * Emits a {Transfer} event.
      */
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external;
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

import "../utils/Context.sol";
/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor () internal {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "./common/CeresStakingRewards.sol";
import "../interfaces/ICeresBank.sol";
import "../interfaces/IMintReceiver.sol";
import "../interfaces/IOracle.sol";
import "../interfaces/ICeresMiner.sol";
import "../library/CeresLibrary.sol";

contract CeresStakingCOL is CeresStakingRewards, IMintReceiver, Initializable {

    IERC20 internal _token;
    uint256 public mintPerShareStored;
    mapping(address => uint256) public userMintPerSharePaid;
    mapping(address => uint256) public mints;

    /* ---------- Events ---------- */
    event MintPaid(address indexed account, uint256 amount);

    modifier updateMint(address account){
        mints[account] = mintEarned(account);
        userMintPerSharePaid[account] = mintPerShareStored;
        _;
    }

    constructor(address _factory) CeresBase(_factory) {}

    function initialize(address _asc, address _crs, address _stakingToken, address _stakerBook) external initializer {
        asc = IERC20(_asc);
        crs = IERC20(_crs);
        _token = IERC20(_stakingToken);
        stakerBook = IStakerBook(_stakerBook);
    }

    /* ---------- Views ---------- */
    function token() public view override returns (address){
        return address(_token);
    }

    function mintEarned(address account) public view override returns (uint256) {
        return _shareBalances[account] * (mintPerShareStored - userMintPerSharePaid[account]) / SHARE_PRECISION + mints[account];
    }

    /* ---------- Functions ---------- */
    function stake(uint256 amount) public override nonReentrant updateReward(msg.sender) updateMint(msg.sender) {
        require(amount > 0, "CeresStaking: cannot stake 0");

        SafeERC20.safeTransferFrom(_token, msg.sender, address(this), amount);
        _stake(msg.sender, amount);
    }

    function withdraw(uint256 shareAmount) public override nonReentrant updateReward(msg.sender) updateMint(msg.sender) {
        require(shareAmount > 0 && shareAmount <= unlockedShareBalanceOf(msg.sender), "CeresStaking: Your unlocked share balance is not enough!");

        uint256 withdrawAmount = shareAmount * _totalStaking / _totalShare;
        _totalStaking -= withdrawAmount;

        _totalShare -= shareAmount;
        _shareBalances[msg.sender] -= shareAmount;

        SafeERC20.safeTransfer(_token, msg.sender, withdrawAmount);
        emit Withdrawn(msg.sender, shareAmount);
    }

    function reinvestMint() public override nonReentrant updateMint(msg.sender) {
        uint256 _mint = mints[msg.sender];
        if (_mint > 0) {
            mints[msg.sender] = 0;
            address stakingASC = factory.getStaking(address(asc));
            SafeERC20.safeTransfer(asc, stakingASC, _mint);
            IReinvestReceiver(stakingASC).notifyReinvest(msg.sender, _mint);
        }
    }

    function reinvestAll() external {
        reinvestReward();
        reinvestMint();
    }

    /* ---------- Miner ---------- */
    function mint(uint256 amountIn) external {
        address bank = factory.ceresBank();
        require(amountIn <= _totalStaking, "CeresStaking: amountIn must be less than the staking balance.");
        require(userStakingRatio(msg.sender) > ICeresBank(bank).minMinerStakingRatio(),
            "CeresStaking: Miner staking ratio is not enough!");

        SafeERC20.safeTransfer(_token, bank, amountIn);
        ICeresMiner(factory.ceresMiner()).mint(token(), msg.sender, amountIn);
    }

    /* ---------- Notifies ---------- */
    function notifyMint(uint256 amountMint, uint256 amountLocked) public override onlyCeresMiner {

        uint256 leftStaking = _totalStaking - amountLocked;
        require(_token.balanceOf(address(this)) >= leftStaking, "CeresStaking: Staking left balance is not enough!");

        if (amountLocked > 0) {
            uint256 leftRatio = leftStaking * SHARE_PRECISION / _totalStaking;
            uint256 newUnusedRatio = shareUnusedRatio * leftRatio / SHARE_PRECISION;
            if (newUnusedRatio > 0)
                shareUnusedRatio = newUnusedRatio;

            _totalStaking = leftStaking;
        }

        if (amountMint > 0)
            mintPerShareStored += amountMint * SHARE_PRECISION / _totalShare;

    }

}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (proxy/utils/Initializable.sol)

pragma solidity ^0.8.0;

import "../../utils/AddressUpgradeable.sol";

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since proxied contracts do not make use of a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To initialize the implementation contract, you can either invoke the
 * initializer manually, or you can include a constructor to automatically mark it as initialized when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() initializer {}
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        // If the contract is initializing we ignore whether _initialized is set in order to support multiple
        // inheritance patterns, but we only do this in the context of a constructor, because in other contexts the
        // contract may have been reentered.
        require(_initializing ? _isConstructor() : !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} modifier, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    function _isConstructor() private view returns (bool) {
        return !AddressUpgradeable.isContract(address(this));
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/math/Math.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import "../../interfaces/ICeresStaking.sol";
import "../../interfaces/IOracle.sol";
import "../../interfaces/ICeresReward.sol";
import "../../interfaces/IStakerBook.sol";
import "../../interfaces/IReinvestReceiver.sol";
import "../../common/CeresBase.sol";

abstract contract CeresStakingRewards is CeresBase, ICeresStaking, ReentrancyGuard {

    IERC20 public asc;
    IERC20 public crs;

    uint256 public override rewardRate;
    uint256 public override rewardsDuration;
    uint256 public override periodFinish;
    uint256 public lastUpdateTime;
    uint256 public rewardPerShareStored;
    uint256 public shareUnusedRatio = 1e18;
    uint256 internal _totalStaking;
    uint256 internal _totalShare;

    mapping(address => uint256) internal _shareBalances;
    mapping(address => uint256) public userRewardPerSharePaid;
    mapping(address => uint256) public rewards;
    mapping(address => UserLock) public userLocks;

    // TODO k: only in testnet  
    IStakerBook public stakerBook;

    /* ---------- Events ---------- */
    event RewardAdded(uint256 amount);
    event Staked(address indexed user, uint256 amount);
    event Withdrawn(address indexed user, uint256 amount);
    event RewardPaid(address indexed user, uint256 percent);
    event RewardsDurationUpdated(uint256 newDuration);

    modifier updateReward(address account) {
        rewardPerShareStored = rewardPerShare();
        lastUpdateTime = lastTimeRewardApplicable();
        if (account != address(0)) {
            rewards[account] = earned(account);
            userRewardPerSharePaid[account] = rewardPerShareStored;
        }
        _;
    }

    /* ---------- Views ---------- */
    function token() public view override virtual returns (address);

    function totalStaking() public view override returns (uint256) {
        return _totalStaking;
    }

    function stakingBalanceOf(address account) public view override returns (uint256) {
        if (_totalShare > 0)
            return shareBalanceOf(account) * _totalStaking / _totalShare;
        else
            return 0;
    }

    function totalShare() public view override returns (uint256) {
        return _totalShare;
    }

    function shareBalanceOf(address account) public view override returns (uint256) {
        return _shareBalances[account];
    }

    function unlockedShareBalanceOf(address account) public view override returns (uint256) {

        if (userLocks[account].timeEnd > block.timestamp)
            return _shareBalances[account] - userLocks[account].shareAmount;
        else
            return _shareBalances[account];
    }

    function unlockedStakingBalanceOf(address account) public view override returns (uint256) {
        uint256 unlockedShare = unlockedShareBalanceOf(account);
        if (unlockedShare > 0)
            return unlockedShare * _totalStaking / _totalShare;
        else
            return 0;
    }

    function userStakingRatio(address account) public view override returns (uint256){
        return _shareBalances[account] * CERES_PRECISION / _totalShare;
    }

    function lastTimeRewardApplicable() public view returns (uint256) {
        return Math.min(block.timestamp, periodFinish);
    }

    function rewardPerShare() public view returns (uint256) {
        if (_totalShare == 0) {
            return rewardPerShareStored;
        }
        return rewardPerShareStored + ((lastTimeRewardApplicable() - lastUpdateTime) * rewardRate * SHARE_PRECISION / _totalShare);
    }

    function earned(address account) public view override returns (uint256) {
        return _shareBalances[account] * (rewardPerShare() - userRewardPerSharePaid[account]) / SHARE_PRECISION + rewards[account];
    }

    function getRewardForDuration() external view returns (uint256) {
        return rewardRate * rewardsDuration;
    }

    // TODO k: off chain
    function yieldAPR() public view override returns (uint256){

        if (_totalStaking == 0 || rewardRate == 0)
            return 999999999;

        uint256 missingDecimals = uint256(18 - IERC20Metadata(token()).decimals());
        uint256 apr = rewardRate * 365 days * factory.getTokenPrice(address(crs)) * CERES_PRECISION
        / _totalStaking / factory.getTokenPrice(token());

        return missingDecimals > 0 ? apr / 10 ** missingDecimals : apr;
    }

    function value() external view override returns (uint256){
        return _totalStaking * factory.getTokenPrice(token()) / 10 ** IERC20Metadata(token()).decimals();
    }

    /* ---------- Functions ---------- */
    function _stake(address account, uint256 amount) internal returns (uint256){
        // stake increase
        _totalStaking += amount;

        // update lock
        uint256 userShare = amount * SHARE_PRECISION / shareUnusedRatio;
        _updateLock(account, userShare);

        // share increase
        _totalShare += userShare;
        _shareBalances[account] += userShare;

        stakerBook.stake(msg.sender);
        emit Staked(account, amount);

        return userShare;
    }

    function _updateLock(address account, uint256 shareAmount) internal {

        UserLock memory _userLock = userLocks[account];
        if (_userLock.timeEnd > 0 && _userLock.timeEnd <= block.timestamp)
            userLocks[account].shareAmount = shareAmount;
        else
            userLocks[account].shareAmount += shareAmount;

        userLocks[account].timeEnd = block.timestamp + ICeresReward(factory.ceresReward()).stakingLockTime();

        emit Staked(account, shareAmount);
    }

    function reinvestReward() public virtual override nonReentrant updateReward(msg.sender) {
        uint256 reward = rewards[msg.sender];
        if (reward > 0) {
            rewards[msg.sender] = 0;
            address stakingCRS = factory.getStaking(address(crs));
            if (stakingCRS != address(this))
                SafeERC20.safeTransfer(crs, stakingCRS, reward);
            IReinvestReceiver(stakingCRS).notifyReinvest(msg.sender, reward);
        }
    }

    function notifyReward(uint256 amount, uint256 duration) external override virtual updateReward(address(0)) {
        require(block.timestamp >= periodFinish, "Period is not finish yet.");

        rewardRate = amount / duration;

        require(rewardRate <= crs.balanceOf(address(this)) / duration, "Provided amount too high");

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + duration;

        rewardsDuration = duration;
        emit RewardAdded(amount);
    }

    function applyReward() external override {
        require(block.timestamp >= periodFinish, "Period is not finish yet.");

        ICeresReward _reward = ICeresReward(factory.ceresReward());
        require(_totalShare > 0, "Can not apply reward with no staking!");
        require(userStakingRatio(msg.sender) > _reward.minApplicantStakingRatio(), "Applicant staking ratio is not enough!");

        uint256 _bonus = _reward.applyReward(msg.sender);
        if (_bonus > 0)
            crs.transfer(msg.sender, _bonus);
    }

    function approveBank(uint256 amount) external onlyCeresBank override {
        IERC20(token()).approve(factory.ceresBank(), amount);
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface ICeresBank {
    struct MintResult {
        uint256 lockedCol;
        uint256 burnedCRS;
        uint256 lockedVol;
        uint256 mintToCol;
        uint256 mintToCRS;
        uint256 mintToVol;
        uint256 bonusToMiner;
        address volStaking;
    }

    /* views */
    function collateralRatio() external view returns(uint256);
    function minCR() external view returns(uint256);
    function nextUpdateTime(address staking) external view returns(uint256);
    function updateCoolDown() external view returns(uint256);
    function ratioStep() external view returns(uint256);
    function minMinerStakingRatio() external view returns(uint256);
    function minMintedASCAmount() external view returns(uint256);

    /* functions */
    function mintFromStaking(address _collateral, uint _collateralAmount, address staking) external returns(MintResult memory result);
    function redeem(uint ascAmount) external;
    function claimColFromStaking(uint256 colValueD18, address token) external returns(uint256);

    /* v2_5 new */
    function mintFromStaking(address _collateral) external returns(MintResult memory result);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface IMintReceiver {
    /* ---------- Views ---------- */
    function mintEarned(address account) external view returns (uint256);

    /* ---------- Functions ---------- */
    function notifyMint(uint256 amountMint, uint256 amountBurn) external;
    function reinvestMint() external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface IOracle {
    /* ---------- Views ---------- */
    function token() external view returns (address);
    function getPrice() external view returns (uint256);
    function updatable() external view returns (bool);
    function period() external view returns (uint256);

    /* ---------- Functions ---------- */
    function update() external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface ICeresMiner {

    function mint(address token, address account, uint256 amount) external;

}

// SPDX-License-Identifier: GPL-2.0-or-later
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";

library CeresLibrary {

    function toAmountD18(address _token, uint256 _amount) internal view returns (uint256) {
        return _amount * 10 ** (18 - IERC20Metadata(_token).decimals());
    }

    function toAmountActual(address _token, uint256 _amountD18) internal view returns (uint256) {
        return _amountD18 / 10 ** (18 - IERC20Metadata(_token).decimals());
    }
    
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/math/Math.sol)

pragma solidity ^0.8.0;

/**
 * @dev Standard math utilities missing in the Solidity language.
 */
library Math {
    /**
     * @dev Returns the largest of two numbers.
     */
    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a >= b ? a : b;
    }

    /**
     * @dev Returns the smallest of two numbers.
     */
    function min(uint256 a, uint256 b) internal pure returns (uint256) {
        return a < b ? a : b;
    }

    /**
     * @dev Returns the average of two numbers. The result is rounded towards
     * zero.
     */
    function average(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b) / 2 can overflow.
        return (a & b) + (a ^ b) / 2;
    }

    /**
     * @dev Returns the ceiling of the division of two numbers.
     *
     * This differs from standard division with `/` in that it rounds up instead
     * of rounding down.
     */
    function ceilDiv(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b - 1) / b can overflow on addition, so we distribute.
        return a / b + (a % b == 0 ? 0 : 1);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface ICeresStaking {
    struct UserLock {
        uint256 shareAmount;
        uint256 timeEnd;
    }

    /* ---------- Views ---------- */
    function token() external view returns (address);
    function totalStaking() external view returns (uint256);
    function stakingBalanceOf(address account) external view returns (uint256);
    function totalShare() external view returns (uint256);
    function shareBalanceOf(address account) external view returns (uint256);
    function unlockedShareBalanceOf(address account) external view returns (uint256);
    function unlockedStakingBalanceOf(address account) external view returns (uint256);
    function userStakingRatio(address account) external view returns (uint256);
    function earned(address account) external view returns (uint256);
    function rewardRate() external view returns (uint256);
    function rewardsDuration() external view returns (uint256);
    function periodFinish() external view returns (uint256);
    function yieldAPR() external view returns (uint256);
    function value() external view returns (uint256);

    /* ---------- Functions ---------- */
    function stake(uint256 amount) external;
    function withdraw(uint256 shareAmount) external;
    function reinvestReward() external;
    function applyReward() external;
    function notifyReward(uint256 amount, uint256 duration) external;
    function approveBank(uint256 amount) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface ICeresReward {
    struct StakingRewardConfig {
        uint256 amount;
        uint256 duration;
    }

    /* ---------- Views ---------- */
    function getAppliedStakings() external view returns (address[] memory);
    function getAppliedStakingsLength() external view returns (uint256);
    function stakingLockTime() external view returns (uint256);
    function minStakingValue() external view returns (uint256);
    function minApplicantStakingRatio() external view returns (uint256);

    /* ---------- Functions ---------- */
    function applyReward(address account) external returns (uint256);

}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface IStakerBook {
    /* ---------- Functions ---------- */
    function stake(address staker) external;
    function refer(address staker, address referer) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface IReinvestReceiver {
    /* ---------- Notifies ---------- */
    function notifyReinvest(address account, uint256 amount) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "../interfaces/ICeresFactory.sol";

contract CeresBase {

    uint256 public constant CERES_PRECISION = 1e6;
    uint256 public constant SHARE_PRECISION = 1e18;

    ICeresFactory public factory;

    modifier onlyCeresBank() {
        require(msg.sender == factory.ceresBank(), "Only CeresBank!");
        _;
    }

    modifier onlyCeresStaking() {
        require(factory.isStaking(msg.sender) == true, "Only CeresStaking!");
        _;
    }

    modifier onlyCeresMiner() {
        require(msg.sender == factory.ceresMiner(), "Only CeresMiner!");
        _;
    }

    modifier onlyOwnerOrGovernance() {
        require(msg.sender == factory.owner() || msg.sender == factory.governorTimelock(),
            "Only owner or governor timelock!");
        _;
    }

    constructor(address _factory) {
        factory = ICeresFactory(_factory);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface ICeresFactory {
    struct TokenInfo {
        address token;
        uint256 tokenType; // 1: asc, 2: crs, 3: col, 4: vol;
        address staking;
        address priceFeed;
        bool isChainlinkFeed;
        bool isStakingRewards;
        bool isStakingMineable;
    }

    /* ---------- Views ---------- */
    function ceresBank() external view returns (address);
    function ceresReward() external view returns (address);
    function ceresMiner() external view returns (address);
    function getTokenInfo(address token) external returns(TokenInfo memory);
    function getStaking(address token) external view returns (address);
    function getPriceFeed(address token) external view returns (address);
    function isStaking(address sender) external view returns (bool);
    function volTokens(uint256 index) external view returns (address);
    function owner() external view returns (address);
    function governorTimelock() external view returns (address);

    function getTokens() external view returns (address[] memory);
    function getTokensLength() external view returns (uint256);
    function getVolTokensLength() external view returns (uint256);
    function getTokenPrice(address token) external view returns(uint256);
    function isChainlinkFeed(address token) external view returns (bool);
    function isStakingRewards(address staking) external view returns (bool);
    function isStakingMineable(address staking) external view returns (bool);
    function oraclePeriod() external view returns (uint256);
    
    /* ---------- Public Functions ---------- */
    function updateOracles(address[] memory tokens) external;
    function updateOracle(address token) external;
    function addStaking(address token, uint256 tokenType, address staking, address oracle, bool _isStakingRewards, bool _isStakingMineable) external;
    function removeStaking(address token, address staking) external;
    /* ---------- Setting Functions ---------- */
    function setCeresBank(address _ceresBank) external;
    function setCeresReward(address _ceresReward) external;
    function setCeresMiner(address _ceresMiner) external;
    function setCeresCreator(address _ceresCreator) external;
    function setTokenType(address token, uint256 tokenType) external;
    function setStaking(address token, address staking) external;
    function setIsStakingRewards(address token, bool _isStakingRewards) external;
    function setIsStakingMineable(address token, bool _isStakingMineable) external;
    /* ---------- RRA ---------- */
    function createStaking(address token, address chainlinkFeed, address quoteToken) external returns (address staking);
    function createOracle(address token, address quoteToken) external returns (address);
}